SUBROUTINE ACMTUD ( KIDIA,KFDIA,KLON,KTDIA,KLEV,KTRA,&
!-----------------------------------------------------------------------
! - INPUT  2D .
& PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PLH,PR,&
& PDELP,PLNPR,&
& PQ,PQI,PQL,PQLIS,PQSAT,PQW,&
& PRDELP,PCVGQ,PT,PTW,PU,PV,PTKE,PTRA,PEVAPO,&
! - INPUT 1D .
& PGM, PTS, PQS,&
! - OUTPUT 2D .
& PDIFCQ,PDIFCQL,PDIFCQI,PDIFCS,PDIFCTH,&
& PFCCQL,PFCCQI,PSTRCU,PSTRCV,PSTRCTRA,&
& PFHMLTS,PFHEVPP,PFPEVPCL,PFPEVPCN,&
& PFPLCL,PFPLCN,PMF_UP,&
& PNEBC,PQLIC,PTU,PQU,PSU,KNLAB,PENTR_U,PDETR_U,PENTR_D,PDETR_D,&
! - OUTPUT 1D .
& KNND,PCAPE,PALF,PALF_CAPE,PALF_CVGQ,&
! - INPUT/OUTPUT 2D .
& PUDAL,PUDOM,PDDAL,PDDOM)

!**** *ACMTUD * - MASS FLUX PROGNOSTIC UPDRAFT OF THE PCMT CONVECTION SCHEME.

! SUJET.
!     ------

!**   INTERFACE.
!     ----------
!        *CALL* *ACMTUD*
!-----------------------------------------------------------------------
! -   INPUT ARGUMENTS.
!     ----------------

! - PHYSICS DIMENSIONNING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KTRA       : NUMBER OF TRACERS (PASSIVE VARIABLES)

! - PHYSICS VARIABLES (ALPHABETICALLY IN EACH CATEGORY)

! - 2D (0:KLEV) .

! PAPHI      : HALF LEVEL GEOPOTENTIAL
! PAPRS      : HALF LEVEL PRESSURE

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : FULL LEVEL GEOPOTENTIAL
! PAPRSF     : FULL LEVEL PRESSURE
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PR         : CONSTANTE DE L'AIR HUMIDE.
! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : MICROPHYSICAL WATER VAPOUR SPECIFIC HUMIDITY
! PQI        : MICROPHYSICAL RESOLVED CLOUD ICE SPECIFIC CONTENTS
! PQL        : MICROPHYSICAL RESOLVED CLOUD LIQUID SPECIFIC CONTENTS
! PQLIS      : MICROPHYSICAL RESOLVED CLOUD WATER SPECIFIC CONTENTSIN THE TIME-STEP
! PQW        : WET BULB SPECIFIC MOISTURE
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PCVGQ      : CONVERGENCE D'HUMIDITE.
! PT         : TEMPERATURE.
! PTW        : WET BULB TEMPERATURE.
! PU         : U WIND COMPONENT
! PV         : V WIND COMPONENT
! PTRA       : TRACER CONCENTRATION
! PTKE       : TKE
! PEVAPO     : evaporation from previous time step

! - 2D (0:KLEV) .

! - 1D .

! PGM        : FACTEUR D'ECHELLE.
! PTS        : temperature de surface.
! PQS        : vapeur d'eau de surface.

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PDIFCQ     : WATER VAPOUR CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQI    : ICE WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQL    : LIQUID WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCS     : DRY STATIC ENERGY CONVECTIVE FLUX
! PDIFCTH    : THETA CONVECTIVE FLUX
! PFCCQL     : LIQUID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PFCCQI     : SOLID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PSTRCU     : U-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCV     : V-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCTRA   : TRACER CONVECTIVE FLUX
! PFHMLTS    : FLUX DE CHALEUR DU A LA FONTE/GEL.
! PFHEVPP    : FLUX DE CHALEUR DU A L'EVAPORATION DES PRECIPITATIONS.
! PFPEVPCL    : EVAPORATION DES PRECIPITATIONS LIQUIDES.
! PFPEVPCN    : EVAPORATION DES PRECIPITATIONS NEIGEUSES.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.

! - 2D (1:KLEV) .

! KNLAB  : CONVECTIVE ACTIVITY INDEX (ACTIVE IF EQUAL TO ONE).
! PTU    : TEMPERATURE OF THE CLOUDY ASCENT.
! PQU    : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! PSU    : DRY STATIC ENERGY OF THE CLOUDY ASCENT.
! PNEBC  : CONVECTIVE CLOUDINESS.
! PQLIC  : CONVECTIVE CONDENSATE.
! PENTR_U    : ENTRAINMENT IN UPDRAFT (IN S**-1).
! PDETR_U    : DETRAINMENT FROM UPDRAFT (IN S**-1).
! PENTR_D    : ENTRAINMENT IN DOWNDRAFT (IN S**-1).
! PDETR_D    : DETRAINMENT FROM DOWNDRAFT (IN S**-1).

! - 1D (DIAGNOSTIQUE) .

! KNND   : ZERO WHENEVER A NON PHYSICAL SITUATION OCCURS, ONE OTHERWISE
! PCAPE  : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).

!-----------------------------------------------------------------------

! -   INPUT/OUTPUT ARGUMENTS
!     ----------------------

! - 2D (0:KLEV) .

! - 2D (1:KLEV) .

! PUDAL    : PROGNOSTIC UPDRAFT MESH FRACTION
! PUDOM    : PROGNOSTIC UPDRAFT RELATIVE VELOCITY

!-----------------------------------------------------------------------

! METHOD.
!     --------

! AUTHOR.
!     -------
! 2011-04-14, J.F. GUEREMY AND J.M. PIRIOU.

! MODIFICATIONS.
!     --------------
! 2013-02-08, J.M. Piriou: protect the LOG in computing ZSIGMA.
! 2013-06-17, J.M. Piriou: LCVNHD NH effects of downdraft on updraft and conversely.
! 2013-06-17, J.M. Piriou: prognostic closure on PALF.
! 2013-07 - G. Bellon  Modifications for MUSC 
! 2014-04-01, J.M. Piriou: LLSINH: SImple NH effect.
! 2014-05   , J.F. Gueremy:
!             * resolution function for closure condition.
!             * CIN threshold for triggering condition.
! 2014-05   , J.F. Gueremy: use ZDQCA instead of ZBCC to compute condensation,
!             to apply the same "ZAUX" reduction in condensation and in transport.
! 2014-05 - D. Saint-Martin : Transport of tracers
! 2014-06 - J.F. Gueremy condensation flux using the same massflux
!             as for the transport flux
! 2014-07-29, Ryad El Khatib: optimize computation time: replace scalars by arrays, etc.
! 2014-09 - J.F. Gueremy new formulation of the resolution function for closure
!             condition and no more smoothing of dw/dp (ZALFAVEC=1.)
! 2015-02 -  I. Beau: Bugfix + some cleanings consistent with cy38 (NCVENTR options)
! 2015-09- J.M Piriou: Phasing of NWP and Climate versions
! 2015-11 - J.F. Gueremy: bugfix concerning the "mass flux" for implicit formulation
! 2015-11 - J.F. Gueremy: explicit transport flux for the dry air potential temperature
!             smoothing of mass flux for the transport fluxes
! 2015-12 - J.M. Piriou: GCVUV factor for momentum transport.
! 2016-06 - J.F. Gueremy et J.M. Piriou: debug sign when using ZFORD for ZAUX.
! 2017-04-21 - J.M. Piriou: use RNEBCX as a maximum value for convective cloudiness.
! 2017-05 - P. Marquet : NCAPEMOD WCAPEMOD RENTRMOD (YOMPHY0) "model-SBCAPE"

!-----------------------------------------------------------------------


USE PARKIND1, ONLY : JPIM, JPRB

USE YOMPHY   , ONLY : YRPHY
USE YOMCST   , ONLY : RATM       ,RG       ,RKAPPA   ,&
  & RD       ,RV       ,RCPD     ,&
  & RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
  & RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
  & RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
  & RGAMD    ,RPI      ,RA       ,&
  & RDT
USE YOMPHY0  , ONLY : YRPHY0
USE YOMPHY2  , ONLY : YRPHY2

USE YOMCT0   , ONLY : LELAM
USE YOMDIM   , ONLY : YRDIM
USE YEMGEO   , ONLY : YREGEO
USE YOMCT3   , ONLY : NSTEP
USE YOMSCM   , ONLY : NFRSCM
USE YOMRIP   , ONLY : YRRIP
USE YOMGEM   , ONLY : YRGEM

IMPLICIT NONE

!     DUMMY ARGUMENTS.

INTEGER(KIND=JPIM), INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KLEV
INTEGER(KIND=JPIM), INTENT(IN) :: KLON
INTEGER(KIND=JPIM), INTENT(IN) :: KTDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KTRA

REAL(KIND=JPRB), INTENT(IN)    :: PALPH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PR(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQI(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQLIS(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQW(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTRA(KLON,KLEV,KTRA)
REAL(KIND=JPRB), INTENT(IN)    :: PTKE(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PEVAPO(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PQS(KLON)

INTEGER(KIND=JPIM), INTENT(INOUT) :: KNLAB(KLON,KLEV)
INTEGER(KIND=JPIM), INTENT(INOUT) :: KNND(KLON)
REAL(KIND=JPRB)   , INTENT(INOUT) :: PALF(KLON)

REAL(KIND=JPRB), INTENT(OUT)   :: PCAPE(KLON)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQ(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCTH(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCU(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCV(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCTRA(KLON,0:KLEV,KTRA)
REAL(KIND=JPRB), INTENT(OUT)   :: PMF_UP(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PTU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PSU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PNEBC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQLIC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFHMLTS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PENTR_U(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PDETR_U(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PENTR_D(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PDETR_D(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCN(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCN(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFHEVPP(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CAPE(KLON)
REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CVGQ(KLON)

REAL(KIND=JPRB), INTENT(INOUT) :: PUDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PUDOM(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDOM(KLON,KLEV)

! LOCALS.

REAL(KIND=JPRB) :: ZWU(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZWD(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZKEDD(KLON)
REAL(KIND=JPRB) :: ZWD_PLUS_BUO(KLON,KLEV)

REAL(KIND=JPRB) :: ZTALF_SCE(KLON)
REAL(KIND=JPRB) :: ZTALF_SIN(KLON)

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
  &,ZBET(KLON,KLEV)&
  &,ZDSE(KLON,KLEV)&
  &,ZIFRAC(KLON,KLEV)&
  &,ZLDN(KLON,KLEV),ZPOID(KLON,KLEV)&
  &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV)&
  &,ZTVE(KLON,KLEV),ZTVN(KLON,KLEV)&
  &,ZUM(KLON,KLEV),ZVM(KLON,KLEV),ZQBW(KLON),ZTBW(KLON)&
  &,ZFORM(KLON,0:KLEV),ZDQCA(KLON,0:KLEV)

INTEGER(KIND=JPIM) :: JTRA
REAL(KIND=JPRB) :: ZA15(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZTRAM(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZTRAMD(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZSTRCTRAD(KLON,0:KLEV,KTRA)

INTEGER(KIND=JPIM) :: INBAS(KLON,KLEV)
REAL(KIND=JPRB) :: ZCP(KLON)&
  &,ZLB(KLON),ZLH(KLON),ZQB(KLON)&
  &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
  &,ZS15(KLON),ZS16(KLON)&
  &,ZTB(KLON)


INTEGER(KIND=JPIM) :: ISUM, ITOP, JIT, JLEV, JLON

REAL(KIND=JPRB) :: ZARLII,ZBAS, &
  &ZCPSMD, ZCPVMD, &
  &ZCPVMS, ZCPVMW, ZCPWMD, ZDCP, ZDELQ, &
  &ZDELT, ZDELTA, ZDPSGDT, ZDQW, &
  &ZFER, ZEPS16, &
  &ZESP, ZEW, ZEXP, &
  &ZAUX, ZGDT, ZGDTI, &
  &ZLIQ, ZMIX, ZNBA, ZQW, &
  &ZRVMD, ZZ, ZZVAL, &
  &ZB, ZLHTBW, &
  &ZIDT, ZTPHY, &
  &ZINCRQ,ZCPBW, &
  &ZEPSW,ZWEIGHT,ZEPS_BCCS
INTEGER(KIND=JPIM) :: ILEV
REAL(KIND=JPRB) :: ZBETA1RIO,ZA1RIO,ZBRIO,ZCRIO

REAL(KIND=JPRB) :: ZLN(KLON),ZTNH(KLON),ZQNH(KLON),ZVVER(KLON,0:KLEV),ZDWNDP(KLON,KLEV)
REAL(KIND=JPRB) :: ZCAPE(KLON),ZEPS_TUR(KLON,KLEV),ZEPS_ORG(KLON,KLEV),ZDEL_ORG(KLON,KLEV)
REAL(KIND=JPRB) :: ZKD(KLON,KLEV),ZSIGMA(KLON,0:KLEV),ZTBH(KLON),ZQBH(KLON)
REAL(KIND=JPRB) :: ZRBBS(KLON),ZRBHS(KLON),ZCPH,ZTNSEC,ZTVNL,ZTVEL,ZCAPEL(KLON)
REAL(KIND=JPRB) :: ZCIN(KLON),ZCINL(KLON)
REAL(KIND=JPRB) :: ZBUO_W(KLON,KLEV),ZFLO_OMEGA,ZDELPF,ZENTR_TUR,ZKDL,ZVVERDEN,ZVVERDM(KLON,KLEV)
REAL(KIND=JPRB) :: ZBUO_W_POS(KLON,KLEV)
REAL(KIND=JPRB) :: ZDELTAP,ZVVERN,ZEPS_TURL,ZKDX,ZMOD(KLON,KLEV),ZPPS,ZMOD_MIN,ZMOD_MAX
REAL(KIND=JPRB) :: ZENTRO,ZEPS_ORG0,ZTLN,ZTLE,ZQSTLE,ZQSTLN,ZSN,ZSE,ZCHIS
REAL(KIND=JPRB) :: ZTVLN,ZTVLE,ZNCHI0,ZDCHI0,ZEPS0,ZEPSVVER,ZCHI0,ZCHI02,ZDLOGM,ZFMDQN
REAL(KIND=JPRB) :: ZFMDSN,ZFMDS(KLON),ZFMDQ(KLON),ZDPLAB,ZS12(KLON),ZS11(KLON),ZTAU(KLON)
REAL(KIND=JPRB) :: ZDLON,ZDLONL,ZFDXTAUX(KLON),ZFORMV,ZTAU_DIAG(KLON)
REAL(KIND=JPRB) :: ZTD,ZQDN(KLON,KLEV),ZBUOY(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIVERG,ZBIN,ZTKLEV,ZTBASE(KLON),ZQBASE(KLON)
REAL(KIND=JPRB) :: ZFLUXDIAG(KLON,KLEV),ZENTR_T_LOC(KLON,KLEV),ZEMD_DIAG(KLON,KLEV)
REAL(KIND=JPRB) :: ZEPSILON_U(KLON,KLEV),ZDELTA_U(KLON,KLEV)
REAL(KIND=JPRB) :: ZDEPTH(KLON),ZVACC(KLON,KLEV)
REAL(KIND=JPRB) :: ZASCENT_BASE(KLON)
REAL(KIND=JPRB) :: ZDIAGFLO(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_ECART_TV(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_ECART_T(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAGFRICTION(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAGTRANSP(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_EPS_ORG(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_EPS_TUR(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_WMAX(KLON)
REAL(KIND=JPRB) :: ZDIAG_TUDAL(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_KNLAB(KLON,KLEV)

INTEGER(KIND=JPIM) :: ICDN(KLON),INCDN(KLON,KLEV),INIVBAC(KLON,KLEV),IBAC(KLON), ISDELTAP
INTEGER(KIND=JPIM) :: IVVER,IDOMDP,IVVN,IVVX,INUA,ICHIS,IENTR,IKUO6(KLON),IQLIC(KLON)
INTEGER(KIND=JPIM) :: IKUO5(KLON),IE0,ILEVTEN(KLON),INLFC(KLON,KLEV),ILFC(KLON),ICIN

REAL(KIND=JPRB) :: ZSU(KLON,KLEV)
REAL(KIND=JPRB) :: ZTU(KLON,KLEV)
REAL(KIND=JPRB) :: ZQU(KLON,KLEV)
REAL(KIND=JPRB) :: ZS5(KLON)
REAL(KIND=JPRB) :: ZEPS3
REAL(KIND=JPRB) :: ZE,ZR,ZTCOND,ZTEL(KLON),ZTEN(KLON)
REAL(KIND=JPRB) :: ZBETD(KLON,KLEV),ZDEL_ORGD(KLON,KLEV),ZEPS_ORGD(KLON,KLEV)
REAL(KIND=JPRB) :: ZQN2D(KLON,KLEV),ZSDND(KLON,KLEV),ZSN2D(KLON,KLEV),ZTN2D(KLON,KLEV)
REAL(KIND=JPRB) :: ZFORU(KLON,0:KLEV),ZFORD(KLON,0:KLEV),ZQDND(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC_C_HALF(KLON,0:KLEV),ZUMD(KLON,KLEV),ZVMD(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC_C_FULL(KLON,KLEV),ZBCC_CD_FULL(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC(KLON,KLEV)
REAL(KIND=JPRB) :: ZCCHSL,ZCCHSN,ZRHO(KLON,KLEV)
REAL(KIND=JPRB) :: ZT_DOWN(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIFF_Z_UD(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZTWU_ADV(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_BUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_DYP(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_NHD(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_FRI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_TOT_RAW(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_TOT(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_STAU(KLON,KLEV)

REAL(KIND=JPRB) :: ZTWD_ADV(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_BUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_DYP(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_NHD(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_FRI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_TOT_RAW(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_TOT(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_STAU(KLON,KLEV)

REAL(KIND=JPRB) :: ZMEAN_U(KLON,KLEV)
REAL(KIND=JPRB) :: ZMEAN_D(KLON,KLEV)
REAL(KIND=JPRB) :: ZINTEG(KLON,KLEV)
REAL(KIND=JPRB) :: ZTARGET_WD(KLON,KLEV)
REAL(KIND=JPRB) :: ZPLH(KLON),ZRME(KLON)
REAL(KIND=JPRB) :: ZEPS1,ZEPS4
REAL(KIND=JPRB) :: ZDIFCS_DD_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDIFCS_UD_NOCORR_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDIFCS_DD_NOCORR_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZRE(KLON,KLEV) ! Reduce Entrainment factor, total.
REAL(KIND=JPRB) :: ZRE_SATDEF(KLON,KLEV) ! Reduce Entrainment factor, due to saturation deficit.
REAL(KIND=JPRB) :: ZRE_HEIGHT(KLON,KLEV)
REAL(KIND=JPRB) :: ZRE_KEDD(KLON,KLEV)
REAL(KIND=JPRB) :: ZSATDEF(KLON),ZDENO(KLON),ZINTE(KLON),ZRH(KLON)
REAL(KIND=JPRB) :: ZBINID(KLON,KLEV),ZBINID2(KLON,KLEV),ZRT,ZIWGREAT
REAL(KIND=JPRB) :: ZBINTKE,ZBINDEPTH,ZHEIGHT,ZBINBUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIFCQD(KLON,0:KLEV),ZDIFCSD(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZI1(KLON,KLEV),ZNND(KLON),ZSTRCUD(KLON,0:KLEV),ZSTRCVD(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZPHIF_U_EQUIP(KLON,KLEV+1) ! geopotential at full levels within updraft, under the equipressure hypothesis.
REAL(KIND=JPRB) :: ZPHIF_U(KLON,KLEV+1) ! geopotential at full levels within updraft.
REAL(KIND=JPRB) :: ZEVAPO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTMPVAR(KLON,KLEV)
REAL(KIND=JPRB) :: ZLISS(KLON,KLEV)
REAL(KIND=JPRB) :: ZRHOH(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZLOGRATIO(KLON), ZALFAVEC(KLON)
REAL(KIND=JPRB) :: ZTLE_ARR(KLON), ZTLN_ARR(KLON), ZEW_TLE(KLON), ZEW_TLN(KLON)
REAL(KIND=JPRB) :: ZLOGSIGMA(KLON), ZDELTA_ARR(KLON)
REAL(KIND=JPRB) :: ZCVGQ(KLON),ZIBCCS(KLON),ZALF_CVGQ(KLON),ZPREVIOUST(KLON),ZT(KLON,KLEV)
REAL(KIND=JPRB) :: ZALFX(KLON)
REAL(KIND=JPRB) :: ZA,ZA2
REAL(KIND=JPRB) :: ZC
REAL(KIND=JPRB) :: ZD
REAL(KIND=JPRB) :: ZWNEW
REAL(KIND=JPRB) :: ZENTR
REAL(KIND=JPRB) :: ZENTRX
REAL(KIND=JPRB) :: ZRESOL
REAL(KIND=JPRB) :: ZNEBCX
! Dummy setup for CALL FPCINCAPE
INTEGER(KIND=JPIM) :: ILCL(KLON),IFCL(KLON),IEL(KLON)

INTEGER(KIND=JPIM) :: INLABD(KLON,KLEV)
LOGICAL :: LLTEXC,LLNHACC,LLCOND1,LLCAPECIN

!-----------------------------------------------------------------------

#include "acmtddd.intfb.h"
#include "fcttrm.func.h"
#include "fctdoi.func.h"
#include "fpcincape.intfb.h"



DO JLEV = 1, KLEV+1
DO JLON = KIDIA, KFDIA
ZWU(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV+1
DO JLON = KIDIA, KFDIA
ZWD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

LLCAPECIN=.FALSE.
DO JLON=KIDIA,KFDIA
  ZPREVIOUST(JLON)=PT(JLON,KTDIA)
ENDDO
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA

    ! Limit T value, so that no instability can be greater than GINSTX Kelvin per level.
    ! This is done in order to prevent temporal instabilities generated by too large time
    ! steps versus vertical resolution, close to orography.
    ZT(JLON,JLEV)=MIN(PT(JLON,JLEV),ZPREVIOUST(JLON)+YRPHY0%GINSTX)
    ZPREVIOUST(JLON)=ZT(JLON,JLEV)

    ! VERTICAL VELOCITY IN M/S COMPUTED FROM VERTICAL VELOCITY IN PA/S.
    ZWU(JLON,JLEV)=-PUDOM(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
    ZWD(JLON,JLEV)=-PDDOM(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
  ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
ZKEDD(JLON) = 0._JPRB
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ! Kinetic energy of the downdraft.
    ZKEDD(JLON)=ZKEDD(JLON)+0.5_JPRB*ZWD(JLON,JLEV)*ZWD(JLON,JLEV)*PDELP(JLON,JLEV)/RG
  ENDDO
ENDDO

!     -------------------
! // INITIALISATIONS

IF(YRPHY0%NCVCLOS == 4) THEN
  !
  !-------------------------------------------------
  ! Surface fraction PALF is prognostic.
  ! In input, PUDAL is the result of 3D advection applied to the uniform in the
  ! vertical PALF field from previous time step.
  ! PUDAL is set in output to ZSIGMA*PALF.
  ! PALF is set in output to the new prognostic value of PALF, and will be set
  ! by the caller of present routine ACMTUD in the array PUDAL, in order to be
  ! advected in 3D.
  ! Here PALF is set to the value of the 3D field PUDAL above PBL.
  ! In 1D, as there is no advection, PUDAL is nothing but the uniform value of PALF from previous time
  ! step.
  !-------------------------------------------------
  !
  ILEV=MAX(KTDIA,INT(YRPHY0%GSIGA*REAL(KLEV)))
  DO JLON=KIDIA,KFDIA
    PALF(JLON)=PUDAL(JLON,ILEV)
  ENDDO
ELSE
  !-------------------------------------------------
  ! Surface fraction PALF is diagnostic.
  !-------------------------------------------------
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  PUDAL(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

ENDIF
DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
PDDAL(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZEPS_TUR(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZEPS_ORG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZEMD_DIAG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFCQD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFCSD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZSTRCUD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZSTRCVD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZVACC(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

ZENTR=YRPHY0%GCVRE*YRPHY0%TENTR
ZENTRX=YRPHY0%GCVRE*YRPHY0%TENTRX
DO JTRA = 1, KTRA
DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZSTRCTRAD(JLON,JLEV,JTRA) = 0._JPRB
ENDDO
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZEPSILON_U(JLON,JLEV) = ZENTRX*RG
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDELTA_U(JLON,JLEV) = ZENTRX*RG
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
PENTR_U(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
PDETR_U(JLON,JLEV) = YRPHY0%GCVENTR_MIN
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
PENTR_D(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
PDETR_D(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBUOY(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZENTR_T_LOC(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZFLUXDIAG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV+1
DO JLON = KIDIA, KFDIA
ZPHIF_U(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV+1
DO JLON = KIDIA, KFDIA
ZPHIF_U_EQUIP(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFF_Z_UD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZMOD(JLON,JLEV) = YRPHY0%GCVEZ
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZSIGMA(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZRE(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZRE_SATDEF(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZRE_HEIGHT(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZRE_KEDD(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

ZTPHY=YRPHY2%TSPHY
DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZLISS(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
PMF_UP(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZRHOH(JLON,JLEV) = 1.0_JPRB
ENDDO
ENDDO


! Compute convective response time as a function of nominal resolution.
! REFLCAPE should in this case be independant of resolution.

IF(YRPHY0%REFLCAPE > 0._JPRB) THEN
  ! Climate tunings.
  IF (LELAM) THEN
    ZDLON=(2.0_JPRB*RPI*RA)/MIN(YREGEO%EDELX, YREGEO%EDELY)
  ELSE
    ZDLON=REAL(YRDIM%NDLON)
  ENDIF
  DO JLON=KIDIA,KFDIA
    ZDLONL=MIN(ZDLON*PGM(JLON),950._JPRB)
    ZFDXTAUX(JLON)=YRPHY0%TEQC*1.E3_JPRB*(2.234E-4_JPRB*ZDLONL**2 &
    & -0.425_JPRB*ZDLONL+252.5_JPRB)
    ZALFX(JLON)=YRPHY0%ALFX
  ENDDO
ELSE
  ! NWP tunings.
  DO JLON=KIDIA,KFDIA
    ZFDXTAUX(JLON)=ABS(YRPHY0%REFLCAPE)*PGM(JLON)
    ! REDLXN is the mean model mesh in meter.
    ! ZRESOL is the local model resolution in 1/meter.
    ZRESOL=PGM(JLON)/YRGEM%RDELXN
    ! ZALFX varies from ALFX_LR to ALFX_HR, as resolution goes from GCVRESN to GCVRESX.
    ZALFX(JLON)=MAX(YRPHY0%ALFX_LR,MIN(YRPHY0%ALFX_HR,YRPHY0%ALFX_LR+(ZRESOL-YRPHY0%GCVRESN)&
               &/(YRPHY0%GCVRESX-YRPHY0%GCVRESN)*(YRPHY0%ALFX_HR-YRPHY0%ALFX_LR)))
  ENDDO
ENDIF

!--------------------------------------------------

!     SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)
! THIS IS DONE NOW BY INITAPLPAR

!     ---------------------------
! // AUXILIARY CONSTANTS.

ZEPS0=1.E-12_JPRB
ZEPS3=1.E-01_JPRB
ZEPSVVER=1._JPRB
ZEPS16=1.E-06_JPRB
ZEPS1=1.E-10_JPRB
ZEPS4=1.E-05_JPRB
ZEPSW=0.01_JPRB
ZEPS_BCCS=2.8E-05_JPRB ! Drizzle threshold in mm/s.
ZNEBCX=MIN(1._JPRB-ZEPS0,YRPHY0%RNEBCX)


! UPDRAFT INIT
DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZFORM(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZFORU(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDQCA(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZVVERDM(JLON,JLEV) = PUDOM(JLON,JLEV)
ENDDO
ENDDO


! DOWNDRAFT INIT
DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZFORD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBETD(JLON,JLEV) = 1._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDEL_ORGD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZEPS_ORGD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZQDND(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZQN2D(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZSN2D(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZTN2D(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZSDND(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZUMD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZVMD(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JTRA = 1, KTRA
DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZTRAMD(JLON,JLEV,JTRA) = 0._JPRB
ENDDO
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
INLABD(JLON,JLEV) = 0
ENDDO
ENDDO


ZARLII=0.004_JPRB
ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD

ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDTI=1.0_JPRB/ZGDT

ZCCHSL=RCW-RCPD
ZCCHSN=RCS-RCPD

ZKDX=YRPHY0%RKDN*ZENTRX/ZENTR
DO JLON = KIDIA, KFDIA
ZDEPTH(JLON) = 0._JPRB
ENDDO

DO JLON = KIDIA, KFDIA
ZASCENT_BASE(JLON) = 100000._JPRB
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAGFLO(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAG_ECART_TV(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAG_ECART_T(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAGFRICTION(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAGTRANSP(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAG_EPS_ORG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAG_EPS_TUR(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLON = KIDIA, KFDIA
ZDIAG_WMAX(JLON) = 0._JPRB
ENDDO

ZDIAG_TUDAL=0._JPRB
DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZDIAG_KNLAB(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBCC(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZBCC_C_HALF(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBCC_C_FULL(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBCC_CD_FULL(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO


DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
PFHMLTS(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
PFHEVPP(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFCS_DD_DIAG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFCS_UD_NOCORR_DIAG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
ZDIFCS_DD_NOCORR_DIAG(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBUO_W(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBUO_W_POS(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO

LLTEXC=YRPHY0%GCVTEXC /= 0._JPRB
DO JLON = KIDIA, KFDIA
ZSATDEF(JLON) = 0._JPRB
ENDDO

DO JLON = KIDIA, KFDIA
ZRH(JLON) = 0._JPRB
ENDDO

DO JLON = KIDIA, KFDIA
ZDENO(JLON) = 0._JPRB
ENDDO

DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA
ZBINBUO(JLON,JLEV) = 0._JPRB
ENDDO
ENDDO


ZMOD_MIN=YRPHY0%GCVEZ
ZMOD_MAX=1.0_JPRB
DO JLON = KIDIA, KFDIA
ZCVGQ(JLON) = 0._JPRB
ENDDO

DO JLEV=KTDIA,KLEV
!DEC$ VECTOR ALWAYS
  DO JLON=KIDIA,KFDIA

    ! DIAGNOSTIC OF MAXIMUM VERTICAL VELOCITY IN THE COLUMN.
    ZDIAG_WMAX(JLON)=MAX(ZDIAG_WMAX(JLON),ZWU(JLON,JLEV))

    ! CONVECTIVE CLOUD DEPTH:
    ! CUMULATE DEPTH OF LAYERS WHERE VERTICAL VELOCITY IS GREATER THAN A GIVEN THRESHOLD.
    ZBINID(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-YRPHY0%GTDCL))
    ZDEPTH(JLON)=ZDEPTH(JLON)+(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV))/RG*ZBINID(JLON,JLEV)

    ! ZASCENT_BASE: height (in m above sea level) of the lowest level where w_updraft > 10 m/s.
    ZIWGREAT=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-10.0_JPRB))
    ZASCENT_BASE(JLON)=ZASCENT_BASE(JLON)*(1._JPRB-ZIWGREAT)+PAPHI(JLON,JLEV)/RG*ZIWGREAT

    ! ZMOD: MODULATES ENTRAINMENT WITH Z.
    ZPPS=PAPRS(JLON,JLEV)/PAPRS(JLON,KLEV)
    ZMOD(JLON,JLEV)=MAX(ZMOD_MIN,MIN(ZMOD_MAX,ZMOD_MIN+(ZMOD_MAX-ZMOD_MIN) &
      &*MAX(0._JPRB,MIN(1._JPRB,(ZPPS-0.35_JPRB)/(0.85_JPRB-0.35_JPRB)))))

    ! DENSITY.
    ZRHO(JLON,JLEV)=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*ZT(JLON,JLEV))
    ZRHOH(JLON,JLEV-1) = 0.5_JPRB*(ZRHO(JLON,JLEV-1)+ZRHO(JLON,JLEV))

    ! SATURATION DEFICIT.
    ZINTE(JLON)=PDELP(JLON,JLEV)/RG &
      & *MAX(0._JPRB,SIGN(1._JPRB,-(PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV)-2500._JPRB*RG) &
      & *(PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV)-4500._JPRB*RG)))
    ZSATDEF(JLON)=ZSATDEF(JLON)+(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*ZINTE(JLON)
    ZRH(JLON)=ZRH(JLON)+PQ(JLON,JLEV)/PQSAT(JLON,JLEV)*ZINTE(JLON)
    ZDENO(JLON)=ZDENO(JLON)+ZINTE(JLON)

    ! HUMIDITY CONVERGENCE INTEGRATED VERTICALLY OVER CONVECTIVE LEVELS.
    ZCVGQ(JLON)=ZCVGQ(JLON)+MIN(YRPHY0%GCVGQX,PCVGQ(JLON,JLEV)) &
      & *ZBINID(JLON,JLEV)*PDELP(JLON,JLEV)/RG
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZRHOH(JLON,KLEV) = ZRHO(JLON,KLEV) ! Usefull for extra-bound computation !!!!
ENDDO

DO JLON=KIDIA,KFDIA
  ZSATDEF(JLON)=ZSATDEF(JLON)/MAX(100._JPRB,ZDENO(JLON))
  ZRH(JLON)=ZRH(JLON)/MAX(100._JPRB,ZDENO(JLON))
ENDDO

! ------------------------------------------------------------------
! // COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS
! OTHER PRELIMINARY CALCULATION

! THETAE
DO JLON=KIDIA,KFDIA
  ILEVTEN(JLON)=0
  ZE=MAX(ZEPS0,PAPRSF(JLON,KTDIA)*PQ(JLON,KTDIA)&
    &/((1.0_JPRB-PQ(JLON,KTDIA))*RD/RV+PQ(JLON,KTDIA)))
  ZR=PQ(JLON,KTDIA)/(1.0_JPRB-PQ(JLON,KTDIA))*1000._JPRB
  IE0=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS0-ZE)))
  ZTCOND=IE0*2840._JPRB/(3.5_JPRB*LOG(ZT(JLON,KTDIA))-LOG(ZE/100._JPRB)&
    &-4.805_JPRB)+55._JPRB+(1-IE0)*ZT(JLON,KTDIA)
  ZTEN(JLON)=ZT(JLON,KTDIA)*(100000._JPRB/PAPRSF(JLON,KTDIA))&
    &**(0.2854_JPRB*(1.0_JPRB-0.28E-3_JPRB*ZR))&
    &*EXP((3.376_JPRB/ZTCOND-0.00254_JPRB)*ZR*(1.0_JPRB+0.81E-3_JPRB*ZR))

  ZPLH(JLON)=0.0_JPRB
  ZRME(JLON)=0.0_JPRB

  ZPHIF_U_EQUIP(JLON,KLEV)=PAPHIF(JLON,KLEV)
  ZPHIF_U_EQUIP(JLON,KLEV+1)=PAPHI(JLON,KLEV)

  ZPHIF_U(JLON,KLEV)=PAPHIF(JLON,KLEV)
  ZPHIF_U(JLON,KLEV+1)=PAPHI(JLON,KLEV)
ENDDO
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZALFAVEC(JLON)=MAX(ZEPS0,PAPRSF(JLON,JLEV)*PQ(JLON,JLEV)&
      &/((1.0_JPRB-PQ(JLON,JLEV))*RD/RV+PQ(JLON,JLEV)))
    ZDELTA_ARR(JLON)=100000._JPRB/PAPRSF(JLON,JLEV)
    ZTLE_ARR(JLON)=PQ(JLON,JLEV)/(1.0_JPRB-PQ(JLON,JLEV))*1000._JPRB
    ZLOGSIGMA(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS0-ZALFAVEC(JLON))))
  ENDDO
  DO JLON=KIDIA,KFDIA
    IE0=ZLOGSIGMA(JLON)
    ZR=ZTLE_ARR(JLON)
    ZTCOND=IE0*2840._JPRB/(3.5_JPRB*LOG(ZT(JLON,JLEV))-LOG(ZALFAVEC(JLON)/100._JPRB)-4.805_JPRB) &
     & +55._JPRB+(1-IE0)*ZT(JLON,JLEV)
    ZTEL(JLON)=ZT(JLON,JLEV)*ZDELTA_ARR(JLON) &
     &**(0.2854_JPRB*(1.0_JPRB-0.28E-3_JPRB*ZR)) &
     &*EXP((3.376_JPRB/ZTCOND-0.00254_JPRB)*ZR*(1.0_JPRB+0.81E-3_JPRB*ZR))
  ENDDO
!DEC$ VECTOR ALWAYS
  DO JLON=KIDIA,KFDIA
    ZTEN(JLON)=MIN(ZTEN(JLON),ZTEL(JLON))
    ILEVTEN(JLON)=MAX(JLEV*NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZTEN(JLON)&
      &-ZTEL(JLON)))),ILEVTEN(JLON))
  ENDDO
ENDDO


LLNHACC=YRPHY0%GNHACC < 0._JPRB
IF(LLNHACC) THEN
  ! In order to maintain convection in the night, where dd activity occurs,
  ! locate maximum of thetav, on the levels where vertical divergency of wd is
  ! larger than the threshold GNHACC.
  ! The result is put in (ZTBASE, ZQBASE): (T, qv) at ascent base.
  DO JLON=KIDIA,KFDIA
    ZTBASE(JLON)=ZT(JLON,KLEV)
    ZQBASE(JLON)=PQ(JLON,KLEV)
  ENDDO
  ILEV=MAX(KTDIA,INT(0.95_JPRB*REAL(KLEV)))
  DO JLEV=ILEV,KLEV
    DO JLON=KIDIA,KFDIA
      ZDIVERG=(ZWD(JLON,JLEV-1)-ZWD(JLON,JLEV)) &
        & /(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))*RG
      ZBIN=MAX(0._JPRB,SIGN(1._JPRB,YRPHY0%GNHACC-ZDIVERG))
      ZTKLEV=ZT(JLON,JLEV)*(PAPRSF(JLON,KLEV)/PAPRSF(JLON,JLEV))**(RD/RCPD)
      ZBIN=ZBIN*MAX(0._JPRB,SIGN(1._JPRB,ZTKLEV-ZTBASE(JLON)))
      ZTBASE(JLON)=ZBIN*ZTKLEV+(1._JPRB-ZBIN)*ZTBASE(JLON)
      ZQBASE(JLON)=ZBIN*PQ(JLON,JLEV)+(1._JPRB-ZBIN)*ZQBASE(JLON)
    ENDDO
  ENDDO
ENDIF

IF(YRPHY0%GCLOEB > 0._JPRB) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Uniform ZRE, linked to saturation deficit.
      ZRE(JLON,JLEV)=YRPHY0%GREMIN+(YRPHY0%GREMAX-YRPHY0%GREMIN) &
        & *MAX(0._JPRB,MIN(1._JPRB,(ZSATDEF(JLON)-YRPHY0%GSDMIN)/(YRPHY0%GSDMAX-YRPHY0%GSDMIN)))
    ENDDO
  ENDDO
ENDIF

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ! ZPOID     : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
    ! ZDSE      : LOCAL ARRAY FOR DRY STATIC ENERGIES.
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*ZT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    INBAS(JLON,JLEV)=0
  ENDDO
ENDDO

!     ------------------------------------------------------------------
! // INITIALISATION AT THE BOTTOM LAYER TO START THE CLOUD PROFILE

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM    : PROFILE OF MASS FLUX AND LATER MASS FLUX TIME DT.
! KNLAB    : FINAL LAYER ACTIVITY INDEX
! KNND     : PHYSICAL SOLUTION FLAG

DO JLON=KIDIA,KFDIA

  ! THERMODYNAMIC CHARACTERISTICS OF THE CLOUD.

  ! ZTDN       : TEMPERATURE OF THE CLOUDY ASCENT.
  ! ZQDN       : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
  ! ZLDN       : CONDENSED SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
  ! ZTVN      : VIRTUAL CLOUD TEMPERATURE
  ! ZTVE      : VIRTUAL ENVIRONMENT TEMPERATURE

  KNLAB(JLON,KLEV)=0
  KNND(JLON)=0

  IF(LLNHACC) THEN
    ! The profile starts at lowest model level from the (T,qv) value from the
    ! level where both dd diverges and theta is maximum. This (T,qv) value
    ! has been put adiabatically at level KLEV.
    PTU(JLON,KLEV)=ZTBASE(JLON)
    PQU(JLON,KLEV)=ZQBASE(JLON)
  ELSEIF(LLTEXC) THEN
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE TEMPERATURE PLUS AN EXCESS DEPENDING ON TKE.
    PTU(JLON,KLEV)=ZT(JLON,KLEV)+MAX(0.1_JPRB,MIN(2.0_JPRB,YRPHY0%GCVTEXC*PTKE(JLON,KLEV)/RCPD))
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE WATER VAPOUR.
    PQU(JLON,KLEV)=PQ(JLON,KLEV)
  ELSE
    ! The profile starts at lowest model level from a mean between lowest model level and surface.
    PTU(JLON,KLEV)=ZT(JLON,KLEV)+MAX(0._JPRB,YRPHY0%GFSURF*(PTS(JLON)-ZT(JLON,KLEV)))
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE WATER VAPOUR.
    PQU(JLON,KLEV)=PQ(JLON,KLEV)
  ENDIF

  PSU(JLON,KLEV)=(RCPD+ZCPVMD*PQU(JLON,KLEV))*PTU(JLON,KLEV)+ZPHIF_U(JLON,KLEV)
  ZSU(JLON,KLEV)=PSU(JLON,KLEV)
  ZLN(JLON)=0.0_JPRB
  ZIFRAC(JLON,KLEV)=FONICE(PTU(JLON,KLEV))
  ZTVN(JLON,KLEV)=PTU(JLON,KLEV)*(1.0_JPRB+RETV*PQU(JLON,KLEV)-ZLN(JLON))
  ZTVE(JLON,KLEV)=ZT(JLON,KLEV)*(1.0_JPRB+RETV*PQ(JLON,KLEV)-PQLIS(JLON,KLEV))
  PQLIC(JLON,KLEV)=ZLN(JLON)
  ZLDN(JLON,KLEV)=ZLN(JLON)
  ZTNH(JLON)=PTW(JLON,KLEV)
  ZQNH(JLON)=PQW(JLON,KLEV)
  ! TEST DE SATURATION DU NIVEAU (NIVEAU DE CONDENSATION ATTEINT).
  ! INCDN     : NIVEAU DU POINT DE CONDENSATION.
  ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,PQU(JLON,KLEV)-PQSAT(JLON,KLEV))))
  INCDN(JLON,KLEV)=KLEV*ICDN(JLON)
  ! INLFC     : NIVEAU DE CONVECTION LIBRE.
  INLFC(JLON,KLEV)=0
  ZTD=ZT(JLON,KLEV)*(1.0_JPRB-RETV*(PQSAT(JLON,KLEV)-PQ(JLON,KLEV))&
    & /(1.0_JPRB+RETV*PLH(JLON,KLEV)*PQSAT(JLON,KLEV)/(RV*ZT(JLON,KLEV))))
  ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD))
  ZEW=FOEW(ZTD,ZDELTA)
  ZESP=ZEW/PAPRSF(JLON,KLEV)
  ZQW=FOQS(ZESP)
  ZTD=ICDN(JLON)*(YRPHY0%GCVADET*ZTD+(1.0_JPRB-YRPHY0%GCVADET)*PTU(JLON,KLEV))+(1-ICDN(JLON))*PTU(JLON,KLEV)
  ZQW=ICDN(JLON)*(YRPHY0%GCVADET*ZQW+(1.0_JPRB-YRPHY0%GCVADET)*PQU(JLON,KLEV))+(1-ICDN(JLON))*PQU(JLON,KLEV)
  ZQDN(JLON,KLEV)=ZQW
  ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*ZQW)*ZTD+ZPHIF_U(JLON,KLEV)

  ! INIVBAC   : NIVEAU DE LA BASE DE L'ASCENDANCE CONVECTIVE.
  INIVBAC(JLON,KLEV)=KLEV
  ! ZVVER     : VITESSE VERT. DE LA COLONNE CONVECTIVE AUX INTERCOUCHES.
  IF (YRPHY0%LGVVEX) THEN
    ZRT=PR(JLON,KLEV)*ZT(JLON,KLEV)
    ZVVER(JLON,KLEV)=-YRPHY0%GCVWEXC*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB &
      &,PTKE(JLON,KLEV))))*RG*PAPRS(JLON,KLEV)/ZRT
    ZVVER(JLON,KLEV-1)=-YRPHY0%GCVWEXC*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB &
      &,PTKE(JLON,KLEV-1))))*RG*PAPRS(JLON,KLEV-1)/ZRT
  ELSE
    ZVVER(JLON,KLEV)=0.0_JPRB
    ZVVER(JLON,KLEV-1)=0.0_JPRB
  ENDIF
  ZVVERDM(JLON,KLEV)=0.5_JPRB*(ZVVER(JLON,KLEV)+ZVVER(JLON,KLEV-1))
  ! ZDWNDP    : DERIVEE VERT. DE VITESSE VERT.
  ZDWNDP(JLON,KLEV)=0.0_JPRB
  ! ZCAPE     : CAPE DU NIVEAU.
  ZCAPE(JLON)=0.0_JPRB
  ZCIN(JLON)=0.0_JPRB
  IF(YRPHY0%NCVENTR == 1 .OR. YRPHY0%NCVENTR == 15) THEN
    ! ZEPS_TUR    : ENTRAINEMENT TURBULENT.
    ZEPS_TUR(JLON,KLEV)=ZRE(JLON,KLEV)*ZENTRX
    ! ZEPS_ORG     : ENTRAINEMENT ORGANISE.
    ZEPS_ORG(JLON,KLEV)=ZMOD(JLON,KLEV)*ZRE(JLON,KLEV)*ZENTRX &
      &*PR(JLON,KLEV)*ZT(JLON,KLEV)/PAPRSF(JLON,KLEV)
    ! ZDEL_ORG     : DETRAINEMENT ORGANISE
    ZDEL_ORG(JLON,KLEV)=0.0_JPRB
    ZEPSILON_U(JLON,KLEV)=(ZMOD(JLON,KLEV)*ZRE(JLON,KLEV)*ZENTRX+ZEPS_TUR(JLON,KLEV))*RG
    ZDELTA_U(JLON,KLEV)=ZRE(JLON,KLEV)*ZENTRX*RG
  ELSE
    ZEPSILON_U(JLON,KLEV)=ZENTRX*RG
  ENDIF
  ! ZKD       : COEFFICIENT DE RESISTANCE.
  IF(YRPHY0%NCVENTR == 1) THEN
    ZKD(JLON,KLEV)=ZKDX
  ELSE
    ZKD(JLON,KLEV)=YRPHY0%ADOE*ZENTRX*PR(JLON,KLEV)*ZT(JLON,KLEV)/PAPRSF(JLON,KLEV)
  ENDIF
  ! ZSIGMA    : FRACTION DE MAILLE COUVERTE PAR L'ASCENDANCE CONVECTIVE.
  ZSIGMA(JLON,KLEV)=1.0_JPRB
  ZSIGMA(JLON,KLEV-1)=1.0_JPRB
  ZS11(JLON)=0.0_JPRB
  ZS12(JLON)=0.0_JPRB
  ZS15(JLON)=0.0_JPRB
  ZS16(JLON)=0.0_JPRB
  ZS5(JLON)=0.0_JPRB
  ZFMDS(JLON)=0.0_JPRB
  ZFMDQ(JLON)=0.0_JPRB
ENDDO

!     ------------------------------------------------------------------
! // FIRST VERTICAL LOOP (UPWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     ITOP MAY HELP AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE

LLCOND1 = YRPHY0%NCVENTR == 1

ITOP=KLEV+1
DO JLEV=KLEV-1,KTDIA,-1

  ! // SATURATED ADIABAT WITH ENTRAINMENT AND LIQUID (OR ICE) WATER SUSTENTATION.


!DEC$ VECTOR ALWAYS
  DO JLON=KIDIA,KFDIA

    ! ENTRAINMENT AT STARTING LEVEL JLEV+1

    ! ZTB       : "PTU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
    ! ZQB       : "PQU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
    ! ZLB       : "ZLDN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".

    ! -------------------------------------------------
    ! // FRACTIONAL ENTRAINMENT RATE ZFER IN (J/KG)**-1.
    ! -------------------------------------------------

    ZFER=ZEPSILON_U(JLON,JLEV+1)/RG

    ! TO APPLY WHEN COMPUTING PROFILE AT JLEV

    ZRMIX(JLON,JLEV)=ZFER*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZRMIX(JLON,JLEV)=ZRMIX(JLON,JLEV)/(1.0_JPRB+ZRMIX(JLON,JLEV))
    ZTBW(JLON)=PTU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PTW(JLON,JLEV+1)-PTU(JLON,JLEV+1))
    ZDELTA_ARR(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTBW(JLON)))

  ENDDO

! Isolate what may not vectorize
  DO JLON=KIDIA,KFDIA
    ZEW_TLE(JLON)= FOEW (ZTBW(JLON),ZDELTA_ARR(JLON))
  ENDDO

  DO JLON=KIDIA,KFDIA

    ZQBW(JLON)=PQU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PQW(JLON,JLEV+1)-PQU(JLON,JLEV+1))
    ZTB(JLON)=PTU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(ZT(JLON,JLEV+1)-PTU(JLON,JLEV+1))
    ZQB(JLON)=PQU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQU(JLON,JLEV+1))
    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV+1)))
    ZLB(JLON)=ZLN(JLON)+ZRMIX(JLON,JLEV)*(PQLIS(JLON,JLEV+1)*ICDN(JLON)-ZLN(JLON))
    ZTBH(JLON)=ZTNH(JLON)+ZRMIX(JLON,JLEV)*(ZT(JLON,JLEV+1)-ZTNH(JLON))
    ZQBH(JLON)=ZQNH(JLON)+ZRMIX(JLON,JLEV)*(PQ(JLON,JLEV+1)-ZQNH(JLON))

    ! RE-ESTIMATE SATURATION AFTER MIXING

    ! ICE FRACTION AND LATENT HEAT FOR MIXED AIR TEMPERATURE

    ! ZDELTBW=FONICE(ZTBW(JLON))
    ZLHTBW=FOLH(ZTBW(JLON),ZDELTA_ARR(JLON))
    ZCPBW=RCPD+ZCPVMD*ZQBW(JLON)

    ! COMPUTATION OF NEW EQUILIBRIUM OF MIXED AIR
    ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV+1)
    ZQW= FOQS (ZESP)
    ZDQW=FODQS (ZQW,ZESP, FODLEW (ZTBW(JLON),ZDELTA_ARR(JLON)))
    ZINCRQ=(ZDQW*(ZCPBW*(ZTB(JLON)-ZTBW(JLON))+ZLHTBW&
      &*(ZQB(JLON)-ZQBW(JLON)))&
      &+(ZQW-ZQBW(JLON))*ZCPBW)/(ZCPBW+ZLHTBW*ZDQW)
    ZQBW(JLON)=ZQBW(JLON)+ZINCRQ
    ZQBW(JLON)=MIN(ZQBW(JLON),ZQB(JLON)+ZLB(JLON))

    ! HYDROSTATIC COEFFICIENTS FOR THE CLOUD PROFILE.

    ! - TEMPORAIRE(S) 1D .

    ! ZRBB      : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
    ! ZRBH      : WEIGHT OF "PTU" AT "PQU=ZQB" IN THE SAME THICKNESS.
    ! ZRVH      : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBBS(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB &
      &-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBHS(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB &
      &-ZLB(JLON))+ZRVMD*ZQBH(JLON))
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQBH(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

    ! TO COMPUTE SATURATED ADIABATIC PROFILE, GCVADS ALLOWS TO GO
    ! CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBB(JLON)+YRPHY0%GCVADS*(PAPHIF(JLON,JLEV)&
      & -PAPHIF(JLON,JLEV+1))/ZTBH(JLON)
    ZRBH(JLON)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBH(JLON)
    ZRBBS(JLON)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBBS(JLON)+YRPHY0%GCVADS*(PAPHIF(JLON,JLEV)&
      & -PAPHIF(JLON,JLEV+1))/ZTB(JLON)
    ZRBHS(JLON)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBHS(JLON)
    ZRVH(JLON)=(1.0_JPRB-YRPHY0%GCVADS)*ZRVH(JLON)

    ! SNOW OPTION DEPENDENT CALCULATIONS.
    ! DIAGNOSTIC ICE FRACTION
    ! ZDELTA=FONICE(ZTB(JLON))
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTBH(JLON)))

    ! CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
    ! LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

    ! ZLH       : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
    ! ZCP       : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)=FOLH(ZTBH(JLON),ZDELTA)+ZRVH(JLON)*ZTBH(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQBH(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
      &ZCPWMD))*ZLB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

    ! CHANGE OF LEVEL TO OBTAIN A
    ! FIRST GUESS AS STARTING POINT FOR THE NEWTON LOOP.

    ZDELT=ZT(JLON,JLEV)-ZT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTBH(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZTNH(JLON)=ZTBH(JLON)+ZDELT
    ZQNH(JLON)=ZQBH(JLON)+ZDELQ
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
  ENDDO


  ! // NEWTON LOOP.

  DO JIT=1,YRPHY%NBITER
!   Isolate what may not vectorize
    DO JLON=KIDIA,KFDIA

      ! SNOW OPTION DEPENDENT COMPUTATIONS.
      ! DIAGNOSTIC ICE FRACTION
      ZDELTA_ARR(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTNH(JLON)))

      ! SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW_TLE(JLON)= FOEW (ZTNH(JLON),ZDELTA_ARR(JLON))

    ENDDO
    DO JLON=KIDIA,KFDIA
      ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTNH(JLON),ZDELTA_ARR(JLON)))

      ! INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA_ARR(JLON)*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQNH(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQNH(JLON)=ZQNH(JLON)+ZDELQ
      ZTNH(JLON)=ZTNH(JLON)+ZDELT
    ENDDO
  ENDDO


  ! // STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
  ! COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
  ! INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.


! Isolate log (may not vectorize)
  DO JLON=KIDIA,KFDIA
    ZLOGRATIO(JLON)=LOG(PAPRSF(JLON,JLEV+1)/PAPRSF(JLON,JLEV))
  ENDDO
  DO JLON=KIDIA,KFDIA

    ! // DRY ADIABAT.
    ZCPH=RCPD*(1.0_JPRB-ZQB(JLON))+RCPV*ZQB(JLON)
    ZTNSEC=(ZCPH-ZRBBS(JLON))*ZTB(JLON)/(ZCPH+ZRBHS(JLON))

    ! // TEST IF SATURATED AT TOP OF LAYER, INITIALIZE VARIABLES FOR NEXT LEVEL.
    ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZTNH(JLON)-ZTNSEC)))
    INCDN(JLON,JLEV)=MAX(INCDN(JLON,JLEV+1),JLEV*ICDN(JLON))
    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
    PTU(JLON,JLEV)=ICDN(JLON)*ZTNH(JLON)+(1-ICDN(JLON))*ZTNSEC
    PQU(JLON,JLEV)=ICDN(JLON)*ZQNH(JLON)+(1-ICDN(JLON))*ZQB(JLON)

    ! Buoyant convective condensation.
    ZBCC_C_HALF(JLON,JLEV)=MIN(0._JPRB,PQU(JLON,JLEV)-ZQB(JLON)) &
      &/(PAPRSF(JLON,JLEV)-PAPRSF(JLON,JLEV+1))

    ! Integrate hydrostatics within the updraft, with the equipressure hypothesis.
    ZPHIF_U_EQUIP(JLON,JLEV)=ZPHIF_U_EQUIP(JLON,JLEV+1) &
      & +RD*0.5_JPRB*(PTU(JLON,JLEV)+PTU(JLON,JLEV+1)) &
      & *(1.0_JPRB+RETV*0.5_JPRB*(PQU(JLON,JLEV)+PQU(JLON,JLEV+1))) &
      & *ZLOGRATIO(JLON)

    ! Choose between equipressure and equigeopotential hypothesis.
    ZPHIF_U(JLON,JLEV)=(1._JPRB-YRPHY0%GCVADS)*ZPHIF_U_EQUIP(JLON,JLEV)+YRPHY0%GCVADS*PAPHIF(JLON,JLEV)


    ! PHASE PARTITION IN THE UPDRAFT
    ! SIMPLY STORE THE UPDRAFT ICE FRACTION IN ZIFRAC

    ZDELTA=FONICE(PTU(JLON,JLEV))
    ZIFRAC(JLON,JLEV)=ZDELTA

    ZLIQ=YRPHY0%ECMNP/(ZRBB(JLON)*ZTBH(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
      &*(PQU(JLON,JLEV)-ZQBH(JLON)))*PTU(JLON,JLEV))
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLN(JLON)=ICDN(JLON)*MAX(0.0_JPRB,ZLB(JLON)*ZEXP &
      &+(PQU(JLON,JLEV)-ZQBH(JLON))*ZLIQ*(ZEXP-1.0_JPRB))
    PQLIC(JLON,JLEV)=ZLN(JLON)
    !ZDQCA(JLON,JLEV)=ZQBW(JLON)-PQU(JLON,JLEV) formule ref
    ZDQCA(JLON,JLEV)=ZQB(JLON)-PQU(JLON,JLEV) ! approche 3MT.

    ZLDN(JLON,JLEV)=ZLN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*PQU(JLON,JLEV))*PTU(JLON,JLEV)&
      &+ZPHIF_U(JLON,JLEV)
    ZDQCA(JLON,JLEV)=ICDN(JLON)*ZDQCA(JLON,JLEV)

    ! // TEST IF LFC REACHED AT TOP OF LAYER
    ILFC(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,PR(JLON,JLEV)*ZT(JLON,JLEV) &
      & -(RD*(1.0_JPRB-ZLN(JLON))+ZRVMD*ZQNH(JLON))*ZTNH(JLON))))
    INLFC(JLON,JLEV)=MAX(INLFC(JLON,JLEV+1),JLEV*ILFC(JLON))
    ILFC(JLON)=MAX(0,-ISIGN(1,-INLFC(JLON,JLEV)))

    ZTVN(JLON,JLEV)=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)-ZLN(JLON))
    ZTVE(JLON,JLEV)=ZT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)-PQLIS(JLON,JLEV))
    ! // COMPUTE VERTICAL VELOCITY. STEP 1: BUOYANCY.
    ! Buoyancy.
    ZBUO_W(JLON,JLEV)=RG*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))/ZTVE(JLON,JLEV)/YRPHY0%GAMAP1

    IBAC(JLON)=MAX(0,-ISIGN(1,-INIVBAC(JLON,JLEV+1)))
    IF (LLCOND1) THEN
      ZCAPEL(JLON)=RD*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ZCINL(JLON)=-NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZCAPEL(JLON)))) &
       & *ZCAPEL(JLON)*IBAC(JLON)*ICDN(JLON)*(1-ILFC(JLON))
    ELSE
      ZCAPEL(JLON)=RD*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)*MAX(0._JPRB,ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
    ENDIF

  ENDDO
! Loop is slightly reordered and split here because the (Intel) compiler fails to vectorize it.
! More powerful compiler may just fuse the two loops.
! The fused loops could be vectorized easily if certain conditional branches are
! replaced by straightforward calculations of the kind : alfa * X + (1-alfa) * Y
! Shorter loops may also reduce the pressure on the cache.
! REK.
  DO JLON=KIDIA,KFDIA

    IF(YRPHY0%LSBUO) ZBUO_W(JLON,JLEV)=0.5_JPRB*(ZBUO_W(JLON,JLEV)+ZBUO_W(JLON,JLEV+1))
    ! VERTICAL COORDINATE WITHIN UPDRAFT.
    ZDIFF_Z_UD(JLON,JLEV)=ZDIFF_Z_UD(JLON,JLEV+1)+PDELP(JLON,JLEV) &
      & /PAPRSF(JLON,JLEV)*RD*(PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)) &
      & -ZT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)))/RG

    IF(LLNHACC .OR. YRPHY0%LCVNHD) THEN
      ! If NH effects are taken into account, the vertical extension of updraft is
      ! estimated as levels where updraft vertical velocity is positive
      ! and buoyancy positive, in order not to take into account in the vertical
      ! extension the area above LNB, where vertical motion is due to NH effects only.
      ZBINBUO(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZBUO_W(JLON,JLEV)))
    ELSE
      ! If NH effects are not taken into account, the vertical extension of updraft is
      ! estimated as levels where updraft vertical velocity is positive.
      ! It does not depend on buoyancy.
      ZBINBUO(JLON,JLEV)=1._JPRB
    ENDIF
    ZBUO_W_POS(JLON,JLEV)=MAX(0._JPRB,ZBUO_W(JLON,JLEV)) ! positive part of the buoyancy.

    ! ZVACC: vertical acceleration in m/s2 due to buoyancy + NH effects.
    ZVACC(JLON,JLEV)=ZBUO_W(JLON,JLEV)

    ! ZFLO_OMEGA: acceleration in Pa/s2.
    ZFLO_OMEGA=-PAPRSF(JLON,JLEV)/RD/ZTVE(JLON,JLEV)*RG*ZVACC(JLON,JLEV)

    ! Diagnostics.
    ZDIAG_ECART_TV(JLON,JLEV)=ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV)
    ZDIAG_ECART_T(JLON,JLEV)=PTU(JLON,JLEV)-ZT(JLON,JLEV)
    ZDIAGFLO(JLON,JLEV)=ZFLO_OMEGA

    ! // COMPUTE VERTICAL VELOCITY. STEP 2: FRICTION.
    ZDELPF=PDELP(JLON,JLEV)
    ZFER=ZEPSILON_U(JLON,JLEV+1)/RG*PR(JLON,JLEV+1)*ZT(JLON,JLEV+1)/PAPRSF(JLON,JLEV+1)
    ZKDL=ZKD(JLON,JLEV+1)
    ZDIAGFRICTION(JLON,JLEV)=(ZKDL+ZFER)*ZVVER(JLON,JLEV)**2

    ! VERTICAL TRANSPORT DIAGNOSTIC.
    ZDIAGTRANSP(JLON,JLEV)=-0.5_JPRB*(ZVVER(JLON,JLEV+1)**2-ZVVER(JLON,JLEV)**2) &
      &/(PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV))


    ! // COMPUTE VERTICAL VELOCITY. STEP 3: IMPLICIT SOLVING OF THE TENDENCY EQUATION.
    ZVVERDM(JLON,JLEV)=PUDOM(JLON,JLEV)
    ZVVERDEN=2.0_JPRB*YRPHY2%TSPHY*(0.25_JPRB*(ZKDL+ZFER)+0.5_JPRB/ZDELPF)
    ZB=0.5_JPRB*(1.0_JPRB-(ZKDL+ZFER)*YRPHY2%TSPHY*ZVVER(JLON,JLEV))
    ZDELTAP=ZB**2 &
      &-2.0_JPRB*ZVVERDEN*((ZFLO_OMEGA+(-0.5_JPRB/ZDELPF+0.25_JPRB*(ZKDL+ZFER))&
      &*ZVVER(JLON,JLEV)**2)*YRPHY2%TSPHY-0.5_JPRB*ZVVER(JLON,JLEV)&
      &+ZVVERDM(JLON,JLEV))
    ISDELTAP=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZDELTAP)))
    ZDELTAP=ZDELTAP*ISDELTAP
    ZVVERN=(ZB-SQRT(MAX(0._JPRB,ZDELTAP)))/ZVVERDEN

    ZVVERN=MIN(0.0_JPRB,ZVVERN)*ISDELTAP

    ZS5(JLON)=ZS5(JLON)+KNLAB(JLON,JLEV+1)*ZCIN(JLON)
    ZCIN(JLON)=ZCINL(JLON)
    IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,(ZVVERN+ZVVER(JLON,JLEV))+0.0_JPRB)))
    ICIN=(1-IVVER)*IBAC(JLON)*ICDN(JLON)*(1-ILFC(JLON))&
      &*NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,(ZS5(JLON)+ZCINL(JLON)-YRPHY0%GCVCINC)+0.0_JPRB)))

    ZVVER(JLON,JLEV-1)=(1-ICIN)*ZVVERN+ICIN*ZVVERDM(JLON,JLEV)*0.5_JPRB
    ZVVERDM(JLON,JLEV)=0.5_JPRB*(ZVVER(JLON,JLEV)+ZVVER(JLON,JLEV-1))

    ZDELPF=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)
    ZDWNDP(JLON,JLEV)=(ZVVERDM(JLON,JLEV+1)-ZVVERDM(JLON,JLEV))/ZDELPF

    ! J.F. Gueremy no more smoothing of dw/dp (ZALFAVEC(JLON)=1.).
    !ZALFAVEC(JLON)=MIN(1.0_JPRB,0.50_JPRB/1000._JPRB*ZDELPF+0.50_JPRB) ! smoothing.
    ZALFAVEC(JLON)=1.0_JPRB ! no smoothing.
    ZDWNDP(JLON,JLEV)=ZALFAVEC(JLON)*ZDWNDP(JLON,JLEV)+(1.0_JPRB-ZALFAVEC(JLON))*ZDWNDP(JLON,JLEV+1)
  ENDDO

  IF(YRPHY0%NCVENTR == 1) THEN
!   Isolate log (may not vectorize)
    DO JLON=KIDIA,KFDIA
      ZTLE_ARR(JLON)=ZT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE_ARR(JLON)))
      ZEW_TLE(JLON)=FOEW(ZTLE_ARR(JLON),ZDELTA)
      ZTLN_ARR(JLON)=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN_ARR(JLON)))
      ZEW_TLN(JLON)=FOEW(ZTLN_ARR(JLON),ZDELTA)
    ENDDO
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      IDOMDP=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDWNDP(JLON,JLEV))))

      IVVN=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)*ZBINBUO(JLON,JLEV)-YRPHY0%VVN)))
      IVVX=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)*ZBINBUO(JLON,JLEV)-YRPHY0%VVX)))

      ! TURBULENT ENTRAINMENT AS A FUNCTION OF VERTICAL VELOCITY AND Z.

      ZEPS_TURL=ZMOD(JLON,JLEV)*(ZENTR+(ZENTRX-ZENTR)*SIN(RPI/2.0_JPRB*MAX(0.0_JPRB,MIN(1.0_JPRB&
         &,((YRPHY0%VVX-ZVVERDM(JLON,JLEV))/(YRPHY0%VVX-YRPHY0%VVN)))))**2)
      ZEPS_TURL=IVVN*ZENTRX+(1-IVVN)*IVVX*ZEPS_TURL+(1-IVVX)*ZENTR

      INUA=KNLAB(JLON,JLEV+1)
      ZEPS_TUR(JLON,JLEV)=(1-IDOMDP)*ZEPS_TUR(JLON,JLEV+1)&
         &+IDOMDP*(INUA*MIN(ZEPS_TURL,ZEPS_TUR(JLON,JLEV+1))&
         &+(1-INUA)*ZEPS_TURL)

      ZKD(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*YRPHY0%RKDN/ZENTR

      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      ZENTRO=-IVVER/MIN(-ZEPS0,ZVVERDM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
         &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZESP=ZEW_TLN(JLON)/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN_ARR(JLON)*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE_ARR(JLON)*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/ &
         &ZEPS_ORG0))))&
         &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-YRPHY0%FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      ZEPS_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=(ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 3) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // ENTRAINMENT AFTER RIO ET AL. 2010.
      ! -------------------------------------------------

      ZTVNL=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)-ZLN(JLON))
      ZTVEL=ZT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZBUOY(JLON,JLEV)=RG*(1._JPRB-ZTVEL/ZTVNL)
      ZBETA1RIO=0.9_JPRB
      ZA1RIO=0.67_JPRB
      ZBRIO=0.002_JPRB
      ZCRIO=0.012_JPRB

      ! -------------------------------------------------
      ! ENTRAINMENT IN M**-1 AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=MAX(0._JPRB,(ZA1RIO*ZBUOY(JLON,JLEV) &
        &/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2-ZBRIO)/(1._JPRB+ZBETA1RIO))
      ZEPSILON_U(JLON,JLEV)=MAX(ZENTR*RG,MIN(ZENTRX*RG,ZEPSILON_U(JLON,JLEV)))
      ZKD(JLON,JLEV)=YRPHY0%ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)

      ! -------------------------------------------------
      ! DETRAINMENT.
      ! -------------------------------------------------

      ZDELTA_U(JLON,JLEV)=MAX(0._JPRB,-ZA1RIO*ZBETA1RIO/(1._JPRB+ZBETA1RIO) &
        &*ZBUOY(JLON,JLEV)/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2)
      ZENTR_T_LOC(JLON,JLEV)=ZCRIO*SQRT(MAX(0._JPRB,PQU(JLON,JLEV)-PQ(JLON,JLEV)) &
         &/MAX(1.E-4_JPRB,PQU(JLON,JLEV))/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2)
      ZDELTA_U(JLON,JLEV)=ZDELTA_U(JLON,JLEV)+ZENTR_T_LOC(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=MAX(ZENTR*RG,MIN(ZENTRX*RG,ZDELTA_U(JLON,JLEV)))
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 5) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! UNIFORM ENTRAINMENT / DETRAINMENT.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=ZENTRX
      ZKD(JLON,JLEV)=YRPHY0%ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 6) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! ENTRAINMENT / DETRAINMENT "V1" AS IN PIRIOU PHD 2005.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      IF(PAPRSF(JLON,JLEV) < 35000.) THEN
         ZEPSILON_U(JLON,JLEV)=1.2E-4
      ELSEIF(PAPRSF(JLON,JLEV) > 82000. ) THEN
         ZEPSILON_U(JLON,JLEV)=6.0E-4
      ELSE
         ZEPSILON_U(JLON,JLEV)=1.2E-4+(6.0E-4-1.2E-4)*(PAPRSF(JLON,JLEV)-35000.)/(82000.-35000.)
      ENDIF
      ZKD(JLON,JLEV)=YRPHY0%ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 7) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! ENTRAINMENT / DETRAINMENT "V1DEV", SAME SHAPE AS IN PIRIOU PHD 2005,
      ! BUT DIFFERENT MAGNITUDE.
      ! -------------------------------------------------

      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT).
      ! -------------------------------------------------

      IF(PAPRSF(JLON,JLEV) < 35000.) THEN
         ZEPSILON_U(JLON,JLEV)=YRPHY0%GCVENTRN
      ELSEIF(PAPRSF(JLON,JLEV) > 82000. ) THEN
         ZEPSILON_U(JLON,JLEV)=YRPHY0%GCVENTRX
      ELSE
         ZEPSILON_U(JLON,JLEV)=YRPHY0%GCVENTRN+(YRPHY0%GCVENTRX-YRPHY0%GCVENTRN)*(PAPRSF(JLON,JLEV)-35000.) &
          &/(82000.-35000.)
      ENDIF
      ZKD(JLON,JLEV)=YRPHY0%ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 16) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // UNIFORM TURBULENT ENTRAINMENT.
      ! // FRICTION FUNCTION OF ZRE.
      ! // ORGANIZED ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      ! TURBULENT ENTRAINMENT.
      ZEPS_TUR(JLON,JLEV)=ZENTRX ! ZENTRX and ZEPS_TUR are in (J/kg)**-1.
      ZKD(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*YRPHY0%ADOE/ZRE(JLON,JLEV)**YRPHY0%GEXPKD &
        & *PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZKD is in Pa**-1.
      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZENTR_TUR is in Pa**-1.

      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      ZENTRO=-IVVER/MIN(-ZEPS0,ZVVERDM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
         &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZTLN=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZTLE=ZT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE))
      ZEW=FOEW(ZTLE,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN))
      ZEW=FOEW(ZTLN,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/ &
         &ZEPS_ORG0))))&
         &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-YRPHY0%FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      ZEPS_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)

      ZDIAG_EPS_ORG(JLON,JLEV)=ZEPS_ORG(JLON,JLEV) &
        &*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*ZT(JLON,JLEV))*RG
      ZDIAG_EPS_TUR(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*RG

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=((ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG)*YRPHY0%GCHI0
    ENDDO

  ELSEIF(YRPHY0%NCVENTR == 17) THEN
   DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! UNIFORM ENTRAINMENT / DETRAINMENT.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=ZENTRX*RG
      ZKD(JLON,JLEV)=YRPHY0%ADOE**(1._JPRB+ZDEPTH(JLON)/7000._JPRB*24._JPRB) &
        & *ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO
  ELSEIF(YRPHY0%NCVENTR == 19) THEN
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // Turbulent entrainment depending on
      ! // * height
      ! // * kinetic energy of the downdraft.
      ! // ORGANIZED ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      ! Uniform ZRE, linked to saturation deficit integrated in the vertical.
      !ZRE_SATDEF(JLON,JLEV)=GREMIN+(GREMAX-GREMIN) &
      !  & *MAX(0._JPRB,MIN(1._JPRB,(ZSATDEF(JLON)-GSDMIN)/(GSDMAX-GSDMIN)))

      ! Local ZRE, linked to local saturation deficit.
      ZRE_SATDEF(JLON,JLEV)=YRPHY0%GREMIN+(YRPHY0%GREMAX-YRPHY0%GREMIN) &
        & *MAX(0._JPRB,MIN(1._JPRB,(PQSAT(JLON,JLEV)-PQ(JLON,JLEV)-YRPHY0%GSDMIN) &
        & /(YRPHY0%GSDMAX-YRPHY0%GSDMIN)))

      ! Height (above surface) in m.
      ZHEIGHT=(ZPHIF_U(JLON,JLEV)-PAPHI(JLON,KLEV))/RG

      ! Reduction factor of entrainment, due to height.
      !ZRE_HEIGHT(JLON,JLEV)=1._JPRB/MAX(1._JPRB,ZHEIGHT/1000._JPRB)
      ZRE_HEIGHT(JLON,JLEV)=1.

      ! Reduction factor (ZRE_KEDD) of entrainment, due to kinetic energy of the downdraft.
      ZRE_KEDD(JLON,JLEV)=1._JPRB/(1._JPRB+YRPHY0%GKEDDRE*ZKEDD(JLON))

      ! The reduction factor that will be applied to turbulent and organized
      ! entrainement and detrainement is the product of all reduction factors.
      ZRE(JLON,JLEV)=ZRE_SATDEF(JLON,JLEV)*ZRE_HEIGHT(JLON,JLEV)*ZRE_KEDD(JLON,JLEV)

      ! TURBULENT ENTRAINMENT.
      ! ZENTRX and ZEPS_TUR are in (J/kg)**-1.
      ZEPS_TUR(JLON,JLEV)=ZENTRX
      ZKD(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*YRPHY0%ADOE/ZRE(JLON,JLEV)**YRPHY0%GEXPKD &
        & *PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZKD is in Pa**-1.
      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZENTR_TUR is in Pa**-1.

      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,PUDOM(JLON,JLEV)+0.0_JPRB)))
      ZDELPF=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)
      ZDWNDP(JLON,JLEV)=(PUDOM(JLON,JLEV+1)-PUDOM(JLON,JLEV))/ZDELPF
      ZENTRO=-IVVER/MIN(-ZEPS0,PUDOM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
        &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZTLN=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZTLE=ZT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE))
      ZEW=FOEW(ZTLE,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN))
      ZEW=FOEW(ZTLN,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/ &
        &ZEPS_ORG0))))&
        &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-YRPHY0%FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      ZEPS_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=YRPHY0%GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)

      ZDIAG_EPS_ORG(JLON,JLEV)=ZEPS_ORG(JLON,JLEV) &
        &*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*ZT(JLON,JLEV))*RG
      ZDIAG_EPS_TUR(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*RG

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=((ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV) &
        &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG)*YRPHY0%GCHI0
    ENDDO

  ELSE

    ! NCVENTR not recognized. Entrainment and detrainment set to 0.
    DO JLON=KIDIA,KFDIA
      ZEPSILON_U(JLON,JLEV)=0._JPRB
      ZKD(JLON,JLEV)=0._JPRB
      ZDELTA_U(JLON,JLEV)=0._JPRB
    ENDDO
  ENDIF

  ! -------------------------------------------------
  ! The reducing factor ZRE is applied to the total entrainment and detrainment.
  ! -------------------------------------------------
  DO JLON=KIDIA,KFDIA
    ZEPSILON_U(JLON,JLEV)=ZRE(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)
    ZKD(JLON,JLEV)=ZRE(JLON,JLEV)*ZKD(JLON,JLEV)
    ZDELTA_U(JLON,JLEV)=ZRE(JLON,JLEV)*ZDELTA_U(JLON,JLEV)
  ENDDO

  IF(YRPHY0%LCVEOD) THEN

    ! -------------------------------------------------
    ! Entrainment Outside Draft (EOD) is set to a large value.
    ! "Outside Draft" means here "where vertical velocity is below a given threshold".
    ! ZBINID is the binary indicateur of Inside Draft.
    ! -------------------------------------------------
    DO JLON=KIDIA,KFDIA
      ZEPSILON_U(JLON,JLEV)=ZBINID(JLON,JLEV)*ZEPSILON_U(JLON,JLEV) &
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX*RG
      ZKD(JLON,JLEV)=ZBINID(JLON,JLEV)*ZKD(JLON,JLEV) &
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX &
        & *PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZBINID(JLON,JLEV)*ZDELTA_U(JLON,JLEV) &
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX*RG
    ENDDO

  ENDIF

  ! -------------------------------------------------
  ! In case of NH approach, entrainment above LNB is set to a large value.
  ! -------------------------------------------------
  DO JLON=KIDIA,KFDIA
    ZEPSILON_U(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZEPSILON_U(JLON,JLEV) &
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX*RG
    ZKD(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZKD(JLON,JLEV) &
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX &
      & *PR(JLON,JLEV)*ZT(JLON,JLEV)/PAPRSF(JLON,JLEV)
    ZDELTA_U(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZDELTA_U(JLON,JLEV) &
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX*RG
  ENDDO

    ! -------------------------------------------------
    ! // INTEGRATE SIGMA IN THE VERTICAL.
    ! -------------------------------------------------

  IF(YRPHY0%NCVSIG == 1 .OR. YRPHY0%NCVSIG == 2) THEN
!   Isolate Log (may not vectorize)
    DO JLON=KIDIA,KFDIA
      ZLOGSIGMA(JLON)=LOG(MAX(1.0E-15_JPRB,ZSIGMA(JLON,JLEV)))
    ENDDO
    DO JLON=KIDIA,KFDIA
      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      ZDLOGM=(ZEPS_ORG(JLON,JLEV)-ZDEL_ORG(JLON,JLEV))/YRPHY0%GCVRE
      ZEMD_DIAG(JLON,JLEV-1)=ZEPSILON_U(JLON,JLEV)-ZDELTA_U(JLON,JLEV)
      ZSIGMA(JLON,JLEV-1)=REAL(IVVER)*(EXP(ZLOGSIGMA(JLON)&
        &+PDELP(JLON,JLEV)*(ZDLOGM+IVVER*ZDWNDP(JLON,JLEV)&
        &*ZRE(JLON,JLEV)/MIN(-ZEPS0,ZVVERDM(JLON,JLEV)))) )&
        &+REAL(1-IVVER)*1.0_JPRB
    ENDDO
  ENDIF
! Isolate Log (may not vectorize)
  DO JLON=KIDIA,KFDIA
    ZTD=ZT(JLON,JLEV)*(1.0_JPRB+(ZLN(JLON)-RETV*(PQSAT(JLON,JLEV)&
      & -PQ(JLON,JLEV)))/(1.0_JPRB+RETV*PLH(JLON,JLEV)*PQSAT(JLON,JLEV)&
      & /(RV*ZT(JLON,JLEV))))
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD))
    ZEW_TLE(JLON)=FOEW (ZTD,ZDELTA)
  ENDDO

  IF(YRPHY0%NCVSIG == 1) THEN
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=MAX(ZEPS0,MIN(ZSIGMA(JLON,JLEV-1),ZSIGMA(JLON,JLEV)))
    ENDDO
  ELSEIF(YRPHY0%NCVSIG == 2) THEN
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=MAX(ZEPS0,MIN(1.0_JPRB,ZSIGMA(JLON,JLEV-1)))
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=1.
    ENDDO
  ENDIF

  DO JLON=KIDIA,KFDIA

    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
    IBAC(JLON)=MAX(0,-ISIGN(1,-INIVBAC(JLON,JLEV+1)))
    IKUO5(JLON)=MAX(0,ISIGN(1,&
     & IBAC(JLON)*INIVBAC(JLON,JLEV+1)+(1-IBAC(JLON)) &
     & -ILEVTEN(JLON)))
    IKUO6(JLON)=MAX(NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVER(JLON,JLEV-1)+0.0_JPRB)))&
      & ,NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVER(JLON,JLEV)+0.0_JPRB))))

    ! KNLAB(JLON,JLEV)=0 IF ASCENT BASE ABOVE THETAE MINIMUM
    KNLAB(JLON,JLEV)=IKUO6(JLON)*IBAC(JLON)*IKUO5(JLON)
    PQLIC(JLON,JLEV)=PQLIC(JLON,JLEV)*KNLAB(JLON,JLEV)*ICDN(JLON)

    ! // FINAL VALUES OF DETRAINMENT.
    ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
    ZQW=FOQS(ZESP)
    ZTD=ICDN(JLON)*(YRPHY0%GCVADET*ZTD+(1.0_JPRB-YRPHY0%GCVADET)*PTU(JLON,JLEV))+(1-ICDN(JLON))*PTU(JLON,JLEV)
    ZQW=ICDN(JLON)*(YRPHY0%GCVADET*ZQW+(1.0_JPRB-YRPHY0%GCVADET)*PQU(JLON,JLEV))+(1-ICDN(JLON))*PQU(JLON,JLEV)

    ZQDN(JLON,JLEV)=ZQW
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQW)*ZTD+ZPHIF_U(JLON,JLEV)

    ! // IN CASE OF UNBUOYANT PARCEL ONE SETS THE STATE TO THAT OF THE ENVIRONMENT.
    INUA=KNLAB(JLON,JLEV)
    INIVBAC(JLON,JLEV)=MAX(INIVBAC(JLON,JLEV+1),JLEV)
    INIVBAC(JLON,JLEV)=INUA*INIVBAC(JLON,JLEV)+(1-INUA)*JLEV
    INIVBAC(JLON,JLEV+1)=INUA*INIVBAC(JLON,JLEV+1)
    INLFC(JLON,JLEV)=INUA*INLFC(JLON,JLEV)

    ZEPSILON_U(JLON,JLEV)=INUA*ZEPSILON_U(JLON,JLEV)+(1-INUA)*ZEPSILON_U(JLON,KLEV)
    ZVVER(JLON,JLEV-1)=INUA*ZVVER(JLON,JLEV-1)
    IF(YRPHY0%LGVVEX) ZVVER(JLON,JLEV-1)=ZVVER(JLON,JLEV-1)-(1-INUA)*YRPHY0%GCVWEXC &
      &*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB,PTKE(JLON,JLEV-1))))&
      &*RG*PAPRS(JLON,JLEV-1)/(PR(JLON,JLEV)*ZT(JLON,JLEV))
    ZVVERDM(JLON,JLEV)=INUA*ZVVERDM(JLON,JLEV)+(1-INUA)*ZVVER(JLON,JLEV-1)
    IF(YRPHY0%NCVSIG == 0) THEN
      ZSIGMA(JLON,JLEV-1)=1.
    ELSE
      ZSIGMA(JLON,JLEV-1)=INUA*ZSIGMA(JLON,JLEV-1)+(1-INUA)*1.0_JPRB
    ENDIF
    PTU(JLON,JLEV)=INUA*PTU(JLON,JLEV)+(1-INUA)*ZT(JLON,JLEV)
    PQU(JLON,JLEV)=INUA*PQU(JLON,JLEV)+(1-INUA)*PQ(JLON,JLEV)

    PSU(JLON,JLEV)=(RCPD+ZCPVMD*PQU(JLON,JLEV))*PTU(JLON,JLEV)+ZPHIF_U(JLON,JLEV)
    ZSU(JLON,JLEV)=PSU(JLON,JLEV)

    ZLN(JLON)=INUA*ZLN(JLON)
    ZQDN(JLON,JLEV)=INUA*ZQDN(JLON,JLEV)+(1-INUA)*PQ(JLON,JLEV)
    ZSDN(JLON,JLEV)=INUA*ZSDN(JLON,JLEV)+(1-INUA)*ZDSE(JLON,JLEV)
    ZTNH(JLON)=INUA*ZTNH(JLON)+(1-INUA)*PTW(JLON,JLEV)
    ZQNH(JLON)=INUA*ZQNH(JLON)+(1-INUA)*PQW(JLON,JLEV)

    ! // SATURATION TEST AT TOP LEVEL.
    ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,PQU(JLON,JLEV)-PQSAT(JLON,JLEV))))
    INCDN(JLON,JLEV)=INUA*INCDN(JLON,JLEV)+(1-INUA)*JLEV*ICDN(JLON)

    ! // MASS FLUX PROFILE.
    ZFORM(JLON,JLEV)=-1.0_JPRB*KNLAB(JLON,JLEV)*ZVVER(JLON,JLEV)&
      & *ZSIGMA(JLON,JLEV)

    ! SUMMATIONS.

    ZFORMV=0.5_JPRB*(ZFORM(JLON,JLEV+1)+ZFORM(JLON,JLEV))
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)*ZCAPE(JLON)
    ZBCC_C_FULL(JLON,JLEV+1)=0.5_JPRB*(ZBCC_C_HALF(JLON,JLEV)+ZBCC_C_HALF(JLON,JLEV+1))

    ZFMDQN=0.5_JPRB*ZFORM(JLON,JLEV)*((PQU(JLON,JLEV)+PQU(JLON,JLEV+1))&
       &-(PQ(JLON,JLEV)+PQ(JLON,JLEV+1)))
    ZFMDSN=0.5_JPRB*ZFORM(JLON,JLEV)*((ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))&
       &-(ZDSE(JLON,JLEV)+ZDSE(JLON,JLEV+1)))

    ! ZS16 = VERTICAL INTEGRAL OF DP/P FROM (dCAPE/dt) DUE TO CONVECTION.
    ZS16(JLON)=ZS16(JLON)+KNLAB(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)&
       &*RD*((1.0_JPRB+RETV*PQ(JLON,JLEV+1))/PCP(JLON,JLEV+1)&
       &*(ZFMDS(JLON)-ZFMDSN+ZFORMV*PLH(JLON,JLEV+1)&
       &*PDELP(JLON,JLEV+1)*ZBCC_C_FULL(JLON,JLEV+1))&
       &+RETV*ZT(JLON,JLEV+1)*(ZFMDQ(JLON)-ZFMDQN&
       &-ZFORMV*PDELP(JLON,JLEV+1)*ZBCC_C_FULL(JLON,JLEV+1)))/PAPRSF(JLON,JLEV+1)
    ZFMDQ(JLON)=ZFMDQN
    ZFMDS(JLON)=ZFMDSN
    ZDPLAB=KNLAB(JLON,JLEV+1)*PDELP(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)
    ! ZS12: vertical extension of the updraft (in Pa).
    ZS12(JLON)=ZS12(JLON)+ZDPLAB
    ! ZS11: mean vertical velocity.
    ZS11(JLON)=ZS11(JLON)+ZDPLAB*ABS(ZVVERDM(JLON,JLEV+1))
    ZCAPE(JLON)=ZCAPEL(JLON)

    PUDOM(JLON,JLEV)=ZVVERDM(JLON,JLEV)
    PUDAL(JLON,JLEV)=0._JPRB
    PDDAL(JLON,JLEV)=0._JPRB
  ENDDO

  ! VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
  ! "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+KNLAB(JLON,JLEV)
  ENDDO

  IF (ISUM /= 0) THEN
    ITOP=JLEV
  ENDIF
ENDDO

IF(YRPHY0%LCVNHD) THEN

  !-------------------------------------------------
  ! Updraft and downdraft velocities are computed using coupled prognostic
  ! equations, using NH pressure-gradient terms.
  !-------------------------------------------------

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  KNLAB(JLON,JLEV) = 1
  ENDDO
  ENDDO


  !-------------------------------------------------
  ! Vertical smoothing of evaporation.
  !-------------------------------------------------

  DO JLON=KIDIA,KFDIA
    ZMEAN_D(JLON,KTDIA)=PEVAPO(JLON,KTDIA)
    ZMEAN_U(JLON,KLEV)=PEVAPO(JLON,KLEV)
    ZINTEG(JLON,KTDIA)=1.E-11_JPRB
  ENDDO
  ! DOWNWARD SMOOTHING.
  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZMEAN_D(JLON,JLEV)=(ZMEAN_D(JLON,JLEV-1)*YRPHY0%GNHDSMOS+PEVAPO(JLON,JLEV) &
        & *(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))) &
        & /(YRPHY0%GNHDSMOS+ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))
      ZINTEG(JLON,JLEV)=ZINTEG(JLON,JLEV-1)+(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV)) &
        & *ABS(PEVAPO(JLON,JLEV))
    ENDDO
  ENDDO
  ! UPWARD SMOOTHING.
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      ZMEAN_U(JLON,JLEV)=(ZMEAN_U(JLON,JLEV+1)*YRPHY0%GNHDSMOS+PEVAPO(JLON,JLEV) &
         & *(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))) &
         & /(YRPHY0%GNHDSMOS+ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))
    ENDDO
  ENDDO
  ! Average upward and downward smoothings, with a weight depending on altitude.
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWEIGHT=ZINTEG(JLON,JLEV)/ZINTEG(JLON,KLEV)
      ZEVAPO(JLON,JLEV)=ZWEIGHT*ZMEAN_U(JLON,JLEV) &
        & +(1._JPRB-ZWEIGHT)*ZMEAN_D(JLON,JLEV)
    ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

      ! Temperature within the downdraft.
      ZT_DOWN(JLON,JLEV)=ZT(JLON,JLEV)-YRPHY0%GNHDEV*ZEVAPO(JLON,JLEV) &
        & *MAX(0._JPRB,SIGN(1._JPRB,ZDEPTH(JLON)-YRPHY0%GDEPTH))
    ENDDO
  ENDDO

  !-------------------------------------------------
  ! Updraft and downdraft prognostic equations.
  !-------------------------------------------------

  ! Tendency due to advection: direct computation.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_ADV(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_ADV(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTWU_ADV(JLON,JLEV)=-ZWU(JLON,JLEV)*(ZWU(JLON,JLEV)-ZWU(JLON,JLEV+1)) &
        & /(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))*RG
      ZTWD_ADV(JLON,JLEV)=-ZWD(JLON,JLEV)*(ZWD(JLON,JLEV)-ZWD(JLON,JLEV+1)) &
        & /(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))*RG
    ENDDO
  ENDDO

  ! Buoyancy.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_BUO(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_BUO(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Updraft.
      ZTVNL=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV))
      ZTVEL=ZT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTWU_BUO(JLON,JLEV)=MAX(0._JPRB,RG*(ZTVNL/ZTVEL-1._JPRB))
      ! Downdraft.
      ZTVNL=ZT_DOWN(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTVEL=ZT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTWD_BUO(JLON,JLEV)=MIN(0._JPRB,RG*(ZTVNL/ZTVEL-1._JPRB))
    ENDDO
  ENDDO

  ! Dynamical production.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_DYP(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_DYP(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Updraft.
      ZTWU_DYP(JLON,JLEV)=0._JPRB
      ! Downdraft due to precipitation evaporation. The only source term is the negative
      ! buoyancy due to this evaporation. No contribution here from dynamical production.
      ZTWD_DYP(JLON,JLEV)=0._JPRB
    ENDDO
  ENDDO

  ! Friction.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_FRI(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_FRI(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTWU_FRI(JLON,JLEV)=-YRPHY0%GFRIC*ZWU(JLON,JLEV)**2
      ZTWD_FRI(JLON,JLEV)= YRPHY0%GFRIC*ZWD(JLON,JLEV)**2
    ENDDO
  ENDDO


  ! Target wd: a vertical smoothing of wd + buoyancy effect.
  ! 1. Buoyancy effect.
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWD_PLUS_BUO(JLON,JLEV)=ZWD(JLON,JLEV)+YRPHY2%TSPHY*ZTWD_BUO(JLON,JLEV)
    ENDDO
  ENDDO
  ! 2. Vertical smoothing.
  DO JLON=KIDIA,KFDIA
    ZMEAN_D(JLON,KTDIA)=0._JPRB
    ZMEAN_U(JLON,KLEV)=0._JPRB
    ZINTEG(JLON,KTDIA)=ZEPSW
  ENDDO
  ! DOWNWARD SMOOTHING.
  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZMEAN_D(JLON,JLEV)=(ZMEAN_D(JLON,JLEV-1)*YRPHY0%GNHDSMOW+ZWD_PLUS_BUO(JLON,JLEV) &
        & *(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))) &
        & /(YRPHY0%GNHDSMOW+ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))
      ZINTEG(JLON,JLEV)=ZINTEG(JLON,JLEV-1)+(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV)) &
        & *ABS(ZWD_PLUS_BUO(JLON,JLEV))
    ENDDO
  ENDDO
  ! UPWARD SMOOTHING.
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      ZMEAN_U(JLON,JLEV)=(ZMEAN_U(JLON,JLEV+1)*YRPHY0%GNHDSMOW+ZWD_PLUS_BUO(JLON,JLEV) &
         & *(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))) &
         & /(YRPHY0%GNHDSMOW+ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))
    ENDDO
  ENDDO
  ! Average upward and downward smoothings, with a weight depending on altitude.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTARGET_WD(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_NHD(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_NHD(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWEIGHT=ZINTEG(JLON,JLEV)/ZINTEG(JLON,KLEV)
      ZTARGET_WD(JLON,JLEV)=ZWEIGHT*ZMEAN_U(JLON,JLEV) &
        & +(1._JPRB-ZWEIGHT)*ZMEAN_D(JLON,JLEV)
      ZTWD_NHD(JLON,JLEV)=(ZTARGET_WD(JLON,JLEV)-ZWD_PLUS_BUO(JLON,JLEV))/YRPHY2%TSPHY
      ZTWU_NHD(JLON,JLEV)=ZTWD_NHD(JLON,JLEV)
    ENDDO
  ENDDO

  ! Temporal integration.
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_TOT_RAW(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_TOT_RAW(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_TOT(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_TOT(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWU_STAU(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTWD_STAU(JLON,JLEV) = 0._JPRB
  ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

      ! Pure diagnostic.

      ! Total tendency (diagnostic).
      ZTWU_TOT_RAW(JLON,JLEV)=ZTWU_ADV(JLON,JLEV)+ZTWU_BUO(JLON,JLEV) &
        & +ZTWU_DYP(JLON,JLEV)+ZTWU_FRI(JLON,JLEV)+ZTWU_NHD(JLON,JLEV)
      ZTWD_TOT_RAW(JLON,JLEV)=ZTWD_ADV(JLON,JLEV)+ZTWD_BUO(JLON,JLEV) &
        & +ZTWD_DYP(JLON,JLEV)+ZTWD_FRI(JLON,JLEV)+ZTWD_NHD(JLON,JLEV)

      ! Solve temporal equation implicitly.
      ! Updraft.
      ZA=0.5_JPRB/(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))+YRPHY0%GFRIC
      ZB=1._JPRB/YRPHY2%TSPHY
      ZC=-ZTWU_BUO(JLON,JLEV)-ZTWU_DYP(JLON,JLEV)-ZTWU_NHD(JLON,JLEV)-0.5_JPRB/(ZPHIF_U(JLON,JLEV) &
        & -ZPHIF_U(JLON,JLEV+1))*ZWU(JLON,JLEV+1)**2.-ZWU(JLON,JLEV)/YRPHY2%TSPHY
      ZD=ZB*ZB-4._JPRB*ZA*ZC
      ZWNEW=(-ZB+SQRT(MAX(0._JPRB,ZD)))/(2._JPRB*ZA)
      ZTWU_TOT(JLON,JLEV)=(ZWNEW-ZWU(JLON,JLEV))/YRPHY2%TSPHY
      ZWU(JLON,JLEV)=ZWNEW
      ! Downdraft.
      ZA=0.5_JPRB/(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))-YRPHY0%GFRIC
      ZC=-ZTWD_BUO(JLON,JLEV)-ZTWD_DYP(JLON,JLEV)-ZTWD_NHD(JLON,JLEV)-0.5_JPRB/(ZPHIF_U(JLON,JLEV) &
        & -ZPHIF_U(JLON,JLEV+1))*ZWD(JLON,JLEV+1)**2.-ZWD(JLON,JLEV)/YRPHY2%TSPHY
      ZD=ZB*ZB-4._JPRB*ZA*ZC
      ZWNEW=(-ZB+SQRT(MAX(0._JPRB,ZD)))/(2._JPRB*ZA)
      ZTWD_TOT(JLON,JLEV)=(ZWNEW-ZWD(JLON,JLEV))/YRPHY2%TSPHY
      ZWD(JLON,JLEV)=ZWNEW

      ! Check extrema.
      ZWU(JLON,JLEV)=MAX(0._JPRB,ZWU(JLON,JLEV))
      ZWD(JLON,JLEV)=MIN(0._JPRB,ZWD(JLON,JLEV))

      ! VERTICAL VELOCITY IN PA/S COMPUTED FROM VERTICAL VELOCITY IN M/S.
      PUDOM(JLON,JLEV)=-ZWU(JLON,JLEV)*PAPRSF(JLON,JLEV) &
      & *RG/(PR(JLON,JLEV)*ZT(JLON,JLEV))
    ENDDO
  ENDDO
ENDIF ! LCVNHD.

!-------------------------------------------------
! CLOSURE: ESTIMATE CONVECTIVE MASSFLUX CLOSER TO EULERIAN EQUATIONS (EUL).
!-------------------------------------------------

!CALL ACMTUDEUL ( KIDIA,KFDIA,KLON,KTDIA,KLEV,&
!   &PDELP,PRDELP,PAPRS,PAPRSF,PAPHI,PAPHIF,PR,ZT, PTU, PQU &
!   &)

! // QUITTING IN CASE OF NO CONVECTION EVERYWHERE.
! THE "IF THEN / ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(YRPHY%NPHYREP /= 0 .AND. YRPHY%NPHYREP /= -1) ITOP=KTDIA

IF (ITOP < KLEV+1) THEN

  ! LAST LEVEL SUMMATION (TOP).

  DO JLON=KIDIA,KFDIA

    ! PUDOM HAS TO BE ZERO I.E. THE LARGE SCALE VERT VEL. AT LEVEL KTDIA

    PUDOM(JLON,KTDIA)=0.0_JPRB
    ZFORMV=ZFORM(JLON,KTDIA)
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)*ZCAPE(JLON)
    ZBCC_C_FULL(JLON,KTDIA)=0.5_JPRB*(ZBCC_C_HALF(JLON,KTDIA-1)+ZBCC_C_HALF(JLON,KTDIA))
    ZFMDQN=0.5_JPRB*ZFORM(JLON,KTDIA)*((PQU(JLON,KTDIA)+PQU(JLON,KTDIA+1))&
       &-(PQ(JLON,KTDIA)+PQ(JLON,KTDIA+1)))
    ZFMDSN=0.5_JPRB*ZFORM(JLON,KTDIA)*((ZSU(JLON,KTDIA)+ZSU(JLON,KTDIA+1))&
       &-(ZDSE(JLON,KTDIA)+ZDSE(JLON,KTDIA+1)))

    ZS16(JLON)=ZS16(JLON)+KNLAB(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)&
       &*RD*((1.0_JPRB+RETV*PQ(JLON,KTDIA))/PCP(JLON,KTDIA)&
       &*(ZFMDS(JLON)-ZFMDSN+ZFORMV*PLH(JLON,KTDIA)&
       &*PDELP(JLON,KTDIA)*ZBCC_C_FULL(JLON,KTDIA))&
       &+RETV*ZT(JLON,KTDIA)*(ZFMDQ(JLON)-ZFMDQN&
       &-ZFORMV*PDELP(JLON,KTDIA)*ZBCC_C_FULL(JLON,KTDIA)))/PAPRSF(JLON,KTDIA)
    ZDPLAB=KNLAB(JLON,KTDIA)*PDELP(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)
    ZS12(JLON)=ZS12(JLON)+ZDPLAB
    ZS11(JLON)=ZS11(JLON)+ZDPLAB*ABS(ZVVERDM(JLON,KTDIA))

  ENDDO

  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
      INBAS(JLON,JLEV)=KNLAB(JLON,JLEV)*(1-KNLAB(JLON,JLEV+1))
    ENDDO
  ENDDO

  ! ------------------------------------------------------------------
  ! // MESH FRACTION, VERTICAL ENTHALPY BUDGET AND DETRAINMENT
  ! ------------------------------------------------------------------

  ! DIAGNOSTIC OF CAPE (FORECASTER NEED) WHATEVER CLOSURE.
  DO JLON = KIDIA, KFDIA
  PCAPE(JLON) = 0.0_JPRB
  ENDDO

  DO JLON = KIDIA, KFDIA
  ZIBCCS(JLON) = 0.0_JPRB
  ENDDO
 ! Integral of BCC multiplied by Sigma.
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      ZIBCCS(JLON)=ZIBCCS(JLON)-PUDOM(JLON,JLEV) &
        & *ZBCC_C_FULL(JLON,JLEV)*ZSIGMA(JLON,JLEV) &
        & *ZBINID(JLON,JLEV)*PDELP(JLON,JLEV)/RG
    ENDDO
    IF (LLCAPECIN) THEN
      DO JLON=KIDIA,KFDIA
        PCAPE(JLON)=PCAPE(JLON) +KNLAB(JLON,JLEV)*RD&
          &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
          &*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ENDDO
    ELSE
      DO JLON=KIDIA,KFDIA
        PCAPE(JLON)=PCAPE(JLON) +KNLAB(JLON,JLEV)*RD&
          &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
          &*MAX(0._JPRB,ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ENDDO
    ENDIF
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTAU(JLON)=1.0_JPRB/ZFDXTAUX(JLON)*ZS12(JLON)*ZS12(JLON)/MAX(ZS11(JLON),ZEPS0)
    ZTAU_DIAG(JLON)=ZTAU(JLON)
    ! Impose tau (time consumption of CAPE) to be larger than the model time step.
    IF(YRPHY0%LCVCONTAU) ZTAU(JLON)=MAX(YRPHY2%TSPHY,ZTAU(JLON))
    ZS15(JLON)=MAX(0.0_JPRB,ZS15(JLON))
    ! ZALF_CVGQ : surface fraction occupied by convection, in case of CVGQ closure.
    ! As CVGQ closure is relevant for precipitating convection only,
    ! the surface computed here will be > 0 only in moist cases, where the vertical integral of
    ! BCC is larger than the drizzle threshold.
    ! In order to avoid instabilities when starting prediction from an
    ! interpolated analysis, one sets the CVGQ to 0 during the first hour of
    ! prediction.
    ZALF_CVGQ(JLON)=MAX(0._JPRB,ZCVGQ(JLON))/MAX(ZEPS_BCCS,ZIBCCS(JLON)) &
      & *MAX(0._JPRB,SIGN(1._JPRB,ZIBCCS(JLON)-ZEPS_BCCS)) &
      & *MAX(0._JPRB, SIGN(1._JPRB,YRRIP%RSTATI-3600._JPRB))


    ! EXTRA-DIAGNOSTICS
    PALF_CVGQ(JLON)=ZALF_CVGQ(JLON)
    PALF_CAPE(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0)

    IF(YRPHY0%NCVCLOS == 1) THEN
      ! ALPHA DIAGNOSED FROM d(CAPE)/dt=-CAPE/TAU.
      PALF(JLON)=MIN(ZALFX(JLON),ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0))

    ELSEIF(YRPHY0%NCVCLOS == 2) THEN
      ! ALPHA AS A FUNCTION OF SATURATION DEFICIT.
      PALF(JLON)=YRPHY0%GALMIN+(YRPHY0%GALMAX-YRPHY0%GALMIN) &
      & *MAX(0._JPRB,MIN(1._JPRB,(YRPHY0%GSDAMAX-ZSATDEF(JLON)) &
      & /(YRPHY0%GSDAMAX-YRPHY0%GSDAMIN)))

      ! LIMITATION OF ALPHA IN STRATOCUMULUS CONTEXT: WHERE TKE AND CLOUD DEPTH ARE SMALL.
      ZBINTKE=MAX(0._JPRB,SIGN(1._JPRB,0.3_JPRB-PTKE(JLON,KLEV)))
      ZBINDEPTH=MAX(0._JPRB,SIGN(1._JPRB,800._JPRB-ZDEPTH(JLON)))
      PALF(JLON)=PALF(JLON)*(1._JPRB-ZBINTKE*ZBINDEPTH)

      ! TAU COMPUTED HERE FOR DIAGNOSTIC PURPOSE ONLY.
      ZTAU(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))/MAX(0.001_JPRB,PALF(JLON))

      ! Impose time consumption of CAPE to be larger than the model time step.
      IF(YRPHY0%LCVCONTAU) PALF(JLON)=PALF(JLON)*MAX(0._JPRB,MIN(1._JPRB,ZTAU(JLON)/YRPHY2%TSPHY))

    ELSEIF(YRPHY0%NCVCLOS == 3) THEN
    ! ALPHA AS A FUNCTION OF DOWNDRAFT ACTIVITY.
      PALF(JLON)=YRPHY0%GALMIN+(YRPHY0%GALMAX-YRPHY0%GALMIN) &
      & *MAX(0._JPRB,MIN(1._JPRB,ZKEDD(JLON)/7500._JPRB))

      ! LIMITATION OF ALPHA IN STRATOCUMULUS CONTEXT: WHERE TKE AND CLOUD DEPTH ARE SMALL.
      ZBINTKE=MAX(0._JPRB,SIGN(1._JPRB,0.3_JPRB-PTKE(JLON,KLEV)))
      ZBINDEPTH=MAX(0._JPRB,SIGN(1._JPRB,800._JPRB-ZDEPTH(JLON)))
      PALF(JLON)=PALF(JLON)*(1._JPRB-ZBINTKE*ZBINDEPTH)

      ! TAU COMPUTED HERE FOR DIAGNOSTIC PURPOSE ONLY.
      ZTAU(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))/MAX(0.001_JPRB,PALF(JLON))

    ELSEIF(YRPHY0%NCVCLOS == 4) THEN
      ! PALF is prognostic.
      ZTALF_SCE(JLON)=PALF(JLON)*YRPHY0%GMUKEDD*ZKEDD(JLON)
      ZTALF_SIN(JLON)=-PALF(JLON)*YRPHY0%GALTAU
      PALF(JLON)=MAX(YRPHY0%GALMIN,MIN(YRPHY0%GALMAX*ZRH(JLON),PALF(JLON)+YRPHY2%TSPHY*(ZTALF_SCE(JLON)+ZTALF_SIN(JLON))))
    ELSEIF(YRPHY0%NCVCLOS == 5) THEN
      ! CVGQ closure.
      PALF(JLON)=ZALF_CVGQ(JLON)
    ELSEIF(YRPHY0%NCVCLOS == 6) THEN
      ! CAPE and CVGQ mixed closure.
      ! 1. ALPHA DIAGNOSED FROM d(CAPE)/dt=-CAPE/TAU.
      PALF(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0)
      ! 2. One uses the max value between CAPE and CVGQ approaches.
      PALF(JLON)=MIN(ZALFX(JLON),MAX(PALF(JLON),ZALF_CVGQ(JLON)))
    ELSE
      ! Constant PALF.
      PALF(JLON)=0.04_JPRB
    ENDIF

    ! FIRST TEST OF PHYSICAL VALIDITY OF THE OBTAINED PALF
    KNND(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PALF(JLON)+0.0_JPRB)))
  ENDDO

  ! DOWNDROUGHTS GYT2011
  IF (YRPHY0%LGDDD .AND. .NOT.YRPHY0%LCVNHD) THEN
    ! Diagnostic downdrafts.
    CALL ACMTDDD( KIDIA,KFDIA,KLON,ITOP,KLEV,KTRA,&
      &INCDN,KNLAB,&
      &PALPH,PAPHIF,PAPRS,PAPRSF,&
      &PDELP,PLH,PLNPR,PQ,PQLIC,PQLIS,PQSAT,PQW,&
      &PR,ZT,PTW,PU,PV,PTRA,ILEVTEN,&
      &ZBETD,ZDEL_ORGD,ZEPS_ORGD,ZFORD,ZQDND,ZQN2D,ZSDND,ZSN2D,ZTN2D,&
      &ZUMD,ZVMD,ZTRAMD,ZBCC_CD_FULL,&
      &INLABD)
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
       PDDAL(JLON,JLEV)=PALF(JLON)*INLABD(JLON,JLEV)*PALF(JLON)/YRPHY0%FEVAPC
       PDDOM(JLON,JLEV)=0.5_JPRB*(ZFORD(JLON,JLEV)+ZFORD(JLON,JLEV-1))
      ENDDO
    ENDDO
  ELSEIF (YRPHY0%LCVNHD) THEN
    ! Prognostic downdraft.
    ! A call to the diagnostic downdrafts is done, only to get the profile of dry
    ! static energy and water vapour.
    CALL ACMTDDD( KIDIA,KFDIA,KLON,ITOP,KLEV,KTRA,&
      &INCDN,KNLAB,&
      &PALPH,PAPHIF,PAPRS,PAPRSF,&
      &PDELP,PLH,PLNPR,PQ,PQLIC,PQLIS,PQSAT,PQW,&
      &PR,ZT,PTW,PU,PV,PTRA,ILEVTEN,&
      &ZBETD,ZDEL_ORGD,ZEPS_ORGD,ZFORD,ZQDND,ZQN2D,ZSDND,ZSN2D,ZTN2D,&
      &ZUMD,ZVMD,ZTRAMD,ZBCC_CD_FULL,&
      &INLABD)
    DO JLEV = 1, KLEV
    DO JLON = KIDIA, KFDIA
    PDDOM(JLON,JLEV) = 0._JPRB
    ENDDO
    ENDDO

    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
       PDDOM(JLON,JLEV)=-PAPRSF(JLON,JLEV)*RG/(PR(JLON,JLEV)*ZT(JLON,JLEV))*ZWD(JLON,JLEV)
      ENDDO
    ENDDO
  ELSE
    ! No downdrafts.
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
       PDDAL(JLON,JLEV)=0._JPRB
       PDDOM(JLON,JLEV)=0._JPRB
      ENDDO
    ENDDO
  ENDIF

  ! ------------------------------------------------------------
  ! // ADAPT THE MESH FRACTION PROFILE AND TENDENCY
  ! // AND COMPUTE MASS FLUX AT THE INTERFACE LEVELS
  ! ------------------------------------------------------------

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      KNLAB(JLON,JLEV)=KNND(JLON)*KNLAB(JLON,JLEV)
      INBAS(JLON,JLEV)=INBAS(JLON,JLEV)*KNND(JLON)
      PUDAL(JLON,JLEV)=PALF(JLON)*0.5_JPRB*(ZSIGMA(JLON,JLEV)+ZSIGMA(JLON,JLEV-1))*KNLAB(JLON,JLEV)
      IF(YRPHY0%LCVNHD) PDDAL(JLON,JLEV)=YRPHY0%GUDDD*PUDAL(JLON,JLEV)
      ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
      PQLIC(JLON,JLEV)=PQLIC(JLON,JLEV)*KNLAB(JLON,JLEV)
      PNEBC(JLON,JLEV)=MAX(ZEPS0,ICDN(JLON)*PALF(JLON)&
       &*0.5_JPRB*(ZSIGMA(JLON,JLEV)+ZSIGMA(JLON,JLEV-1))*KNLAB(JLON,JLEV))
    ENDDO
  ENDDO

  ! ------------------------------------------------------------
  ! // PROTECTION AGAINST NL INSTABILITY
  ! // AND COMPUTE THE ENVIRONMENT VALUES AFFECTED Y PSEUDO-SUBSIDENCE
  ! ------------------------------------------------------------
  ! ADITIONAL VERTICAL LOOP (UPWARDS) TO MODIFY THE PREVIOUSLY
  ! COMPUTED MASS FLUX.


  ! ZFORM HAS BEEN COMPLETELY INITIALIZED TO ZERO EARLIER, SO IT IS ZERO
  ! ABOVE ITOP (AND NORMALLY ALSO AT KLEV - BUT SEE VIB...)

  ! cdir unroll=8
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA

      !     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
      !     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.
      ZFORU(JLON,JLEV)=PALF(JLON)*YRPHY2%TSPHY*ZFORM(JLON,JLEV)
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV)-PALF(JLON)/YRPHY0%FEVAPC&
        & *ZFORD(JLON,JLEV))
      ZFORM(JLON,JLEV)=PALF(JLON)*YRPHY2%TSPHY*ZFORM(JLON,JLEV)
      ZFORD(JLON,JLEV)=PALF(JLON)*YRPHY2%TSPHY*PALF(JLON)/YRPHY0%FEVAPC*ZFORD(JLON,JLEV)
    ENDDO
  ENDDO

  IF (YRPHY0%LGPRONI1) THEN
    DO JLEV=KLEV-1,ITOP,-1
      DO JLON=KIDIA,KFDIA      

      ! PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
      ! APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV+1)+(ZFORM(JLON,JLEV)&
        &-ZFORM(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
        &*MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV+1))))
      ZFORU(JLON,JLEV)=MAX(0.0_JPRB,ZFORU(JLON,JLEV+1)+(ZFORU(JLON,JLEV)&
        &-ZFORU(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
        &*MAX(0.0_JPRB,ZFORU(JLON,JLEV)-ZFORU(JLON,JLEV+1))))
      ENDDO
    ENDDO
    DO JLEV=ITOP,KLEV-1
      DO JLON=KIDIA,KFDIA
      ! PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
      ! APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORD(JLON,JLEV)=MAX(0.0_JPRB,ZFORD(JLON,JLEV-1)+(ZFORD(JLON,JLEV)&
         &-ZFORD(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
         &*MAX(0.0_JPRB,ZFORD(JLON,JLEV)-ZFORD(JLON,JLEV-1))))
      ENDDO
    ENDDO
  ENDIF  



  ! -----------------------------------------------------------------
  ! // WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
  ! // PRESSURE GRADIENT TERM (KERSHAW & GREGORY SCHEME), CONSIDERING
  ! // THE ACTIVE LAYERS
  ! -----------------------------------------------------------------

  ! INITIALIZE WORK ARRAYS

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZUM(JLON,JLEV) = 0.0_JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZVM(JLON,JLEV) = 0.0_JPRB
  ENDDO
  ENDDO

  DO JTRA = 1, KTRA
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZTRAM(JLON,JLEV,JTRA) = 0.0_JPRB
  ENDDO
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZBET(JLON,JLEV) = 0.0_JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZA13(JLON,JLEV) = 0.0_JPRB
  ENDDO
  ENDDO

  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZA14(JLON,JLEV) = 0.0_JPRB
  ENDDO
  ENDDO

  DO JTRA = 1, KTRA
  DO JLEV = 1, KLEV
  DO JLON = KIDIA, KFDIA
  ZA15(JLON,JLEV,JTRA) = 0.0_JPRB
  ENDDO
  ENDDO
  ENDDO


  ! -------------------------------------------------------------------
  ! // UPWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
  ! ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

  ! TEMPORAIRES 1D.

  ! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.

  ! TEMPORAIRE 0D.

  ! TEMPORAIRES 2D.

  ! ZA13      : UC0.
  ! ZA14      : VC0.
  ! ZA15      : TRA0.
  ! ZUM       : AVERAGE U VALUE FROM CLOUD BASE.
  ! ZVM       : AVERAGE V VALUE FROM CLOUD BASE.
  ! ZTRAM     : AVERAGE TRACER VALUE FROM CLOUD BASE.

  DO JLON=KIDIA,KFDIA

    ! AT BASE: ZBE=1, ZUM=PU, ZVM=PV, ZA13=PU, ZA14=PV
    ! INACTIVE LAYERS AND BASE => ZUM=PU, ZVM=PV

    ZBET(JLON,KLEV)=1.0_JPRB
    ZUM(JLON,KLEV)=PU(JLON,KLEV)
    ZVM(JLON,KLEV)=PV(JLON,KLEV)

    DO JTRA=1,KTRA
      ZTRAM(JLON,KLEV,JTRA) = PTRA(JLON,KLEV,JTRA)
    ENDDO
    ! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

    ZA13(JLON,KLEV)=PU(JLON,KLEV)
    ZA14(JLON,KLEV)=PV(JLON,KLEV)
  
    DO JTRA=1,KTRA
      ZA15(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
    ENDDO
    
  ENDDO

  ! cdir unroll=4
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA

      ! ZNBA=1 WHERE ACTIVE AND NOT BASE
      ! ZBAS=1  AT BASE (AND THEN ACTIVE)
      ! BOTH ARE 0 IN INACTIVE LAYERS
      ZBAS=REAL(INBAS(JLON,JLEV),JPRB)
      ZNBA=REAL(1-INBAS(JLON,JLEV),JPRB)*KNLAB(JLON,JLEV)

      ! ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
      ! ZBET IS 0 IN INACTIVE LAYERS
      ! 1 IN THE BASE LAYERS
      ! DECREASE UPWARDS IN THE ACTIVE LAYERS
      ! ZUM = PU IN INACTIVE LAYERS
      ! ZVM = PV IN INACTIVE LAYERS
      ! AND BOTH ARE ACCUMULATED IN ACTIVE LAYERS


      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX)+ZBAS

      ! ZUM BACK TO PU IF  KNLAB=0 OR INBAS=1
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
         &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
         &+YRPHY0%TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
         &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
         &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
         &+YRPHY0%TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
         &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA) = (ZTRAM(JLON,JLEV+1,JTRA)&
         &+(ZMIX*(PTRA(JLON,JLEV+1,JTRA)-ZTRAM(JLON,JLEV+1,JTRA))&
         &+YRPHY0%TUDGP*(PTRA(JLON,JLEV,JTRA)-PTRA(JLON,JLEV+1,JTRA)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
         &+PTRA(JLON,JLEV,JTRA)*(1.0_JPRB-ZNBA)
      ENDDO

      ! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

      ! ZA13,ZA14 : WIND AT BASE OF ACTIVE SEGMENT
      ! SAME VALUE UP TO NEXT BASE
      ! (IN INACTIVE LAYERS, MULTIPLIED BY ZBET=0)

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
      
      DO JTRA=1,KTRA    
        ZA15(JLON,JLEV,JTRA) = ZA15(JLON,JLEV+1,JTRA)*ZNBA+PTRA(JLON,JLEV,JTRA)*ZBAS      
      ENDDO

    ENDDO
  ENDDO

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
       ZZ=1.0_JPRB-ZBET(JLON,JLEV)
       ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
       ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
       DO JTRA=1,KTRA
         ZTRAM(JLON,JLEV,JTRA)=ZZ*ZTRAM(JLON,JLEV,JTRA)+ZBET(JLON,JLEV)*ZA15(JLON,JLEV,JTRA)           
       ENDDO
    ENDDO
  ENDDO

  ! ------------------------------------------------------------------
  ! // FLUXES COMPUTATION

  DO JLON=KIDIA,KFDIA
    ZBCC(JLON,ITOP)=-PUDAL(JLON,ITOP)*PUDOM(JLON,ITOP)*ZBCC_C_FULL(JLON,ITOP) &
                   &+PDDAL(JLON,ITOP)*PDDOM(JLON,ITOP)*ZBCC_CD_FULL(JLON,ITOP)
    PFCCQL(JLON,ITOP)=PFCCQL(JLON,ITOP-1)-1._JPRB/RG*(1._JPRB-ZIFRAC(JLON,ITOP)) &
      &*(-ZBCC(JLON,ITOP))*PDELP(JLON,ITOP)
    PFCCQI(JLON,ITOP)=PFCCQI(JLON,ITOP-1)-1._JPRB/RG*ZIFRAC(JLON,ITOP) &
      &*(-ZBCC(JLON,ITOP))*PDELP(JLON,ITOP)
  ENDDO

  ! cdir unroll=8
  DO JLEV=ITOP+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZBCC(JLON,JLEV)=-PUDAL(JLON,JLEV)*PUDOM(JLON,JLEV)*ZBCC_C_FULL(JLON,JLEV) &
                     &+PDDAL(JLON,JLEV)*PDDOM(JLON,JLEV)*ZBCC_CD_FULL(JLON,JLEV)
      PFCCQL(JLON,JLEV)=PFCCQL(JLON,JLEV-1)-1._JPRB/RG*(1._JPRB-ZIFRAC(JLON,JLEV)) &
        &*(-ZBCC(JLON,JLEV))*PDELP(JLON,JLEV)
      PFCCQI(JLON,JLEV)=PFCCQI(JLON,JLEV-1)-1._JPRB/RG*ZIFRAC(JLON,JLEV) &
        &*(-ZBCC(JLON,JLEV))*PDELP(JLON,JLEV)
    ENDDO
  ENDDO

  DO JLON=KIDIA,KFDIA
    PFCCQL(JLON,KLEV)=PFCCQL(JLON,KLEV-1)
    PFCCQI(JLON,KLEV)=PFCCQI(JLON,KLEV-1)
  ENDDO

  IF(YRPHY0%LCVUVM) THEN
    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES
    ! ---------------------------
    ! COMPUTE ZUM AND ZVM AS UNIFORM, EQUAL TO THE MEAN VALUE ALONG CONVECTIVE LEVELS.
    DO JLEV = 1, KLEV
    DO JLON = KIDIA, KFDIA
    ZUM(JLON,JLEV) = 0._JPRB
    ENDDO
    ENDDO

    DO JLEV = 1, KLEV
    DO JLON = KIDIA, KFDIA
    ZVM(JLON,JLEV) = 0._JPRB
    ENDDO
    ENDDO

    DO JLON = KIDIA, KFDIA
    ZDENO(JLON) = 0._JPRB
    ENDDO

    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        ZBINID2(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-0.1_JPRB))
        ZINTE(JLON)=ZBINID2(JLON,JLEV)*PDELP(JLON,JLEV)/RG
        ZUM(JLON,KLEV)=ZUM(JLON,KLEV)+PU(JLON,JLEV)*ZINTE(JLON)
        ZVM(JLON,KLEV)=ZVM(JLON,KLEV)+PV(JLON,JLEV)*ZINTE(JLON)
        ZDENO(JLON)=ZDENO(JLON)+ZINTE(JLON)
      ENDDO
    ENDDO
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        ZUM(JLON,JLEV)=ZBINID2(JLON,JLEV)*ZUM(JLON,KLEV)/MAX(100._JPRB,ZDENO(JLON)) &
          &+(1._JPRB-ZBINID2(JLON,JLEV))*PU(JLON,JLEV)
        ZVM(JLON,JLEV)=ZBINID2(JLON,JLEV)*ZVM(JLON,KLEV)/MAX(100._JPRB,ZDENO(JLON)) &
          &+(1._JPRB-ZBINID2(JLON,JLEV))*PV(JLON,JLEV)
        DO JTRA=1,KTRA
          ZTRAM(JLON,JLEV,JTRA)=ZBINID2(JLON,JLEV)*ZTRAM(JLON,KLEV,JTRA)/MAX(100._JPRB,ZDENO(JLON)) &
            &+(1._JPRB-ZBINID2(JLON,JLEV))*PTRA(JLON,JLEV,JTRA)
        ENDDO
      ENDDO
    ENDDO
  ENDIF

  IF(YRPHY0%LCVIMPT) THEN

    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES: IMPLICIT COMPUTATION.
    ! ---------------------------

  DO JLEV=ITOP,KLEV
     DO JLON=KIDIA,KFDIA
        ! REPLACE THE UPDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_U)
        ZQU(JLON,JLEV)=(PQ(JLON,JLEV)-PQU(JLON,JLEV))
        ZSU(JLON,JLEV)=(ZDSE(JLON,JLEV)-ZSU(JLON,JLEV))
        ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
        ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
          DO JTRA=1, KTRA
             ZTRAM(JLON,JLEV,JTRA) = PTRA(JLON,JLEV,JTRA) - ZTRAM(JLON,JLEV,JTRA)
          ENDDO
          ! ABOVE ITOP: ALL FLUXES ARE ZERO
          ! LEVEL KLEV: ZFORM=0 THERE, HENCE ALL THE FLUXES ARE ZERO, TOO.
          ! ELSEWHERE WE NEED ZFORM >=0 - AVOID THE ROUNDING ERRORS
          ! REQUIRE AT LEAST THAT PDIFCQ<0
     ENDDO
  ENDDO

    ! cdir unroll=8
    DO JLEV=ITOP,KLEV-1
       DO JLON=KIDIA,KFDIA
          ZAUX=ZFORU(JLON,JLEV)/(PDELP(JLON,JLEV)+ZFORU(JLON,JLEV))
          ZDPSGDT=PDELP(JLON,JLEV)*ZGDTI*0.5_JPRB
          PDIFCQ(JLON,JLEV)=ZAUX*( PDIFCQ(JLON,JLEV-1)+ &
             & ZDPSGDT*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1)) )
          IF (YRPHY0%NCVENTR == 1) THEN
            PDIFCS(JLON,JLEV)=ZAUX*( PDIFCS(JLON,JLEV-1)+ &
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) )
          ELSE
            PDIFCS(JLON,JLEV)=MIN(YRPHY0%GCVTRMIN,ZAUX*( PDIFCS(JLON,JLEV-1)+ &
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) ))
          ENDIF
          PSTRCU(JLON,JLEV)=ZAUX*( PSTRCU(JLON,JLEV-1)+ &
             & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
          PSTRCV(JLON,JLEV)=ZAUX*( PSTRCV(JLON,JLEV-1)+ &
             & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
          DO JTRA=1, KTRA
            PSTRCTRA(JLON,JLEV,JTRA)=ZAUX*( PSTRCTRA(JLON,JLEV-1,JTRA)+ &
               & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )
          ENDDO
       ENDDO
    ENDDO

    IF (YRPHY0%LGDDD) THEN
    DO JLEV=ITOP,KLEV
       DO JLON=KIDIA,KFDIA
          ! replace the DOWNDRAFT values by the difference (psi-psi_d)
          ZQU(JLON,JLEV)=(PQ(JLON,JLEV)-ZQN2D(JLON,JLEV))
          ZSU(JLON,JLEV)=(ZDSE(JLON,JLEV)-ZSN2D(JLON,JLEV))
          ZUM(JLON,JLEV)=(PU(JLON,JLEV)-ZUMD(JLON,JLEV))
          ZVM(JLON,JLEV)=(PV(JLON,JLEV)-ZVMD(JLON,JLEV))
          DO JTRA=1, KTRA
             ZTRAM(JLON,JLEV,JTRA)=(PTRA(JLON,JLEV,JTRA)-ZTRAMD(JLON,JLEV,JTRA))
          ENDDO
       ENDDO
    ENDDO
    DO JLEV=KLEV-1,ITOP,-1
       DO JLON=KIDIA,KFDIA
          ZAUX=ZFORD(JLON,JLEV)/(PDELP(JLON,JLEV+1)+ZFORD(JLON,JLEV))
          ZDPSGDT=PDELP(JLON,JLEV+1)*ZGDTI*0.5_JPRB
          ZDIFCQD(JLON,JLEV)=ZAUX*( ZDIFCQD(JLON,JLEV+1)+ &
             & ZDPSGDT*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1)) )
          IF (YRPHY0%NCVENTR == 1) THEN
            ZDIFCSD(JLON,JLEV)=ZAUX*( ZDIFCSD(JLON,JLEV+1)+ &
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) )
          ELSE
            ZDIFCSD(JLON,JLEV)=MAX(-YRPHY0%GCVTRMIN,ZAUX*( ZDIFCSD(JLON,JLEV+1)+ &
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) ))
          ENDIF
          ZSTRCUD(JLON,JLEV)=ZAUX*( ZSTRCUD(JLON,JLEV+1)+ &
             & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
          ZSTRCVD(JLON,JLEV)=ZAUX*( ZSTRCVD(JLON,JLEV+1)+ &
             & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
          DO JTRA=1,KTRA
            ZSTRCTRAD(JLON,JLEV,JTRA)=ZAUX*( ZSTRCTRAD(JLON,JLEV+1,JTRA)+ &
               & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )
          ENDDO
       ENDDO
    ENDDO

    DO JLEV=ITOP,KLEV-1
       DO JLON=KIDIA,KFDIA
          ZDIFCQD(JLON,JLEV)=-ZDIFCQD(JLON,JLEV)
          PDIFCQ(JLON,JLEV)=PDIFCQ(JLON,JLEV)+ZDIFCQD(JLON,JLEV)
          ZDIFCSD(JLON,JLEV)=-ZDIFCSD(JLON,JLEV)
          PDIFCS(JLON,JLEV)=PDIFCS(JLON,JLEV)+ZDIFCSD(JLON,JLEV)
          ZSTRCUD(JLON,JLEV)=-ZSTRCUD(JLON,JLEV)
          PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV)+ZSTRCUD(JLON,JLEV)
          ZSTRCVD(JLON,JLEV)=-ZSTRCVD(JLON,JLEV)
          PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV)+ZSTRCVD(JLON,JLEV)
          DO JTRA=1,KTRA
            ZSTRCTRAD(JLON,JLEV,JTRA)=-ZSTRCTRAD(JLON,JLEV,JTRA)
            PSTRCTRA(JLON,JLEV,JTRA)=PSTRCTRA(JLON,JLEV,JTRA)+ZSTRCTRAD(JLON,JLEV,JTRA)
          ENDDO
       ENDDO
    ENDDO
    ENDIF  !LGDDD

  ELSE ! LCVIMPT.

    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES: EXPLICIT COMPUTATION.
    ! ---------------------------

  DO JLEV=ITOP,KLEV
     DO JLON=KIDIA,KFDIA
        ! REPLACE THE UPDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_U)
        ZQU(JLON,JLEV)=(PQ(JLON,JLEV)-PQU(JLON,JLEV))
        ZSU(JLON,JLEV)=(ZDSE(JLON,JLEV)-ZSU(JLON,JLEV))
        ZTU(JLON,JLEV)=(ZT(JLON,JLEV)-PTU(JLON,JLEV))*(RATM/PAPRSF(JLON,JLEV))**RKAPPA
        ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
        ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
          DO JTRA=1, KTRA
             ZTRAM(JLON,JLEV,JTRA) = PTRA(JLON,JLEV,JTRA) - ZTRAM(JLON,JLEV,JTRA)
          ENDDO
     ENDDO
  ENDDO

    ! cdir unroll=8
    DO JLEV=ITOP,KLEV-1
       DO JLON=KIDIA,KFDIA
          ZDIFCS_UD_NOCORR_DIAG(JLON,JLEV)=ZGDTI*ZFORU(JLON,JLEV) &
            &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          IF (YRPHY0%NPRONI == 1) THEN  
             ZAUX=ZFORU(JLON,JLEV)/(1._JPRB+ZFORU(JLON,JLEV)/PDELP(JLON,JLEV))
          ELSEIF (YRPHY0%NPRONI == 2) THEN  
             ZAUX=MIN(ZFORU(JLON,JLEV),YRPHY0%GPRONI*PDELP(JLON,JLEV))
          ELSE   
             ZAUX=ZFORU(JLON,JLEV)
          ENDIF   
          PDIFCQ(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1))
          PDIFCS(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PDIFCTH(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZTU(JLON,JLEV)+ZTU(JLON,JLEV+1))
          PSTRCU(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1))
          PSTRCV(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1))
          DO JTRA=1,KTRA
            PSTRCTRA(JLON,JLEV,JTRA)=ZGDTI*ZAUX*0.5_JPRB*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA))
          ENDDO
       ENDDO
    ENDDO

    IF (YRPHY0%LGDDD) THEN
    DO JLEV=ITOP,KLEV
       DO JLON=KIDIA,KFDIA
          ! REPLACE THE DOWNDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_D)
          ZQU(JLON,JLEV)=(PQ(JLON,JLEV)-ZQN2D(JLON,JLEV))
          ZSU(JLON,JLEV)=(ZDSE(JLON,JLEV)-ZSN2D(JLON,JLEV))
          ZTU(JLON,JLEV)=(ZT(JLON,JLEV)-ZTN2D(JLON,JLEV))*(RATM/PAPRSF(JLON,JLEV))**RKAPPA
          ZUM(JLON,JLEV)=(PU(JLON,JLEV)-ZUMD(JLON,JLEV))
          ZVM(JLON,JLEV)=(PV(JLON,JLEV)-ZVMD(JLON,JLEV))
          DO JTRA=1,KTRA
            ZTRAM(JLON,JLEV,JTRA)=(PTRA(JLON,JLEV,JTRA)-ZTRAMD(JLON,JLEV,JTRA))
          ENDDO
       ENDDO
    ENDDO
    DO JLEV=ITOP,KLEV-1
       DO JLON=KIDIA,KFDIA
          ZDIFCS_DD_NOCORR_DIAG(JLON,JLEV)=ZGDTI*ZFORD(JLON,JLEV) &
            &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          IF (YRPHY0%NPRONI == 1) THEN  
            ZAUX=ZFORD(JLON,JLEV)/(1._JPRB+ZFORD(JLON,JLEV)/PDELP(JLON,JLEV))
          ELSEIF (YRPHY0%NPRONI == 2) THEN  
            ZAUX=MIN(ZFORD(JLON,JLEV),YRPHY0%GPRONI*PDELP(JLON,JLEV))
          ELSE
            ZAUX=ZFORD(JLON,JLEV)
          ENDIF   
          PDIFCQ(JLON,JLEV)=PDIFCQ(JLON,JLEV)-ZGDTI*ZAUX &
            &*0.5_JPRB*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1))
          PDIFCS(JLON,JLEV)=PDIFCS(JLON,JLEV)-ZGDTI*ZAUX &
              &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PDIFCTH(JLON,JLEV)=PDIFCTH(JLON,JLEV)-ZGDTI*ZAUX &
            &*0.5_JPRB*(ZTU(JLON,JLEV)+ZTU(JLON,JLEV+1))
          ZDIFCS_DD_DIAG(JLON,JLEV)=-ZGDTI*ZAUX*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV)-ZGDTI*ZAUX*0.5_JPRB*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1))
          PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV)-ZGDTI*ZAUX*0.5_JPRB*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1))
          DO JTRA=1,KTRA
            PSTRCTRA(JLON,JLEV,JTRA)= PSTRCTRA(JLON,JLEV,JTRA) & 
              & -ZGDTI*ZAUX*0.5_JPRB*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA))
          ENDDO
       ENDDO
    ENDDO
    ENDIF  !LGDDD

  ENDIF ! LCVIMPT.

  ! // SETTING ALL RESULTS TO ZERO IN CASE OF NEGATIVE PRECIPITATIONS AT
  ! // THE SURFACE OR OF "KNND" ALREADY ZERO.

  DO JLON=KIDIA,KFDIA
    ZZVAL=PFCCQL(JLON,KLEV)+PFCCQI(JLON,KLEV)-YRPHY0%SCO
    KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZZVAL))
  ENDDO

  IF(YRPHY0%LCVNAUV) THEN
    ! RECOMPUTE PSTRCU AND PSTRCV FROM AN IDEA INSPIRED BY FRANCOIS BOUYSSEL (2012-08-01) AND 3MT.
    DO JLON=KIDIA,KFDIA
     ZNND(JLON)=1.0_JPRB-REAL(KNLAB(JLON,KLEV))
     ZI1(JLON,KLEV)=REAL(KNLAB(JLON,KLEV))
     ZUM(JLON,KLEV)=PU(JLON,KLEV)
     ZVM(JLON,KLEV)=PV(JLON,KLEV)
     ZBET(JLON,KLEV)=REAL(KNLAB(JLON,KLEV))
     ZA13(JLON,KLEV)=PU(JLON,KLEV)
     ZA14(JLON,KLEV)=PV(JLON,KLEV)
     DO JTRA=1,KTRA
       ZTRAM(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
       ZA15(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
     ENDDO
    ENDDO
    DO JLEV=KLEV-1,ITOP,-1
     DO JLON=KIDIA,KFDIA
      ZI1(JLON,JLEV)=REAL(KNLAB(JLON,JLEV)*ZNND(JLON))
      ZNND(JLON)=MIN(ZNND(JLON),1.0_JPRB-REAL(KNLAB(JLON,JLEV)))
      ZBAS=REAL(KNLAB(JLON,JLEV)*(1.0_JPRB-REAL(KNLAB(JLON,JLEV+1))),JPRB)
      ZNBA=REAL(KNLAB(JLON,JLEV)*KNLAB(JLON,JLEV+1),JPRB)
      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX)+ZBAS
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
       &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
       &+YRPHY0%TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
       &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
       &+YRPHY0%TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA)=(ZTRAM(JLON,JLEV+1,JTRA)&
         &+(ZMIX*(PTRA(JLON,JLEV+1,JTRA)-ZTRAM(JLON,JLEV+1,JTRA))&
         &+YRPHY0%TUDGP*(PTRA(JLON,JLEV,JTRA)-PTRA(JLON,JLEV+1,JTRA)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
         &+PTRA(JLON,JLEV,JTRA)*(1.0_JPRB-ZNBA)
        ZA15(JLON,JLEV,JTRA)=ZA15(JLON,JLEV+1,JTRA)*ZNBA+PTRA(JLON,JLEV,JTRA)*ZBAS
      ENDDO
     ENDDO
    ENDDO
    DO JLEV=ITOP,KLEV
     DO JLON=KIDIA,KFDIA
      ZZ=1.0_JPRB-ZBET(JLON,JLEV)
      ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
      ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
      ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
      ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA)=ZZ*ZTRAM(JLON,JLEV,JTRA)+ZBET(JLON,JLEV)*ZA15(JLON,JLEV,JTRA)
        ZTRAM(JLON,JLEV,JTRA)=PTRA(JLON,JLEV,JTRA)-ZTRAM(JLON,JLEV,JTRA)
      ENDDO
     ENDDO
    ENDDO
    DO JLON = KIDIA, KFDIA
    ZNND(JLON) = 1.0_JPRB
    ENDDO

    DO JLEV = 0, KLEV
    DO JLON = KIDIA, KFDIA
    PSTRCU(JLON,JLEV) = 0._JPRB
    ENDDO
    ENDDO

    DO JLEV = 0, KLEV
    DO JLON = KIDIA, KFDIA
    PSTRCV(JLON,JLEV) = 0._JPRB
    ENDDO
    ENDDO

    DO JTRA = 1, KTRA
    DO JLEV = 0, KLEV
    DO JLON = KIDIA, KFDIA
    PSTRCTRA(JLON,JLEV,JTRA) = 0._JPRB
    ENDDO
    ENDDO
    ENDDO

    DO JLEV=ITOP,KLEV-1
     DO JLON=KIDIA,KFDIA
      ZNND(JLON) = MIN(ZNND(JLON),1.0_JPRB-ZI1(JLON,JLEV))
      ZZ=(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV)) &
      & /(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV-1))
      ZAUX=YRPHY0%TVFC*ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV)+YRPHY0%TVFC*ZFORM(JLON,JLEV))
      ZDPSGDT=PDELP(JLON,JLEV)*ZGDTI*0.5_JPRB
      PSTRCU(JLON,JLEV)=ZNND(JLON)*ZAUX*( PSTRCU(JLON,JLEV-1)+ &
       & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )+ &
       & (1.0_JPRB-ZNND(JLON))*PSTRCU(JLON,JLEV-1)*ZZ
      PSTRCV(JLON,JLEV)=ZNND(JLON)*ZAUX*( PSTRCV(JLON,JLEV-1)+ &
       & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )+ &
       & (1.0_JPRB-ZNND(JLON))*PSTRCV(JLON,JLEV-1)*ZZ
      DO JTRA=1,KTRA
        PSTRCTRA(JLON,JLEV,JTRA)=ZNND(JLON)*ZAUX*( PSTRCTRA(JLON,JLEV-1,JTRA)+ &
         & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )+ &
         & (1.0_JPRB-ZNND(JLON))*PSTRCTRA(JLON,JLEV-1,JTRA)*ZZ
      ENDDO
     ENDDO
    ENDDO
  ENDIF

  ! ------------------------------------------------------------------
  ! BEFORE LEAVING
  ! - LEAVE IN PUDOM THE RELATIVE UPDRAFT VELOCITY FOR ADVECTION
  ! - RESET ALL FLUXES TO ZERO WHERE KNND IS
  ! AND ALSO RESET PUDOM AND PUDAL
  ! ------------------------------------------------------------------

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=KNND(JLON)*PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=KNND(JLON)*PFPLCN(JLON,JLEV)
      PDIFCQ(JLON,JLEV)=KNND(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCQL(JLON,JLEV)=KNND(JLON)*PDIFCQL(JLON,JLEV)
      PDIFCQI(JLON,JLEV)=KNND(JLON)*PDIFCQI(JLON,JLEV)
      PDIFCS(JLON,JLEV)=KNND(JLON)*PDIFCS(JLON,JLEV)
      PDIFCTH(JLON,JLEV)=KNND(JLON)*PDIFCTH(JLON,JLEV)
      PSTRCU(JLON,JLEV)=KNND(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=KNND(JLON)*PSTRCV(JLON,JLEV)
      PFCCQL(JLON,JLEV)=KNND(JLON)*PFCCQL(JLON,JLEV)
      PFCCQI(JLON,JLEV)=KNND(JLON)*PFCCQI(JLON,JLEV)
      PFPEVPCL(JLON,JLEV)=KNND(JLON)*PFPEVPCL(JLON,JLEV)
      PFPEVPCN(JLON,JLEV)=KNND(JLON)*PFPEVPCN(JLON,JLEV)
      PFHEVPP(JLON,JLEV)=KNND(JLON)*PFHEVPP(JLON,JLEV)
      PFHMLTS(JLON,JLEV)=KNND(JLON)*PFHMLTS(JLON,JLEV)
      DO JTRA=1,KTRA
        PSTRCTRA(JLON,JLEV,JTRA)=KNND(JLON)*PSTRCTRA(JLON,JLEV,JTRA)
      ENDDO
    ENDDO
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      KNLAB(JLON,JLEV)=KNND(JLON)*KNLAB(JLON,JLEV)
      PQLIC(JLON,JLEV)=KNND(JLON)*PQLIC(JLON,JLEV)*YRPHY0%FQLIC*PNEBC(JLON,JLEV)*KNLAB(JLON,JLEV)
      PNEBC(JLON,JLEV)=KNND(JLON)*MAX(ZEPS0,MIN(ZNEBCX,PNEBC(JLON,JLEV)*YRPHY0%FNEBC&
      &*NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PQLIC(JLON,JLEV)+0.0_JPRB)))))
    ENDDO
  ENDDO

  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      PUDAL(JLON,JLEV)=KNND(JLON)*PUDAL(JLON,JLEV)
      PDDAL(JLON,JLEV)=KNND(JLON)*PDDAL(JLON,JLEV)

      IF(.NOT.YRPHY0%LCVNHD) THEN
      ! VERTICAL VELOCITY IN M/S COMPUTED FROM VERTICAL VELOCITY IN PA/S.
        ZWU(JLON,JLEV)=-PUDOM(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
        ZWD(JLON,JLEV)=-PDDOM(JLON,JLEV)*PR(JLON,JLEV)*ZT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
      ENDIF

      IQLIC(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PQLIC(JLON,JLEV)+0.0_JPRB)))
      ! ENTRAINMENT/DETRAINMENT (IN S**-1).
      PENTR_U(JLON,JLEV)=ZWU(JLON,JLEV)*PUDAL(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)*IQLIC(JLON)
      PDETR_U(JLON,JLEV)=KNLAB(JLON,JLEV)*ZWU(JLON,JLEV)*PUDAL(JLON,JLEV)*ZDELTA_U(JLON,JLEV) &
         &+(1-KNLAB(JLON,JLEV))*YRPHY0%GCVENTR_MIN
      PENTR_D(JLON,JLEV)=-ZWD(JLON,JLEV)*PDDAL(JLON,JLEV) &
         &*(ZEPS_ORGD(JLON,JLEV)*PAPRSF(JLON,JLEV) &
         &/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZENTRX)*RG*IQLIC(JLON)*YRPHY0%GCVFRR
      PDETR_D(JLON,JLEV)=-ZWD(JLON,JLEV)*PDDAL(JLON,JLEV) &
         &*(ZDEL_ORGD(JLON,JLEV)*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*ZT(JLON,JLEV))+ZENTRX)*RG*YRPHY0%GCVFRR
    ENDDO
  ENDDO

  ! -------------------------------------------------
  ! NUMERICAL VERTICAL FILTER OF TRANSPORT FLUXES
  ! TO AVOID 2-VERTICAL-LEVELS INSTABILITY.
  ! -------------------------------------------------

  ZA=0.5_JPRB
  ZA2=ZA*0.5_JPRB
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
       PDIFCQ(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQ(JLON,JLEV)+ZA2*(PDIFCQ(JLON,JLEV-1)+PDIFCQ(JLON,JLEV+1))
       PDIFCQI(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQI(JLON,JLEV)+ZA2*(PDIFCQI(JLON,JLEV-1)+PDIFCQI(JLON,JLEV+1))
       PDIFCQL(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQL(JLON,JLEV)+ZA2*(PDIFCQL(JLON,JLEV-1)+PDIFCQL(JLON,JLEV+1))
       PDIFCS(JLON,JLEV)=(1._JPRB-ZA)*PDIFCS(JLON,JLEV)+ZA2*(PDIFCS(JLON,JLEV-1)+PDIFCS(JLON,JLEV+1))
       PDIFCTH(JLON,JLEV)=(1._JPRB-ZA)*PDIFCTH(JLON,JLEV)+ZA2*(PDIFCTH(JLON,JLEV-1)+PDIFCTH(JLON,JLEV+1))
       PSTRCU(JLON,JLEV)=(1._JPRB-ZA)*PSTRCU(JLON,JLEV)+ZA2*(PSTRCU(JLON,JLEV-1)+PSTRCU(JLON,JLEV+1))
       PSTRCV(JLON,JLEV)=(1._JPRB-ZA)*PSTRCV(JLON,JLEV)+ZA2*(PSTRCV(JLON,JLEV-1)+PSTRCV(JLON,JLEV+1))
       DO JTRA=1,KTRA
         PSTRCTRA(JLON,JLEV,JTRA)=(1._JPRB-ZA)*PSTRCTRA(JLON,JLEV,JTRA)+ZA2*(PSTRCTRA(JLON,JLEV-1,JTRA)+&
                                 & PSTRCTRA(JLON,JLEV+1,JTRA))
       ENDDO
    ENDDO
  ENDDO

  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
       PMF_UP(JLON,JLEV)=ZGDTI*ZFORM(JLON,JLEV)  ! "mass flux" for implicit formulation
    ENDDO
  ENDDO
ENDIF ! IF ITOP < KLEV+1

!
!-------------------------------------------------
! Momentum is not transported as thermodynamic variables (theta, qt).
!-------------------------------------------------
!
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    PSTRCU(JLON,JLEV)=YRPHY0%GCVUV*PSTRCU(JLON,JLEV)
    PSTRCV(JLON,JLEV)=YRPHY0%GCVUV*PSTRCV(JLON,JLEV)
  ENDDO
ENDDO


!----------------------------------------------------------------
! ERASE PCAPE COMPUTED PREVIOUSLY IF NCAPEMOD DIFFERENT FROM 0 :
!----------------------------------------------------------------
! NCAPEMOD.EQ.0 : the previous value of PCAPE is retained
! NCAPEMOD.EQ.1 : call FPCINCAPE  with the mask "KNND(JLON)=1"
! NCAPEMOD.EQ.2 : call FPCINCAPE  with the mask "ZDIAG_WMAX>WCAPEMOD"
!
IF (YRPHY0%NCAPEMOD /= 0) THEN
  !
  !- - - - - - - - - - - - - - -
  ! Set the "SBCAPE" to zero :
  !- - - - - - - - - - - - - - -
  DO JLON = KIDIA, KFDIA
  PCAPE(JLON)=0.0_JPRB
  ENDDO
  !
  !- - - - - - - - - - - - - - - - - - - - -
  ! Compute the "SBCAPE" for the parcel at 
  ! the lowest atmospheric model level :
  !- - - - - - - - - - - - - - - - - - - - -
  CALL FPCINCAPE(KIDIA,KFDIA,KLON,KLEV,KLEV, &
      & PT,PAPRSF,PQ,PCAPE,ZCIN,ILCL,IFCL,IEL)
  !
  !- - - - - - - - - - - - - - - - - - - - - - -
  ! This "SBCAPE" is set to zero where PCMT is 
  ! "inactive" :
  !- - - - - - - - - - - - - - - - - - - - - - -
  IF (YRPHY0%NCAPEMOD == 1) THEN
    ! "inactive" if : KNND(JLON)=0.0
    DO JLON=KIDIA,KFDIA
      PCAPE(JLON)=MAX(0._JPRB,PCAPE(JLON)) * &
      &           FLOAT(KNND(JLON))
    ENDDO
  ELSEIF (YRPHY0%NCAPEMOD == 2) THEN
    ! "inactive" if : ZDIAG_WMAX(JLON) < WCAPEMOD
    DO JLON=KIDIA,KFDIA
      PCAPE(JLON)=MAX(0._JPRB,PCAPE(JLON)) * &
      &           MAX(0._JPRB,SIGN(1._JPRB, &
      &           ZDIAG_WMAX(JLON)-YRPHY0%WCAPEMOD))
    ENDDO
  ENDIF
  !
ENDIF ! NCAPEMOD



END SUBROUTINE ACMTUD

