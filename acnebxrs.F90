!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACNEBXRS ( KIDIA,KFDIA,KGPBLKS,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PQ,PQC,PQSAT,&
 ! - OUTPUT 2D .
 & PNEB)  

!**** *ACNEBXR* - CALCUL DE LA NEBULOSITE XU-RANDALL.
!                 COMPUTATION OF XU-RANDALL CLOUDINESS.

!     Sujet.
!     ------
!     CALCUL DE LA NEBULOSITE CONFORMEMENT A XU ET RANDALL, JAS 96.
!     CLOUDINESS COMPUTATION, AS IN XU AND RANDALL, JAS 96.

!**   Interface.
!     ----------
!        *CALL* *ACNEBXRS*

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA, KFDIA : BORNES BOUCLES HORIZONTALES   (IST,IEND DANS CPG).
! KIDIA, KFDIA : START/END OF HORIZONTAL LOOP  (IST,IEND IN *CPG*).
! KLON : DIMENSION HORIZONTALE                 (NPROMA DANS CPG).
! KLON : HORIZONTAL DIMENSION                  (NPROMA IN *CPG*).
! KTDIA : DEBUT BOUCLE VERTICALE DANS LA PHYSIQUE.
! KTDIA : START OF THE VERTICAL LOOP IN THE PHYSICS (IF SOME LEVELS ARE
!                     SKIPPED AT THE TOP OF THE MODEL).
! KLEV : FIN BOUCLE VERTICE ET DIMENSION VERTICALE (NFLEV DANS CPG).
! KLEV : END OF VERTICAL LOOP AND VERTICAL DIMENSION(NFLEV IN *CPG*).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQC        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE (LIQUIDE + GLACE).
! PQC        : SPECIFIC HUMIDITY OF CONDENSATED WATER.
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PQSAT      : SPECIFIC HUMIDITY AT SATURATION.

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (1:KLEV) .

! PNEB       : NEBULOSITE PARTIELLE "RADIATIVE".
! PNEB       : FRACTIONAL CLOUDINESS FOR RADIATION.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY0/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2002-01, J.M. Piriou.

!     Modifications.
!     --------------
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        R.Brozkova    05-Nov-2004 new RH function

!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE YOMPHY   , ONLY : YRPHY
USE YOMPHY0  , ONLY : YRPHY0
IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KGPBLKS,KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQC(KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEB(KLON,KLEV,KGPBLKS) 
INTEGER(KIND=JPIM) :: JLEV, JBLK,JLON

REAL(KIND=JPRB) :: ZEPS1, ZRH, ZRHLIM, ZNEB, ZBIN, ZARGLI, ZRHEXP

!*
!     ------------------------------------------------------------------
!     I - CALCUL DE LA NEBULOSITE.
!         CLOUDINESS COMPUTATION.


ZEPS1=1.E-6_JPRB
ZARGLI=125._JPRB**(1.0_JPRB/YRPHY0%QXRTGH)

IF (YRPHY%LQXRTGH) THEN
  !$acc parallel loop gang vector collapse (2) vector_length (KLON) private (JBLK, JLEV, JLON, ZBIN, ZNEB, ZRH, ZRHEXP, ZRHLIM) 
  DO JBLK = 1, KGPBLKS
  DO JLON = KIDIA, KFDIA
  DO JLEV=KTDIA,KLEV
    
      ZRH=MIN(ZARGLI,MAX(ZEPS1,PQ(JLON,JLEV,JBLK)/PQSAT(JLON,JLEV,JBLK)))
      ZRHEXP=EXP(-2.0_JPRB*ZRH**YRPHY0%QXRTGH)
      ZRH=((1.0_JPRB-ZRHEXP)/(1.0_JPRB+ZRHEXP))**(1.0_JPRB/YRPHY0%QXRTGH)
      ZRHLIM=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZRH))
      ZNEB=ZRHLIM**YRPHY0%QXRR*(1.0_JPRB-&
    &  EXP(-YRPHY0%QXRAL*PQC(JLON,JLEV,JBLK)/((1.0_JPRB-ZRHLIM)*PQSAT(JLON,JLEV,JBLK))**YRPHY0%QXRDEL))
      ZBIN=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRH-1.0_JPRB))
      PNEB(JLON,JLEV,JBLK)=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZBIN+(1.0_JPRB-ZBIN)*ZNEB))
    
  ENDDO
  ENDDO
  ENDDO

ELSE
  !$acc parallel loop gang vector collapse (2) vector_length (KLON) private (JBLK, JLEV, JLON, ZBIN, ZNEB, ZRH, ZRHLIM) 
  DO JBLK = 1, KGPBLKS
  DO JLON = KIDIA, KFDIA
  DO JLEV=KTDIA,KLEV
    
      ZRH=MIN(YRPHY0%QXRHX,PQ(JLON,JLEV,JBLK)/PQSAT(JLON,JLEV,JBLK))
      ZRHLIM=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZRH))
      ZNEB=ZRHLIM**YRPHY0%QXRR*(1.0_JPRB-&
    &  EXP(-YRPHY0%QXRAL*PQC(JLON,JLEV,JBLK)/((1.0_JPRB-ZRHLIM)*PQSAT(JLON,JLEV,JBLK))**YRPHY0%QXRDEL))
      ZBIN=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRH-1.0_JPRB))
      PNEB(JLON,JLEV,JBLK)=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZBIN+(1.0_JPRB-ZBIN)*ZNEB))
    
  ENDDO
  ENDDO
  ENDDO

ENDIF


END SUBROUTINE ACNEBXRS
