SUBROUTINE ACMTENTR ( KIDIA,KFDIA,KGPBLKS,KLON,KTDIA,KLEV,&
!-----------------------------------------------------------------------
! - INPUT  2D .
&PDELP,PPSIC,PPSIR,PUDAL,PDDAL,PENTR_U,PENTR_D,PDETR_U,PDETR_D,&
! - OUTPUT 2D .
&PFCED)

!**** *ACMTENTR * - FLUX DUE TO ENTRAINMENT AND DETRAINMENT PROCESSES.

!     Sujet.
!     ------

!**   Interface.
!     ----------
!        *CALL* *ACMTENTR*
!-----------------------------------------------------------------------

! - PHYSICS DIMENSIONNING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".


! - 2D (1:KLEV) .

! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER

! - 2D (0:KLEV) .

! PFCED      : FLUX OF PSI DUE TO THE SUM OF ENTRAINEMENT AND DETRAINMENT PROCESSES.

!-----------------------------------------------------------------------

!     Method.
!     --------

!     Author.
!     -------
!        2011-04-19, J.M. Piriou.

!     Modifications.
!     --------------

!-----------------------------------------------------------------------

USE PARKIND1, ONLY: JPIM, JPRB

USE YOMCST   , ONLY : RG
USE YOMPHY2  , ONLY : YRPHY2

IMPLICIT NONE

INTEGER(KIND=JPIM), INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KLEV
INTEGER(KIND=JPIM), INTENT(IN) :: KGPBLKS,KLON
INTEGER(KIND=JPIM), INTENT(IN) :: KTDIA

REAL(KIND=JPRB), INTENT(IN)    :: PDELP(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PPSIC(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PPSIR(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PUDAL(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PDDAL(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PENTR_U(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PENTR_D(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PDETR_U(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN)    :: PDETR_D(KLON,KLEV,KGPBLKS)

REAL(KIND=JPRB), INTENT(OUT)   :: PFCED(KLON,0:KLEV,KGPBLKS)

REAL(KIND=JPRB) :: ZINC,ZMINVALUE_ZINC,ZMAXVALUE_ZINC
INTEGER(KIND=JPIM) :: JLEV,JBLK,JLON



!-------------------------------------------------
! INITIALIZATION.
!-------------------------------------------------

DO JBLK = 1, KGPBLKS
DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA
  
    PFCED(JLON,JLEV,JBLK)=0._JPRB
  
ENDDO
ENDDO
ENDDO

DO JBLK = 1, KGPBLKS
DO JLEV=KTDIA,KLEV
DO JLON = KIDIA, KFDIA
  
    
    ! PSIC INCREMENT.
    ZINC=((PENTR_U(JLON,JLEV,JBLK)+PENTR_D(JLON,JLEV,JBLK)) &
      & *PPSIR(JLON,JLEV,JBLK)/MAX(1.E-4_JPRB,1._JPRB-PUDAL(JLON,JLEV,JBLK)-PDDAL(JLON,JLEV,JBLK)) &
      & -(PDETR_U(JLON,JLEV,JBLK)+PDETR_D(JLON,JLEV,JBLK)) &
      & *PPSIC(JLON,JLEV,JBLK)/MAX(1.E-4_JPRB,PUDAL(JLON,JLEV,JBLK)+PDDAL(JLON,JLEV,JBLK)))*YRPHY2%TSPHY

    ! PROTECTION IN CASE OF LARGE TIME STEPS:
    ! TO AVOID INSTABILITIES
    ! THIS ZINC INCREMENT HAS TO BE SUCH THAT THE FINAL PSIC VALUE
    ! IN THE END OF TIME STEP WILL BE BETWEEN 0 AND (PSIC+PSIR).
    ! THEREFORE, THE INCREMENT HAS TO BE BETWEEN -PSIC AND PSIR.
    ZMINVALUE_ZINC=-PPSIC(JLON,JLEV,JBLK)
    ZMAXVALUE_ZINC=PPSIR(JLON,JLEV,JBLK)
    ZINC=MAX(ZMINVALUE_ZINC,MIN(ZMAXVALUE_ZINC,ZINC))
    PFCED(JLON,JLEV,JBLK)=PFCED(JLON,JLEV-1,JBLK)-PDELP(JLON,JLEV,JBLK)/RG*ZINC/YRPHY2%TSPHY
  
ENDDO
ENDDO
ENDDO



END SUBROUTINE ACMTENTR
