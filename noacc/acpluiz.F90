!-----------------------------------------------------------------------
SUBROUTINE ACPLUIZ( KIDIA, KFDIA, KGPBLKS,KLON, KTDIA, KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT -
 & PT, PQ, PQL, PQI, PQR, PQS,&
 & PDELP, PAPRSF, PCP, PR, PNEBS, PQCS, PNEB_CVPP, PQLI_CVPP,PQC_DET_PCMT,&
 & PTENDH, PTENDQ, LDADJCLD, PAPHI,&
 & PTS, PNEIJ, PLSM, PGM, PVETAF,&
 ! - OUTPUT -
 & PFCSQL, PFCSQN, PFPLSL, PFPLSN,&
 & PFPEVPL, PFPEVPN, PFPFPL, PFPFPN, PSEDIQL, PSEDIQN )

!**** *ACPLUIZ * - PROGNOSTIC CLOUD SCMEME (LOPEZ)

!**   Interface.
!     ----------
!        *CALL* *ACPLUIZ*

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE./INPUT ARGUMENTS.
!     ------------------------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
!            : START OF HORIZONTAL LOOP 
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
!            : END OF HORIZONTAL LOOP
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
!            : HORIZONTAL DIMENSION  
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES. 
!            : START OF THE VERTICAL LOOP IN THE PHYSICS. 
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
!            : END OF VERTICAL LOOP AND VERTICAL DIMENSION

! - 2D (1:KLEV)

! PT         : TEMPERATURE.
!            : TEMPERATURE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
!            : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQL        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE LIQUIDE.
!            : SPECIFIC HUMIDITY OF LIQUID CONDENSATED WATER.
! PQI        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE GLACE.
!            : SPECIFIC HUMIDITY OF SOLID CONDENSATED WATER.
! PQR        : PRECIPITATIONS LIQUIDES.
!            : LIQUID PECIPITATIONS.
! PQS        : PRECIPITATIONS SOLIDES.
!            : SOLID PECIPITATIONS.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
!            : LAYER THICKNESS IN PRESSURE UNITS.  
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
!            : PRESSURE ON FULL LEVELS.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
!            : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
!            : GAS CONSTANT FOR AIR.
! PNEBS      : NEBULOSITE PARTIELLE STRATIFORME.
!            : STRATIFORM FRACTIONAL CLOUDINESS.
! PQCS       : CONTENU "STRATIFORME" EN CONDENSAT NUAGEUX (LIQ. + SOL.).
!            : STRATIRORM CLOUD WATER (LIQUID + SOLID).
! PNEB_CVPP  : NEBULOSITE PARTIELLE CONVECTION PEU PROFONDE.
!            : SHALLOW CONVECTION FRACTIONAL CLOUDINESS.
! PQLI_CVPP  : CONTENU "CVPP" EN CONDENSAT NUAGEUX (LIQ. + SOL.).
!            : SHALLOW CONVECTION CLOUD WATER (LIQUID + SOLID).
! PQC_DET_PCMT: CONTENU EN CONDENSAT NUAGEUX DETRAINE PAR PCMT (LIQ. + SOL.).
!            : DEEP CONVECTION DETRAINED CLOUD WATER (LIQUID + SOLID).
! PTENDH     : TENDANCE D'ENTHALPIE.
!            : ENTHALPY TENDENCY.
! PTENDQ     : TENDANCE D'HUMIDITE SPECIFIQUE.
!            : SPECIFIC HUMIDITY TENDENCY.

! - 2D (0:KLEV)

! PAPHI      : GEOPOTENTIEL SUR DEMI-NIVEAUX.
!            : GEOPOTENTIAL ON HALF-LEVELS.

! - 1D (1:KLON) .
                                                                                
! PTS        : TEMPERATURE DE SURFACE.
!            : SURFACE TEMPERATURE.
! PNEIJ      : PROPORTION DE LA MAILLE RECOUVERTE DE NEIGE.
!            : SNOW FRACTION.
! PLSM       : INDICE TERRE/MER.
!            : LAND/SEA MASK.
! PGM        : FACTEUR D'ECHELLE.
!            : MAPPING FACTOR.
! PVETAF     : COORDONNEE VERTICALE ETA.
!            : VERTICAL COORDINATE ETA.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PFCSQN     : FLUX DE CONDENSATION LIE AUX RR STRATIFORMES NEIGEUSES.
!            : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL     : FLUX DE CONDENSATION LIE AUX RR STRATIFORMES LIQUIDES.
!            : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PFPLSL     : FLUX DE PRECIPITATION LIQUIDE (PLUIE).
!            : RAIN FLUX.
! PFPLSN     : FLUX DE PRECIPITATION SOLIDE  (NEIGE).
!            : ICE PRECIPITATION FLUX.
! PFPEVPL    : FLUX ASSOCIE A L'EVAPORATION DES PRECIP.
!            : FLUX ASSOCIATED TO EVAPORATION OF PRECIPITATIONS.
! PFPEVPN    : FLUX ASSOCIE A LA SUBLIMATION DES PRECIP.
!            : FLUX ASSOCIATED TO SUBLIMATION OF PRECIPITATIONS.
! PFPFPL     : FLUX DE GENERATION DE PRECIPITATIONS LIQUIDES.
!            : FLUX OF LIQUID PRECIPITATION GENERATION.
! PFPFPN     : FLUX DE GENERATION DE PRECIPITATIONS SOLIDES.
!            : FLUX OF SOLID PRECIPITATION GENERATION.
! PSEDIQL    : FLUX SEDIMENTATION D'EAU LIQUIDE NUAGEUSE.
!            : FLUX SEDIMENTATION OF CLOUD LIQUID WATER.
! PSEDIQN    : FLUX SEDIMENTATION D'EAU SOLIDE NUAGEUSE.
!            : FLUX SEDIMENTATION OF CLOUD SOLID WATER.

!-----------------------------------------------------------------------

!     Auteur.
!     -------
!         04-10, Francois Bouyssel

!     Modifications.
!     --------------
!         05-05, F.Bouyssel : Introduction of vertical coordinate eta
!         05-07, F.Bouyssel : New arguments from aplpar to acmicro
!         05-11, F.Bouyssel : Remove ZAUTO,ZQCMIC,ZQPMIC
!         06-01, F.Bouyssel : Introduction of PQS, PFPEVPL, PFPEVPN
!         06-01, F.Bouyssel : Microphysical adjustment at the beginning
!         06-04, F.Bouyssel : Key for Smith's adjustment LADJCLD 
!         06-06, Y.Bouteloup: Statistical sedimentation (ADVPRCS)
!         09-10, F.Bouyssel : Cleaning ADVPRC and ACNEBSM
!         10-04, Y.Bouteloup: No rain production by autoconversion in
!                             case of negative temperature
!         10-07, F.Bouyssel : Bug correction related with ZDPSGDT
!         10-12, F.Bouyssel : Introduction of LADJCLD
!         11-01, F.Bouyssel : Bug correction in case of negative temperature
!       2011-06, M. Jerczynski : some cleaning to meet norms
!       2014-04, JM Piriou : add detrained condensate from PCMT convection
!       2016-06, Y. Bouteloup : adjustment done as a function of a dummy logical argument, instead of global.

!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE YOMPHY    , ONLY : YRPHY
USE YOMPHY0   , ONLY : YRPHY0
USE YOMPHY2   , ONLY : YRPHY2
USE YOMCST    , ONLY : RG   , RTT  , RDT

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KGPBLKS,KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT     (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ     (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL    (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI    (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQR    (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS    (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP  (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP    (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR     (KLON,KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEBS  (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQCS   (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEB_CVPP(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLI_CVPP(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQC_DET_PCMT(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDQ (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI  (KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS    (KLON,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ  (KLON,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM   (KLON,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM   (KLON,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVETAF(KLEV)

LOGICAL           ,INTENT(IN)    :: LDADJCLD

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQL (KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQN (KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL (KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN (KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPL(KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPN(KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPL (KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPN (KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSEDIQL(KLON,0:KLEV,KGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSEDIQN(KLON,0:KLEV,KGPBLKS) 

REAL(KIND=JPRB) :: ZRHCRI (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZRMF   (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZQSATS (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZT     (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZQ     (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZAUTOL (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZAUTOI (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZQL    (KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZQI    (KLON,KLEV,KGPBLKS)

REAL(KIND=JPRB) :: ZFLUXCOR(KLON,KLEV,KGPBLKS)

REAL(KIND=JPRB) :: ZDPSGDT,ZICE,ZGDT,ZGDTI,ZIGEL,ZEPS1
INTEGER(KIND=JPIM) :: JBLK,JLON,JLEV

#include "acmicro.intfb.h"
#include "acnebsm.intfb.h"
#include "advprcs.intfb.h"

#include "fctdoi.func.h"

!     ------------------------------------------------------------------

!     CHECK RELIABILITY OF INPUT ARGUMENTS.



!     ------------------------------------------------------------------

! - - - - - - - -
! ADJUSTMENT
! - - - - - - - -

ZEPS1=1.E-12_JPRB

IF ( LDADJCLD ) THEN

  DO JBLK = 1, KGPBLKS
  DO JLON = KIDIA, KFDIA
  DO JLEV = KTDIA, KLEV
    
      ZT(JLON,JLEV,JBLK) = PT(JLON,JLEV,JBLK)+YRPHY2%TSPHY*PTENDH(JLON,JLEV,JBLK)/PCP(JLON,JLEV,JBLK)
      ZQ(JLON,JLEV,JBLK) = MAX( 0.0_JPRB,PQ(JLON,JLEV,JBLK)+YRPHY2%TSPHY*PTENDQ(JLON,JLEV,JBLK) )
    
  ENDDO
  ENDDO
  ENDDO


  CALL ACNEBSM ( KIDIA,KFDIA,KGPBLKS,KLON,KTDIA,KLEV,&
               & ZT,ZQ,PQL,PQI,PAPHI,PAPRSF,PCP,PR,&
               & PGM,PVETAF,&
               & PQCS,PNEBS,ZRHCRI,ZRMF,ZQSATS )

ELSE

  DO JBLK = 1, KGPBLKS
  DO JLON = KIDIA, KFDIA
  DO JLEV = KTDIA, KLEV
    
      ZT(JLON,JLEV,JBLK) = PT(JLON,JLEV,JBLK)
      ZQ(JLON,JLEV,JBLK) = PQ(JLON,JLEV,JBLK)
    
  ENDDO
  ENDDO
  ENDDO


ENDIF

! CLOUD OVERLAP FOR STRATIFORM AND SHALLOW CLOUDS
! -----------------------------------------------
                                                                                
DO JBLK = 1, KGPBLKS
DO JLON = KIDIA, KFDIA
DO JLEV = KTDIA, KLEV
  
    ZICE = FONICE(ZT(JLON,JLEV,JBLK))
    PQCS (JLON,JLEV,JBLK) = PQCS(JLON,JLEV,JBLK) + PQLI_CVPP(JLON,JLEV,JBLK) + PQC_DET_PCMT(JLON,JLEV,JBLK)
    PNEBS(JLON,JLEV,JBLK) = MIN(1.0_JPRB-ZEPS1,MAX(PNEBS(JLON,JLEV,JBLK),ZEPS1))
    PNEBS(JLON,JLEV,JBLK) = MAX( PNEBS(JLON,JLEV,JBLK) , PNEB_CVPP(JLON,JLEV,JBLK) )
    ZQL  (JLON,JLEV,JBLK) = PQCS(JLON,JLEV,JBLK)*(1.0_JPRB-ZICE)
    ZQI  (JLON,JLEV,JBLK) = PQCS(JLON,JLEV,JBLK)*ZICE
  
ENDDO
ENDDO
ENDDO


! - - - - - - - -
! AUTOCONVERSION
! - - - - - - - -

CALL ACMICRO ( KIDIA, KFDIA, KGPBLKS,KLON, KTDIA, KLEV,&
             & PNEBS, ZT, ZQL, ZQI, PTS, PNEIJ, PLSM,&
             & ZAUTOL, ZAUTOI )

! - - - - - - - - - - - - - - - - - -
! In case of negative temperature 
! no rain production by autoconversion 
! - - - - - - - - - - - - - - - - - -

ZGDT=RG*YRPHY2%TSPHY
ZGDTI=1.0_JPRB/ZGDT

DO JBLK = 1, KGPBLKS
DO JLON = KIDIA, KFDIA
DO JLEV = KTDIA, KLEV
  
    ZDPSGDT  = ZGDTI * PDELP(JLON,JLEV,JBLK)
    ZIGEL = MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PT(JLON,JLEV,JBLK)))
    ZFLUXCOR(JLON,JLEV,JBLK) = ZIGEL*ZAUTOL(JLON,JLEV,JBLK)
    ZAUTOL(JLON,JLEV,JBLK) = ZAUTOL(JLON,JLEV,JBLK) - ZFLUXCOR(JLON,JLEV,JBLK)
    ZAUTOI(JLON,JLEV,JBLK) = ZAUTOI(JLON,JLEV,JBLK) + ZFLUXCOR(JLON,JLEV,JBLK)
    ZQL(JLON,JLEV,JBLK) = ZQL(JLON,JLEV,JBLK) - ZFLUXCOR(JLON,JLEV,JBLK)*YRPHY2%TSPHY
    ZQI(JLON,JLEV,JBLK) = ZQI(JLON,JLEV,JBLK) + ZFLUXCOR(JLON,JLEV,JBLK)*YRPHY2%TSPHY
    PFCSQL(JLON,JLEV,JBLK) = PFCSQL(JLON,JLEV-1,JBLK) &
     & + ( ZQL(JLON,JLEV,JBLK) - PQL(JLON,JLEV,JBLK) ) * ZDPSGDT
    PFCSQN(JLON,JLEV,JBLK) = PFCSQN(JLON,JLEV-1,JBLK) &
     & + ( ZQI(JLON,JLEV,JBLK) - PQI(JLON,JLEV,JBLK) ) * ZDPSGDT
  
ENDDO
ENDDO
ENDDO


! - - - - - - - - - - - - - - - - - - -
! FALLING OF PRECIPITATING PARTICLES,
! COLLECTION AND EVAPORATION PROCESSES.
! - - - - - - - - - - - - - - - - - - -

CALL ADVPRCS ( KIDIA, KFDIA, KGPBLKS,KLON, KTDIA, KLEV,&
             & ZT, ZQ, ZQL, ZQI, ZAUTOL, ZAUTOI,&
             & PQR, PQS, PNEBS,&
             & PCP, PR, PAPHI, PAPRSF, PDELP,&
             & PFPLSL , PFPLSN, PFPEVPL, PFPEVPN, PFPFPL, PFPFPN,&
             & PSEDIQL, PSEDIQN )


END SUBROUTINE ACPLUIZ
