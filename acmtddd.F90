! -----------------------------------------------------------------------
SUBROUTINE ACMTDDD( KIDIA,KFDIA,KGPBLKS,KLON,KTDIA,KLEV,KTRA,&
! -----------------------------------------------------------------------
! - INPUT  2D .
&KNCDN,KNLAB,&
&PALPH,PAPHIF,PAPRS,PAPRSF,&
&PDELP,PLH,PLNPR,PQ,PQLIC,PQLIS,&
&PQSAT,PQW,PR,PT,PTW,&
&PU,PV,PTRA,&
! - INPUT  1D .
&KLEVTEN,&
! - OUTPUT 2D .
&PBET,PDELA,PEPSI,PFORM,PQDN,&
&PQN2,PSDN,PSN2,PTN2,PUM,PVM,PTRAM,&
&PBCC_CD_FULL,&
! - OUTPUT 1D .
&KNLABD)

!**** *ACCVIMPD * - SCHEMA DE COURANTS DESCENDANTS A FLUX DE MASSE.


! Sujet.
! ------
!     - CALCUL DES FLUX DE CONDENSATION ET DE TRANSPORT CONVECTIFS
!       DESCENDANTS PAR UN SCHEMA A FLUX DE MASSE.

!     - MASS-FLUX DOWNDRAFT SCHEME, COMPUTING CONVECTIVE
!       CONDENSATION AND TRANSPORT FLUXES.

! **   Interface.
! ----------
! *CALL* *ACCVIMPD*

! -----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
! "APLPAR" CODE.
! -----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
! -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (NIVEL DU SOMMET
!            : DU NUAGE).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".


! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
! CATEGORIE).


! KNCDN      : NIVEAU DU POINT DE CONDENSATION.
! KNLAB      : INDICE D'ACTIVITE CONVECTIVE (NUAGE SI EGAL A UN).

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLH        : CHALEUR LATENTE A LA TEMPERATURE DE L'AIR.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQLIC      : EAU LIQUIDE OU SOLIDE CONVECTIVE.
! PQLIS      : EAU LIQUIDE OU SOLIDE STRATIFORME.
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D .

! KLEVTEN    : NIVEAU MINIMUM DE THETAE
! -----------------------------------------------------------------------

! -   ARGUMENTS EN SORTIE.
! ---------------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PFORM      : PROFIL DE FLUX DE MASSE.

! - 2D (1:KLEV) .

! PBET       : COEFFICIENT BETA DE LA VITESSE HORIZONTALE.
! PDELA      : COEFFICIENT DE DETRAINEMENT.
! PEPSI      : COEFFICIENT D'ENTRAINEMENT.
! PQDN       : HUMIDITE DE DETRAINEMENT DU COURANT DESCENDANT.
! PQN2       : HUMIDITE DU COURANT DESCENDANT.
! PSDN       : ENERGIE STATIQUE SECHE DE DETRAINEMENT DU C. DESCENDANT.
! PSN2       : ENERGIE STATIQUE SECHE DU C. DESCENDANT.
! PTN2       : TEMPERATURE DU C. DESCENDANT.
! PUM        : MOYENNE PONDEREE DE LA VITESSE U DEPUIS LA SURFACE.
! PVM        : MOYENNE PONDEREE DE LA VITESSE V DEPUIS LA SURFACE.
! PTRAM      : MOYENNE PONDEREE DE LA CONCENTRATION DE TRACEUR DEPUIS LA SURFACE.
! PBCC_CD_FULL : DERIVEE VERTICALE DE L'HUMIDITE SPECIFIQUE NUAGEUSE

! KNLABD     : INDICE DE COURANT DESCENDANT (C.D. SI EGAL A UN).

! -----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
! ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

! -----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------
!        SCHEMA A FLUX DE MASSE (J.F. GUEREMY)

!        MASS-FLUX SCHEME (J.F. GUEREMY)

!     Auteur.
!     -------
!        02-09, J.F. GUEREMY (à partir de accvimp codé par J.F. GELEYN)

!     Modifications.
!     --------------
!        04-11, Calcul de la subsidence compensatoire avec le courant
!               ascendant - J.F. GUEREMY
!        06-01, Modif variables de detrainement dans la couche
!               seche - J.F. GUEREMY
!        06-02, P. Marquet : set the same (?) NAMELIST tuning parameter 
!                            as done by Alias/Gueremy in cy30 codes
!                            ZGAMA = 1.5_JPRB      -> GAMAP1
!                            ZKD   = 240.E-06_JPRB -> RKDN*TENTRX/TENTR
!        06-03, KDN renamed to RKDN - A.Alias
!        09-07, remove CDLOCK + some cleanings -  K. Yessad
!        09-10, modif indice de courant descendant et fonction isign - J.F. GUEREMY
!        10-08, correction vit verticale conv - J.F. GUEREMY
!        14-04, tracer transport - D. SAINT-MARTIN
!        15-09, BUGFIX-J.F Guérémy

! -----------------------------------------------------------------------

USE PARKIND1, ONLY : JPIM, JPRB


USE YOMPHY   , ONLY : YRPHY
USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
            &RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
            &RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
            &RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
            &RGAMD
USE YOMPHY0  , ONLY : YRPHY0

! -----------------------------------------------------------------------

IMPLICIT NONE

!     DUMMY ARRAYS
INTEGER(KIND=JPIM), INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KLEV
INTEGER(KIND=JPIM), INTENT(IN) :: KGPBLKS,KLON
INTEGER(KIND=JPIM), INTENT(IN) :: KTDIA
INTEGER(KIND=JPIM), INTENT(IN) :: KTRA

INTEGER(KIND=JPIM), INTENT(IN) :: KNCDN(KLON,KLEV,KGPBLKS)
INTEGER(KIND=JPIM), INTENT(IN) :: KNLAB(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PALPH(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PAPHIF(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PAPRS(KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PAPRSF(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PDELP(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PLH(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PLNPR(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PQ(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PQLIC(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PQLIS(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PQSAT(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PQW(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PR(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PT(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PTW(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PU(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PV(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(IN) :: PTRA(KLON,KLEV,KTRA,KGPBLKS)
INTEGER(KIND=JPIM), INTENT(IN) :: KLEVTEN(KLON,KGPBLKS)

REAL(KIND=JPRB), INTENT(OUT) :: PBET(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PDELA(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PEPSI(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PFORM(KLON,0:KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PQDN(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PQN2(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PSDN(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PSN2(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PTN2(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PUM(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PVM(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB), INTENT(OUT) :: PTRAM(KLON,KLEV,KTRA,KGPBLKS)
REAL(KIND=JPRB), INTENT(INOUT) :: PBCC_CD_FULL(KLON,KLEV,KGPBLKS)
INTEGER(KIND=JPIM), INTENT(OUT) :: KNLABD(KLON,KLEV,KGPBLKS)

! -----------------------------------------------------------------------

!     LOCAL ARRAYS
INTEGER(KIND=JPIM) :: INIVBDC(KLON,KLEV,KGPBLKS)
REAL(KIND=JPRB) :: ZCP(KLON,KGPBLKS)&
    &,ZLH(KLON,KGPBLKS),ZMIX(KLON,KGPBLKS)&
    &,ZQB(KLON,KGPBLKS),ZQN(KLON,KGPBLKS)&
    &,ZRBB(KLON,KGPBLKS),ZRBH(KLON,KGPBLKS),ZRVH(KLON,KGPBLKS)&
    &,ZTB(KLON,KGPBLKS),ZTN(KLON,KGPBLKS)&
    &,ZVVERFM1(KLON,KGPBLKS)&
    &,ZVVER(KLON,0:KLEV,KGPBLKS),ZDWNDP(KLON,0:KLEV,KGPBLKS)&
    &,ZFMOD(KLON,0:KLEV,KGPBLKS),ZBCC_CD_HALF(KLON,0:KLEV,KGPBLKS)

REAL(KIND=JPRB) :: ZGAMA, ZKD, ZRVMD, ZCPVMD, ZCPVMW, ZCPVMS&
    &, ZEPSO, ZENTR, ZDELTA, ZDCP, ZDELT, ZDELQ, ZEW&
    &, ZESP, ZQW, ZDQW, ZCPS, ZTD, ZTNSEC, ZTVN, ZTVE, ZFLO, ZFLOI&
    &, ZDELPF, ZENTRT, ZVVERDEN, ZVVERN, ZVVERF, ZENTRO, ZTEST&
    &, ZDELTAP, ZB

!     LOCAL SCALAR
INTEGER(KIND=JPIM) :: JLEV, JBLK,JLON, ITOP, JIT, ICDN, IBDC, IVVER, IDOMDP&
    &, IKUO1, IKUO2, INUA, ILEVBDC, ISUM, ISDELTAP

INTEGER(KIND=JPIM) :: JTRA


#include "fcttrm.func.h"


!$acc data present (PTW, PU, PUM, PV, PVM)
!$acc data present (PQN2, PQSAT, PQW, PR, PSDN, PSN2, PT, PTN2, PTRA, PTRAM)
!$acc data present (PDELA, PDELP, PEPSI, PFORM, PLH, PLNPR, PQ, PQDN, PQLIC, PQLIS)
!$acc data present (KLEVTEN, KNCDN, KNLAB, KNLABD, PALPH, PAPHIF, PAPRS, PAPRSF, PBCC_CD_FULL, PBET)
!$acc data create (ZRBH, ZRVH, ZTB, ZTN, ZVVER, ZVVERFM1)
!$acc data create (INIVBDC, ZBCC_CD_HALF, ZCP, ZDWNDP, ZFMOD, ZLH, ZMIX, ZQB, ZQN, ZRBB)
! -----------------------------------------------------------------------

! *
! ------------------------------------------------------------------

!     I - CONSTANTES AUXILIAIRES.
!         AUXILIARY CONSTANTS.

! PARAMETRE DE MASSE VIRTUELLE + 1.
ZGAMA=YRPHY0%GAMAP1

! COEFFICIENT DE RESISTANCE.
ZKD=YRPHY0%RKDN*YRPHY0%TENTRX/YRPHY0%TENTR

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS

! *
! ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES, CONSTANTES DE SECURITE (POUR
!     TROIS DES INTEGRALES VERTICALES DU SCHEMA).

!     COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME).

ZEPSO=1.E-12_JPRB

! *
! ------------------------------------------------------------------
!     III - MISE A ZERO DE TOUS LES FLUX (POUR LE CAS SANS CONVECTION)

!           SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZFMOD      : FONCTION DE LA VITESE DU COURANT DESCENDANT.

!$acc kernels default (present)
DO JBLK = 1, KGPBLKS
DO JLEV=KTDIA-1,KLEV
DO JLON = KIDIA, KFDIA
  

!      MISE A UN DE LA FONCTION DE MODULATION.
!      SETTING MODULATION FUNCTION TO ONE.

    ZFMOD(JLON,JLEV,JBLK)=1.0_JPRB

  
ENDDO
ENDDO
ENDDO
!$acc end kernels


!$acc kernels default (present)
DO JBLK = 1, KGPBLKS
DO JLEV = 0, KLEV
DO JLON = KIDIA, KFDIA

ZBCC_CD_HALF(JLON,JLEV,JBLK) = 0._JPRB

ENDDO
ENDDO
ENDDO
!$acc end kernels


!$acc kernels default (present)
DO JBLK = 1, KGPBLKS
DO JLEV = 1, KLEV
DO JLON = KIDIA, KFDIA

PBCC_CD_FULL(JLON,JLEV,JBLK) = 0._JPRB

ENDDO
ENDDO
ENDDO
!$acc end kernels



! *
! ------------------------------------------------------------------
!     IV - INITIALISATION A LA BASE. LE PROFIL DU COURANT DESCENDANT
!     SERA FINALEMENT CARACTERISE PAR SON HUMIDITE TOTALE ET SON ENERGIE
!     STATIQUE SECHE DE DETRAINEMENT AINSI QUE PAR UN INDICE DE STABILITE
!     (UN AUX DEUX BOUTS DE TOUTE COUCHE (DE COUCHE DU MODELE A COUCHE DU
!     MODELE ADJACENTE) ET ZERO AILLEURS) ET UN PROFIL NON ENCORE NORME
!     DU FLUX DE MASSE, TABLEAU DANS LEQUEL SERA ENSUITE RANGE LE
!     PRODUIT DU FLUX DE MASSE PAR LE PAS DE TEMPS (HOMOGENE A UNE
!     PRESSION).

!     BOTTOM INITIALISATION. THE DOWNDRAFT PROFILE WILL EVENTUALY BE
!     CHARACTERISED BY ITS TOTAL HUMIDITY AND ITS DETRAINING DRY STATIC
!     ENERGY AS WELL AS BY A STABILITY INDEX (ONE AT BOTH ENDS OF ANY
!     SLAB (FROM MODEL LAYER TO THE ADJACENT ONE) AND ZERO
!     ELSEWHERE) AND A YET UNNORMALIZED MASS FLUX PROFILE, ARRAY IN
!     WHICH THE PRODUCT OF THE MASS FLUX BY THE TIME STEP (QUANTITY
!     HOMOGENEOUS TO A PRESSURE) WILL AFTERWARDS BE WRITEN.


! - TEMPORAIRE(S) 2D (0:KLEV) .

! PFORM     : PROFIL DE FLUX DE MASSE.
!           : PROFILE OF MASS FLUX.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! PSDN       : ENERGIE STATIQUE SECHE DE DETRAINEMENT DU COURANT DESCENDANT.
!            : DETRAINING DRY STATIC ENERGY OF THE DOWNDRAFT.
! PQDN       : HUMIDITE DE DETRAINEMENT DU COURANT DESCENDANT.
!            : DETRAINING DOWNDRAFT HUMIDITY.

!$acc kernels default (present)
DO JBLK = 1, KGPBLKS
DO JLON = KIDIA, KFDIA


!     CARACTERISTIQUES THERMODYNAMIQUES DU COURANT DESCENDANT.
!     THERMODYNAMIC CHARACTERISTICS OF THE DOWNDRAFT.

! - TEMPORAIRE(S) 1D .

! ZTN        : TEMPERATURE DU COURANT DESCENDANT.
!            : TEMPERATURE OF THE DOWNDRAFT.
! ZQN        : HUMIDITE SPECIFIQUE VAPEUR DU COURANT DESCENDANT.
!            : WATER VAPOUR SPECIFIC HUMIDITY OF THE DOWNDRAFT.


  ZTN(JLON,JBLK)=PTW(JLON,KTDIA,JBLK)
  ZQN(JLON,JBLK)=PQW(JLON,KTDIA,JBLK)
  PQN2(JLON,KTDIA,JBLK)=ZQN(JLON,JBLK)
  PSN2(JLON,KTDIA,JBLK)=(RCPD+ZCPVMD*ZQN(JLON,JBLK))*ZTN(JLON,JBLK)+PAPHIF(JLON,KTDIA,JBLK)
  PTN2(JLON,KTDIA,JBLK)=ZTN(JLON,JBLK)

  ZTD=PT(JLON,KTDIA,JBLK)*(1.0_JPRB+(PQLIC(JLON,KTDIA,JBLK)-RETV*(PQSAT(JLON,KTDIA,JBLK)&
   &-PQ(JLON,KTDIA,JBLK)))/(1.0_JPRB+RETV*PLH(JLON,KTDIA,JBLK)*PQSAT(JLON,KTDIA,JBLK)&
   &/(RV*PT(JLON,KTDIA,JBLK))))
  ZTD=YRPHY0%GCVADET*ZTD+(1.0_JPRB-YRPHY0%GCVADET)*ZTN(JLON,JBLK)
  IF (YRPHY%LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF
  ZEW=FOEW(ZTD,ZDELTA)
  ZESP=ZEW/PAPRSF(JLON,KTDIA,JBLK)
  ZQW=FOQS(ZESP)
  ZQW=YRPHY0%GCVADET*ZQW+(1.0_JPRB-YRPHY0%GCVADET)*ZQN(JLON,JBLK)

  PQDN(JLON,KTDIA,JBLK)=ZQW
  PSDN(JLON,KTDIA,JBLK)=(RCPD+ZCPVMD*ZQW)*ZTD+PAPHIF(JLON,KTDIA,JBLK)

!      CARACTERISTIQUES ACTIVES DU COURANT DESCENDANT.
! ACTIVE CHARACTERISTICS OF THE DOWNDRAFT.

! - TEMPORAIRE(S) 1D .

  KNLABD(JLON,KTDIA,JBLK)=0
  PFORM(JLON,KTDIA-1,JBLK)=0.0_JPRB

  PFORM(JLON,KLEV,JBLK)=0.0_JPRB

! INIVBDC    : NIVEAU DE LA BASE DE LA DESCENDANCE CONVECTIVE.
! ZVVER      : VITESSE VERT. DE LA COLONNE CONVECTIVE AUX INTERCOUCHES.
! PEPSI      : ENTRAINEMENT.
! PDELA      : DETRAINEMENT.
! ZVVERFM1   : VITESSE VERT. DE LA COLONNE CONVECTIVE, AU NIVEAU MOINS.
! ZDWNDP     : DERIVEE VERT. DE VITESSE VERT.
! ZCAPE      : CAPE DU NIVEAU.

  INIVBDC(JLON,KTDIA,JBLK)=KTDIA*KNLAB(JLON,KTDIA,JBLK)*MAX(0,-ISIGN(1,-KNCDN(JLON,KTDIA,JBLK)))
  ZVVER(JLON,KTDIA-1,JBLK)=0.0_JPRB
  ZVVER(JLON,KTDIA,JBLK)=0.0_JPRB
  PEPSI(JLON,KTDIA,JBLK)=YRPHY0%TENTRX*PR(JLON,KTDIA,JBLK)*PT(JLON,KTDIA,JBLK)/PAPRSF(JLON,KTDIA,JBLK)
  PDELA(JLON,KTDIA,JBLK)=0.0_JPRB
  ZVVERFM1(JLON,JBLK)=0.0_JPRB
  ZDWNDP(JLON,KTDIA-1,JBLK)=0.0_JPRB
  ZDWNDP(JLON,KTDIA,JBLK)=0.0_JPRB

! -TEMPORAIRES 2D: VALEUR A LA SURFACE:

! PBET       : COEFFICIENT BETA DE LA VITESSE HORIZONTALE
! PUM        : MOYENNE PONDEREE DE LA VITESSE U DEPUIS LA SURFACE
! PVM        : MOYENNE PONDEREE DE LA VITESSE V DEPUIS LA SURFACE
! PTRAM      : MOYENNE PONDEREE DE LA CONCENTRATION DE TRACEUR DEPUIS LA SURFACE

  PBET(JLON,KTDIA,JBLK)=1.0_JPRB
  PUM(JLON,KTDIA,JBLK)=PU(JLON,KTDIA,JBLK)
  PVM(JLON,KTDIA,JBLK)=PV(JLON,KTDIA,JBLK)

  DO JTRA=1,KTRA
    PTRAM(JLON,KTDIA,JTRA,JBLK)=PTRA(JLON,KTDIA,JTRA,JBLK)    
  ENDDO


ENDDO
ENDDO
!$acc end kernels


! *
! ------------------------------------------------------------------
!     V - PREMIERE BOUCLE VERTICALE (VERS LE BAS) INCLUANT LE CALCUL
!     "EN ADIABATIQUE SATUREE" DU PROFIL DU COURANT DESCENDANT.

!     FIRST VERTICAL LOOP (DOWNWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE DOWNDRAFT PROFILE.

!     INITIALISATION DU PARAMETRE QUI SERVIRA EVENTUELLEMENT A EVITER
!     DES CALCULS INUTILES EN HAUT DE L'ATMOSPHERE ET DEBUT DE LA
!     BOUCLE.
!     INITIALIZATION OF THE PARAMETER THAT SHALL EVENTUALLY HELP
!     AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE AND START OF THE VERTICAL LOOP.

ITOP=KTDIA
!$acc kernels default (present)
DO JBLK = 1, KGPBLKS
DO JLEV=KTDIA+1,KLEV
DO JLON = KIDIA, KFDIA

!     ADIABATIQUE SATUREE AVEC ENTRAINEMENT.
!     SATURATED ADIABAT WITH ENTRAINMENT.

  

!     ENTRAINEMENT.
!     ENTRAINEMENT.

! - TEMPORAIRE(S) 1D .

! ZTB        : "ZTN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB        : "ZQN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".

    ZENTR=PEPSI(JLON,JLEV-1,JBLK)*PAPRSF(JLON,JLEV-1,JBLK)/(PR(JLON,JLEV-1,JBLK)&
     &*PT(JLON,JLEV-1,JBLK))
    ZENTR=(ZENTR+YRPHY0%TENTRX)
    ZMIX(JLON,JBLK)=ZENTR*(PAPHIF(JLON,JLEV-1,JBLK)-PAPHIF(JLON,JLEV,JBLK))
    ZMIX(JLON,JBLK)=ZMIX(JLON,JBLK)/(1.0_JPRB+ZMIX(JLON,JBLK))
    ZTB(JLON,JBLK)=ZTN(JLON,JBLK)+ZMIX(JLON,JBLK)*(PT(JLON,JLEV-1,JBLK)-ZTN(JLON,JBLK))
    ZQB(JLON,JBLK)=ZQN(JLON,JBLK)+ZMIX(JLON,JBLK)*(PQ(JLON,JLEV-1,JBLK)-ZQN(JLON,JBLK))

!     ENTRAINEMENT DE LA VITESSE HORIZONTALE:
!     NOUVELLE PARAMETRISATION (KERSHAW & GREGORY).
!     MOMENTUM ENTRAINMENT:
!     NEW PARAMETERIZATION (KERSHAW & GREGORY).

    PBET(JLON,JLEV,JBLK)=PBET(JLON,JLEV-1,JBLK)*(1.0_JPRB-ZMIX(JLON,JBLK))
    PUM(JLON,JLEV,JBLK)=PUM(JLON,JLEV-1,JBLK)&
     &+(ZMIX(JLON,JBLK)*(PU(JLON,JLEV-1,JBLK)-PUM(JLON,JLEV-1,JBLK))&
     &+YRPHY0%TDDGP*(PU(JLON,JLEV,JBLK)-PU(JLON,JLEV-1,JBLK)))&
     &/(1.0_JPRB-PBET(JLON,JLEV,JBLK))
    PVM(JLON,JLEV,JBLK)=PVM(JLON,JLEV-1,JBLK)&
     &+(ZMIX(JLON,JBLK)*(PV(JLON,JLEV-1,JBLK)-PVM(JLON,JLEV-1,JBLK))&
     &+YRPHY0%TDDGP*(PV(JLON,JLEV,JBLK)-PV(JLON,JLEV-1,JBLK)))&
     &/(1.0_JPRB-PBET(JLON,JLEV,JBLK))

    DO JTRA=1,KTRA
      PTRAM(JLON,JLEV,JTRA,JBLK)=PTRAM(JLON,JLEV-1,JTRA,JBLK)&
       &+(ZMIX(JLON,JBLK)*(PTRA(JLON,JLEV-1,JTRA,JBLK)-PTRAM(JLON,JLEV-1,JTRA,JBLK))&
       &+YRPHY0%TDDGP*(PTRA(JLON,JLEV,JTRA,JBLK)-PTRA(JLON,JLEV-1,JTRA,JBLK)))&
       &/(1.0_JPRB-PBET(JLON,JLEV,JBLK))
    ENDDO

!     COEFFICIENTS DE L'HYDROSTATIQUE DU PROFIL COURANT DESCENDANT.
!     HYDROSTATIC COEFFICIENTS FOR THE DOWNDRAFT PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB       : POIDS DE "ZTB" DANS L'EPAISSEUR EN GEOPOTENTIEL.
!            : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH       : POIDS DE "ZTN" A "ZQN=ZQB" DANS LA MEME EPAISSEUR.
!            : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH       : POUR LA CORRECTION DE VARIATION DE Q A ZRBH.
!            : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON,JBLK)=-PALPH(JLON,JLEV-1,JBLK)*(RD+ZRVMD*ZQB(JLON,JBLK))
    ZRBH(JLON,JBLK)=(-PLNPR(JLON,JLEV,JBLK)+PALPH(JLON,JLEV,JBLK))*(RD+ZRVMD*ZQB(JLON,JBLK))
    ZRVH(JLON,JBLK)=(-PLNPR(JLON,JLEV,JBLK)+PALPH(JLON,JLEV,JBLK))*RV

!     DANS LE CALCUL DE L'ADIABATIQUE SATUREE GCVADS PERMET UNE
!     TRANSITION CONTINUE ENTRE LE TRAITEMENT EQUIPRESSION ET
!     EQUIGEOPOTENTIEL.

!     TO COMPUTE ADIABATIC PROFILE GCVADS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON,JBLK)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBB(JLON,JBLK)+YRPHY0%GCVADS*(PAPHIF(JLON,JLEV,JBLK)&
     & -PAPHIF(JLON,JLEV-1,JBLK))/ZTB(JLON,JBLK)  
    ZRBH(JLON,JBLK)=(1.0_JPRB-YRPHY0%GCVADS)*ZRBH(JLON,JBLK)
    ZRVH(JLON,JBLK)=(1.0_JPRB-YRPHY0%GCVADS)*ZRVH(JLON,JBLK)

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (YRPHY%LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON,JBLK)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     APPEL A LA FONCTION DEFINIE POUR LE CALCUL DE LA PSEUDO CHALEUR
!     LATENTE A LA BASE (PARMI TROIS PARAMETRES CARACTERISTIQUES).
!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH        : VALEUR COURANTE DU PSEUDO LH DURANT LA BOUCLE DE NEWTON.
!            : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP        : COMME PZLH MAIS POUR LA CHALEUR MASSIQUE CP.
!            : AS PZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON,JBLK)= FOLH (ZTB(JLON,JBLK),ZDELTA)+ZRVH(JLON,JBLK)*ZTB(JLON,JBLK)
    ZCP(JLON,JBLK)=RCPD+ZCPVMD*ZQB(JLON,JBLK)+ZRBH(JLON,JBLK)
    ZDCP=ZRVH(JLON,JBLK)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     CHANGEMENT DE NIVEAU POUR OBTENIR LA PREMIERE EBAUCHE, POINT DE
!     DEPART DE LA BOUCLE DE NEWTON.
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.

    ZDELT=PT(JLON,JLEV,JBLK)-PT(JLON,JLEV-1,JBLK)
    ZLH(JLON,JBLK)=ZLH(JLON,JBLK)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON,JBLK)+ZRBH(JLON,JBLK))*ZTB(JLON,JBLK)+ZCP(JLON,JBLK)*ZDELT)/ZLH(JLON,JBLK)
    ZCP(JLON,JBLK)=ZCP(JLON,JBLK)+ZDCP*ZDELQ
    ZTN(JLON,JBLK)=ZTB(JLON,JBLK)+ZDELT
    ZQN(JLON,JBLK)=ZQB(JLON,JBLK)+ZDELQ
  

!     BOUCLE DE NEWTON.
!     NEWTON'S LOOP.

  DO JIT=1,YRPHY%NBITER
    

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

      IF (YRPHY%LNEIGE) THEN
        ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON,JBLK)))
      ELSE
        ZDELTA=0.0_JPRB
      ENDIF

!     CALCULS DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (ZTN(JLON,JBLK),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV,JBLK)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON,JBLK),ZDELTA))

!     INCREMENTATIONS.
!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON,JBLK)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON,JBLK))*ZCP(JLON,JBLK)/(ZCP(JLON,JBLK)+ZLH(JLON,JBLK)*ZDQW)
      ZCP(JLON,JBLK)=ZCP(JLON,JBLK)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON,JBLK)/ZCP(JLON,JBLK)
      ZLH(JLON,JBLK)=ZLH(JLON,JBLK)+ZDCP*ZDELT
      ZQN(JLON,JBLK)=ZQN(JLON,JBLK)+ZDELQ
      ZTN(JLON,JBLK)=ZTN(JLON,JBLK)+ZDELT
    
  ENDDO

!     ADIABATIQUE SECHE.

  
    ZCPS=RCPD*(1.0_JPRB-ZQB(JLON,JBLK))+RCPV*ZQB(JLON,JBLK)
    ZTNSEC=(ZCPS-ZRBB(JLON,JBLK))*ZTB(JLON,JBLK)/(ZCPS+ZRBH(JLON,JBLK))
    ICDN=MAX(0,-ISIGN(1,-KNCDN(JLON,JLEV,JBLK)))
    ZTN(JLON,JBLK)=ICDN*ZTN(JLON,JBLK)+(1-ICDN)*ZTNSEC
    ZQN(JLON,JBLK)=ICDN*ZQN(JLON,JBLK)+(1-ICDN)*ZQB(JLON,JBLK)
    ZBCC_CD_HALF(JLON,JLEV,JBLK)=MAX(0._JPRB,ZQN(JLON,JBLK)-ZQB(JLON,JBLK)) &
      &/(PAPRSF(JLON,JLEV-1,JBLK)-PAPRSF(JLON,JLEV,JBLK))
    PBCC_CD_FULL(JLON,JLEV-1,JBLK)=0.5_JPRB*(ZBCC_CD_HALF(JLON,JLEV,JBLK)+ZBCC_CD_HALF(JLON,JLEV-1,JBLK))
  

!     TEST DE STABILITE, RECTIFICATION EVENTUELLE DU NIVEAU SUPERIEUR,
!     CALCUL DU PARAMETRE DE PROFIL DU FLUX DE MASSE ET DES DEUX
!     INTEGRALES INTERVENANT DANS LA CONDITION DE FERMETURE "TYPE KUO".
!     STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
!     COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
!     INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

  
!     TEST DE DECLENCHEMENT.
!     CALCUL DE L'INTEGRALE DE LA FLOTTABILITE
!     DURANT LE PAS DE TEMPS ET TEST DE SON SIGNE.

    IBDC=MAX(0,-ISIGN(1,-INIVBDC(JLON,JLEV-1,JBLK)))
    ZTVN=ZTN(JLON,JBLK)*(1.0_JPRB+RETV*ZQN(JLON,JBLK))
    ZTVE=PT(JLON,JLEV,JBLK)*(1.0_JPRB+RETV*PQ(JLON,JLEV,JBLK))
    ZFLO=(ZTVE-ZTVN)/ZTVE/ZTVE
    ZFLOI=RG*RG*PAPRSF(JLON,JLEV,JBLK)/(RD*ZGAMA)
    ZFLO=ZFLOI*ZFLO

    ZDELPF=PDELP(JLON,JLEV,JBLK)
    ZENTR=PEPSI(JLON,JLEV-1,JBLK)
    ZENTRT=YRPHY0%TENTRX*PR(JLON,JLEV-1,JBLK)*PT(JLON,JLEV-1,JBLK)/PAPRSF(JLON,JLEV-1,JBLK)
    ZENTR=ZENTR+ZENTRT

    ZVVERDEN=2.0_JPRB*(0.25_JPRB*(ZKD+ZENTR)+0.5_JPRB/ZDELPF)
    ZB=0.5_JPRB*(ZKD+ZENTR)*ZVVER(JLON,JLEV-1,JBLK)
    ZDELTAP=ZB**2 &
     &+2.0_JPRB*ZVVERDEN*((ZFLO+(0.5_JPRB/ZDELPF-0.25_JPRB*(ZKD+ZENTR))&
     &*ZVVER(JLON,JLEV-1,JBLK)**2))
    ISDELTAP=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZDELTAP)))
    ZDELTAP=ZDELTAP*ISDELTAP
    ZVVERN=(ZB+SQRT(ZDELTAP))/ZVVERDEN

    ZVVER(JLON,JLEV,JBLK)=ZVVERN
    ZVVERF=0.5_JPRB*(ZVVER(JLON,JLEV,JBLK)+ZVVER(JLON,JLEV-1,JBLK))
    ZDELPF=PAPRSF(JLON,JLEV,JBLK)-PAPRSF(JLON,JLEV-1,JBLK)
    ZDWNDP(JLON,JLEV-1,JBLK)=(ZVVERF-ZVVERFM1(JLON,JBLK))/ZDELPF

    ZVVERFM1(JLON,JBLK)=ZVVERF

!     ENTRAINEMENT-DETRAINEMENT ORGANISES.
    IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERF+0.0_JPRB)))
    IDOMDP=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDWNDP(JLON,JLEV-1,JBLK))))
    ZENTRO=IVVER/MAX(ZEPSO,ZVVERF)*ZDWNDP(JLON,JLEV-1,JBLK)
    PEPSI(JLON,JLEV,JBLK)=IDOMDP*ZENTRO
    PDELA(JLON,JLEV,JBLK)=-(1-IDOMDP)*ZENTRO

    IKUO1=MAX(NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZVVER(JLON,JLEV-1,JBLK)+0.0_JPRB)))&
     &,NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZVVER(JLON,JLEV,JBLK)+0.0_JPRB))))
    ILEVBDC=IBDC*INIVBDC(JLON,JLEV-1,JBLK)+(1-IBDC)*(KLEV+1)
    IKUO2=MAX(0,ISIGN(1,KLEVTEN(JLON,JBLK)-ILEVBDC))
    KNLABD(JLON,JLEV,JBLK)=IKUO1*IKUO2*IBDC

!     VALEURS FINALES DE DETRAINEMENT AVEC APPEL A LA FONCTION DEFINIE
!     POUR REVENIR A LA VERITABLE CHALEUR LATENTE.
!     FINAL DETRAINMENT VALUES WITH A CALL TO THE DEFINED FUNCTION TO
!     COME BACK TO THE TRUE LATENT HEAT.

    ZTD=PT(JLON,JLEV,JBLK)*(1.0_JPRB+(PQLIC(JLON,JLEV,JBLK)-RETV*(PQSAT(JLON,JLEV,JBLK)&
     &-PQ(JLON,JLEV,JBLK)))/(1.0_JPRB+RETV*PLH(JLON,JLEV,JBLK)*PQSAT(JLON,JLEV,JBLK)&
     &/(RV*PT(JLON,JLEV,JBLK))))
    IF (YRPHY%LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF
    ZEW=FOEW (ZTD,ZDELTA)
    ZESP=ZEW/PAPRSF(JLON,JLEV,JBLK)
    ZQW=FOQS(ZESP)
    ICDN=MAX(0,-ISIGN(1,-KNCDN(JLON,JLEV,JBLK)))
    ZTD=ICDN*(YRPHY0%GCVADET*ZTD+(1.0_JPRB-YRPHY0%GCVADET)*ZTN(JLON,JBLK))+(1-ICDN)*ZTN(JLON,JBLK)
    ZQW=ICDN*(YRPHY0%GCVADET*ZQW+(1.0_JPRB-YRPHY0%GCVADET)*ZQN(JLON,JBLK))+(1-ICDN)*ZQN(JLON,JBLK)
    IF (YRPHY%LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

    PQDN(JLON,JLEV,JBLK)=ZQW
    PSDN(JLON,JLEV,JBLK)=(RCPD+ZCPVMD*ZQW)*ZTD+PAPHIF(JLON,JLEV,JBLK)
    PQN2(JLON,JLEV,JBLK)=ZQN(JLON,JBLK)
    PSN2(JLON,JLEV,JBLK)=(RCPD+ZCPVMD*ZQN(JLON,JBLK))*ZTN(JLON,JBLK)+PAPHIF(JLON,JLEV,JBLK)
    PTN2(JLON,JLEV,JBLK)=ZTN(JLON,JBLK)

!     EN CAS DE COURANT DESCENDANT PLUS "CHAUD" QUE L'ENVIRONEMENT ON
!     REEGALISE  TEMPERATURE ET HUMIDITE SPECIFIQUES DU COURANT
!     DESCENDANT A CELLES DU THERMOMETRE MOUILLE DE L'ENVIRONEMENT.
!     IN CASE OF A "WARMER" DOWNDRAFT THAN THE ENVIRONMENT ONE SETS
!     BACK TEMPERATURE AND SPECIFIC HUMIDITY OF THE DOWNDRAFT TO
!     THAT OF THE WET BULB CHARACTERISTICS OF THE ENVIRONMENT.

    INUA=KNLABD(JLON,JLEV,JBLK)

    ICDN=MAX(0,-ISIGN(1,-KNCDN(JLON,JLEV-1,JBLK)))
    INIVBDC(JLON,JLEV,JBLK)=MIN(INIVBDC(JLON,JLEV-1,JBLK),JLEV)*KNLAB(JLON,JLEV-1,JBLK)*ICDN
    INIVBDC(JLON,JLEV,JBLK)=INUA*INIVBDC(JLON,JLEV,JBLK)+(1-INUA)*JLEV
    INIVBDC(JLON,JLEV-1,JBLK)=INUA*INIVBDC(JLON,JLEV-1,JBLK)

    ZVVER(JLON,JLEV,JBLK)=INUA*ZVVER(JLON,JLEV,JBLK)

    ZTN(JLON,JBLK)=INUA*ZTN(JLON,JBLK)+(1-INUA)*PTW(JLON,JLEV,JBLK)
    ZQN(JLON,JBLK)=INUA*ZQN(JLON,JBLK)+(1-INUA)*PQW(JLON,JLEV,JBLK)

!     PROFIL DE FLUX DE MASSE.
!     MASS FLUX PROFILE.

    PFORM(JLON,JLEV-1,JBLK)=KNLABD(JLON,JLEV,JBLK)*ZVVER(JLON,JLEV-1,JBLK)

    ZTEST=MAX(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PFORM(JLON,JLEV-1,JBLK)+0.0_JPRB))&
     &,MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZFMOD(JLON,JLEV-2,JBLK)-1.0_JPRB+0.0_JPRB)))
    ZFMOD(JLON,JLEV-1,JBLK)=(1.0_JPRB-ZTEST)+ZTEST*ZFMOD(JLON,JLEV-2,JBLK)&
     &*((PAPRS(JLON,KLEV,JBLK)-PAPRS(JLON,JLEV-1,JBLK))&
     &/(PAPRS(JLON,KLEV,JBLK)-PAPRS(JLON,JLEV-2,JBLK)))&
     &**YRPHY0%GDDSDE
    PFORM(JLON,JLEV-1,JBLK)=PFORM(JLON,JLEV-1,JBLK)*ZFMOD(JLON,JLEV-1,JBLK)

    ZFMOD(JLON,JLEV-1,JBLK)=INUA*ZFMOD(JLON,JLEV-1,JBLK)+(1-INUA)*1.0_JPRB

  

!     VERIFICATION DE LA PRESENCE D'AU MOINS UN COURANT DESCENDANT LE
!     LONG DU VECTEUR "JLON" ET MODIFICATION SI NECESSAIRE DE ITOP.
!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE DOWNDRAFT ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  
    ISUM=ISUM+KNLABD(JLON,JLEV,JBLK)
  

  IF ((ISUM == 0).AND.(ITOP == JLEV-1)) THEN
    ITOP=JLEV
  ENDIF

ENDDO
ENDDO
ENDDO
!$acc end kernels



!$acc end data
!$acc end data
!$acc end data
!$acc end data
!$acc end data
!$acc end data
END SUBROUTINE ACMTDDD
